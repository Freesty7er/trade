
#Область ОбработчикиСобытий

Процедура ОбработкаЗагрузки(КлючОбъекта, КлючНастроек, Настройки, ОписаниеНастроек, Пользователь)
	
	Если КлючНастроек <> Неопределено Тогда
	
		Настройки = КлючНастроек.ХранилищеНастроек.Получить();	// Ключом настроек является ссылка элемента справочника "Настройки отчётов".
		
		Если ОписаниеНастроек <> Неопределено Тогда
			ОписаниеНастроек.Представление = КлючНастроек.Наименование;
		КонецЕсли; 
	
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПолученияОписания(КлючОбъекта, КлючНастроек, ОписаниеНастроек, Пользователь)
	
	ОписаниеНастроек.КлючОбъекта = КлючОбъекта;
	ОписаниеНастроек.КлючНастроек = КлючНастроек;
	
	Если ЗначениеЗаполнено(КлючНастроек) Тогда
		ОписаниеНастроек.Представление = КлючНастроек.Наименование;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаСохранения(КлючОбъекта, КлючНастроек, Настройки, ОписаниеНастроек, Пользователь)
	
	// Ключом настроек является ссылка элемента справочника "Настройки отчётов".
	Если ЗначениеЗаполнено(КлючНастроек) Тогда
		
		НастройкиОтчётовОбъект = КлючНастроек.ПолучитьОбъект();
		НастройкиОтчётовОбъект.ХранилищеНастроек = Новый ХранилищеЗначения(Настройки, Новый СжатиеДанных);
		НастройкиОтчётовОбъект.Пользователь = ПараметрыСеанса.ТекущийПользователь;
		
		Если ОписаниеНастроек <> Неопределено Тогда
			НастройкиОтчётовОбъект.Наименование = ОписаниеНастроек.Представление;
		КонецЕсли; 
		
		НастройкиОтчётовОбъект.Записать();
	
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаУстановкиОписания(КлючОбъекта, КлючНастроек, ОписаниеНастроек, Пользователь)
	
	ОписаниеНастроек.КлючОбъекта = КлючОбъекта;
	ОписаниеНастроек.КлючНастроек = КлючНастроек;
	
	Если КлючНастроек = Неопределено Тогда
	
		НастройкиОтчётовОбъект = Справочники.НастройкиОтчётов.СоздатьЭлемент();
		НастройкиОтчётовОбъект.КлючОбъекта = КлючОбъекта;
		НастройкиОтчётовОбъект.Пользователь = ПараметрыСеанса.ТекущийПользователь;
		
		ОписаниеНастроек.КлючНастроек = НастройкиОтчётовОбъект.Ссылка;
	
	Иначе
		НастройкиОтчётовОбъект = КлючНастроек.ПолучитьОбъект();
	КонецЕсли; 
	
	НастройкиОтчётовОбъект.Наименование = ОписаниеНастроек.Представление;
	НастройкиОтчётовОбъект.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область ДополнительныеПроцедурыИФункции

// Удаляет настроки отчёта из информационной базы.
//
// Параметры
//  мНастройкиОтчёта  - Массив - Набор значений типа СправочникСсылка.НастройкиОтчётов, являющиеся удаляемыми
//                 настройками отчёта.
//
Процедура УдалитьНастройкиОтчёта(мНастройкиОтчёта) Экспорт

	Для каждого НастройкаОтчёта Из мНастройкиОтчёта Цикл
		НастройкаОтчёта.ПолучитьОбъект().Удалить();
	КонецЦикла; 

КонецПроцедуры // УдалитьНастройкиОтчёта()

#КонецОбласти
