
#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИмяМакета = Параметры.ИмяМакета;
	ПараметрыОбъектовПечати = Параметры.ПараметрыОбъектовПечати;
	мПропущенныеОбъекты = Параметры.ПропущенныеОбъекты;
	
	Элементы.Печать.Доступность = ПравоДоступа("Вывод", Метаданные);
	
	ОбразецОбъектаПечатиМетаданные = Неопределено;
	
	// Формируем печатные формы.
	Если ПараметрыОбъектовПечати.Количество() > 0 Тогда
		
		ОбразецОбъектаПечатиМетаданные = УправлениеПечатьюСервер.ПолучитьОбразецОбъектаПечати(ПараметрыОбъектовПечати).Метаданные();
		
		// Зададим заголовок формы как представление печатаемых объектов.
		Заголовок = ОбразецОбъектаПечатиМетаданные.Представление();
		
		// Формируем печатные формы.
		УправлениеПечатьюСервер.СформироватьПечатныеФормы(ПараметрыОбъектовПечати, ИмяМакета, КоллекцияПечатныхФорм, мПропущенныеОбъекты, РезультатТабличныйДокумент, Параметры.ДополнительныеПараметры);
		
	КонецЕсли; 
	
	// Пропущенные объекты.
	тзПропущенныеОбъекты = РеквизитФормыВЗначение("ПропущенныеОбъекты");
	Для каждого СтрокаПропущенныеОбъекты Из мПропущенныеОбъекты Цикл
		ЗаполнитьЗначенияСвойств(тзПропущенныеОбъекты.Добавить(), СтрокаПропущенныеОбъекты);
	КонецЦикла; 
	ЗначениеВРеквизитФормы(тзПропущенныеОбъекты, "ПропущенныеОбъекты");
	
	// Скроем ненужные в данном сеансе реквизиты формы.
	КоличествоПечатныхФорм = КоллекцияПечатныхФорм.Количество();
	Если КоличествоПечатныхФорм <= 1 Тогда
	
		Элементы.КоллекцияПечатныхФорм.Видимость = Ложь;
		
		Если КоличествоПечатныхФорм = 0 Тогда
			
			Элементы.РезультатТабличныйДокумент.Видимость = Ложь;
			Элементы.ГруппаПечать.Доступность = Ложь;
			
		Иначе
			Элементы.РезультатТабличныйДокумент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		КонецЕсли;
	
	КонецЕсли; 
	Если КоличествоПечатныхФорм > 0 Тогда
		
		СуфиксИмениОбъекта = ОбразецОбъектаПечатиМетаданные.Имя + "." + ИмяМакета;
		
		РезультатТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати." + СуфиксИмениОбъекта;
		КлючСохраненияПоложенияОкна = "СохранениеПоложенияОкна." + СуфиксИмениОбъекта;
		
	КонецЕсли; 
	
	Элементы.ГруппаПропущенныеОбъекты.Видимость = ПропущенныеОбъекты.Количество() > 0;
	РедактированиеТаблиц = Не ПроверкаДанныхСервер.ПроверитьДоступностьРоли("РедактированиеТаблиц");
	Элементы.РезультатТабличныйДокумент.Защита = РедактированиеТаблиц;
	
	КоличествоЭкземпляров = 1;
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоЭкземпляровПриИзменении(Элемент)
	
	РезультатТабличныйДокумент.КоличествоЭкземпляров = КоличествоЭкземпляров;
	
КонецПроцедуры

&НаКлиенте
Процедура КоллекцияПечатныхФормПриАктивизацииСтроки(Элемент)
	
	Если КоллекцияПечатныхФорм.Количество() > 0 Тогда
		Элементы.РезультатТабличныйДокумент.ТекущаяОбласть = РезультатТабличныйДокумент.Области.Найти(Элемент.ТекущиеДанные.Значение);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПропущенныеОбъектыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьФормуОбъекта();
КонецПроцедуры

#КонецОбласти 

#Область ДействияКоманд

&НаКлиенте
Процедура Печать(Команда)
	
	РезультатТабличныйДокумент.Напечатать();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьПропущенныйОбъект(Команда)
	ОткрытьФормуОбъекта();
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьПропущенныхОбъектов(Команда)
	
	Элементы.ПропущенныеОбъекты.Видимость = Не Элементы.ПропущенныеОбъекты.Видимость;
	Элементы.ИзменитьВидимостьПропущенныхОбъектов.Картинка = ?(Элементы.ПропущенныеОбъекты.Видимость, БиблиотекаКартинок.ПереместитьВниз, БиблиотекаКартинок.ПереместитьВверх);
	
КонецПроцедуры

#КонецОбласти 

#Область ДополнительныеПроцедурыИФункции

// Открывает форму текущего объекта из списка пропущенных объектов.
//
&НаКлиенте
Процедура ОткрытьФормуОбъекта()

	ТекущийДокумент = Элементы.ПропущенныеОбъекты.ТекущиеДанные.Объект;
	Если ТекущийРежимЗапуска() = РежимЗапускаКлиентскогоПриложения.УправляемоеПриложение Тогда
		ПерейтиПоНавигационнойСсылке(ПолучитьНавигационнуюСсылку(ТекущийДокумент));
	Иначе
		ТекущийДокумент.ПолучитьФорму().Открыть();
	КонецЕсли; 

КонецПроцедуры // ОткрытьФормуОбъекта()

#КонецОбласти 
