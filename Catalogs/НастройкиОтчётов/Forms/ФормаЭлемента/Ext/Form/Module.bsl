
#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидыОбъектовДоступа.Добавить("Все", НСтр("ru = '<Все>'"));
	ВидыОбъектовДоступа.Добавить("Пользователи", Команды.ПодборПользователи.Заголовок);
	Если ПравоДоступа("Просмотр", Метаданные.Справочники.ГруппыПользователей) Тогда
		ВидыОбъектовДоступа.Добавить("ГруппыПользователей", Команды.ПодборГруппыПользователей.Заголовок);
	КонецЕсли; 
	
	Если Параметры.Ключ.Пустая() Тогда
		ЗаполнитьИндексыКартинокОбъектовДоступа(Объект.ОбщийДоступ);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ЗаполнитьИндексыКартинокОбъектовДоступа(Объект.ОбщийДоступ);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ЗаполнитьИндексыКартинокОбъектовДоступа(Объект.ОбщийДоступ);
КонецПроцедуры

&НаКлиенте
Процедура ОбщийДоступПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьВыборВидаОбъектаДоступа", ЭтотОбъект);
	ВидыОбъектовДоступа.ПоказатьВыборЭлемента(ОписаниеОповещения, НСтр("ru = 'Объекты доступа'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщийДоступОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьВыборОбъектаДоступаПриПодбореПеретаскивании", ЭтотОбъект);
	ПодборИПеретаскивание.ОбработатьПодбор(ВыбранноеЗначение, Объект.ОбщийДоступ, "ОбъектДоступа", Элементы.ОбщийДоступОбъектДоступа, ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти 

#Область ДействияКоманд

&НаКлиенте
Процедура ПодборПользователи(Команда)
	
	стПараметрыПодбора = Новый Структура;
	стПараметрыПодбора.Вставить("ИмяОбъекта", "Справочник.Пользователи");
	стПараметрыПодбора.Вставить("ПолеТаблицы", Элементы.ОбщийДоступОбъектДоступа);
	
	ПодборИПеретаскивание.ОткрытьПодбор(стПараметрыПодбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборГруппыПользователей(Команда)
	
	стПараметрыПодбора = Новый Структура;
	стПараметрыПодбора.Вставить("ИмяОбъекта", "Справочник.ГруппыПользователей");
	стПараметрыПодбора.Вставить("ПолеТаблицы", Элементы.ОбщийДоступОбъектДоступа);
	стПараметрыПодбора.Вставить("ДополнительныеПараметрыОтбора", ПолучитьПараметрыОтбораГруппПользователейОбщегоДоступа());
	
	ПодборИПеретаскивание.ОткрытьПодбор(стПараметрыПодбора);
	
КонецПроцедуры

#КонецОбласти 

#Область ДополнительныеПроцедурыИФункции

#Область ОбработкаДанныхСтрок

// Процедура обрабатывает изменение значения поля "ОбъектДоступа" таблицы "ОбщийДоступ".
//
// Параметры:
//  ДанныеСтрок  - ДанныеФормыЭлементКоллекции, Массив - Одна строка или набор строк,
//                 для которых выполняется обработка данных.
//
&НаКлиенте
Процедура ОбработатьДанныеСтрокПриИзмененииОбъектаДоступа(ДанныеСтрок)
	
	мДанныеСтрок = ОбщегоНазначенияКлиентСервер.ПреобразоватьЗначениеВМассив(ДанныеСтрок);
	ЗаполнитьИндексыКартинокОбъектовДоступа(мДанныеСтрок);	
	
КонецПроцедуры // ОбработатьДанныеСтрокПриИзмененииОбъектаДоступа()

// Заполняет индексы картинок во всех строках таблицы "ОбщийДоступ".
//
// Параметры:
//  Коллекция  - ДанныеФормыКоллекция, Массив - Коллекция данных типа ДанныеФормыКоллекция или массив элементов
//                 типа ДанныеФормыЭлементКоллекции. Для всех элементов коллекции будет заполнен индекс картинки.
//
&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьИндексыКартинокОбъектовДоступа(Коллекция)

	Для каждого СтрокаКоллекция Из Коллекция Цикл
		СтрокаКоллекция.ИндексКартинки = ПолучитьИндексКартинкиОбъектаДоступа(СтрокаКоллекция.ОбъектДоступа);
	КонецЦикла; 

КонецПроцедуры // ЗаполнитьИндексыКартинокОбъектовДоступа()

#КонецОбласти 

#Область ПодборИПеретаскивание

// Процедура обратного вызова для обработки выбора номенклатуры в табличной части
// "ОбщийДоступ" при подборе или перетаскивании.
//
// Параметры:
//  ДанныеСтрок  - Массив - Набор элементов типа ДанныеФормыЭлементКоллекции, представляющих
//                 собой добавленные в табличную часть строки.
//  ДополнительныеПараметры  - Неопределено - Не используется.
//
&НаКлиенте
Процедура ОбработатьВыборОбъектаДоступаПриПодбореПеретаскивании(ДанныеСтрок, ДополнительныеПараметры) Экспорт

	ОбработатьДанныеСтрокПриИзмененииОбъектаДоступа(ДанныеСтрок);

КонецПроцедуры // ОбработатьВыборОбъектаДоступаПриПодбореПеретаскивании()

#КонецОбласти 

// Возвращает индекс картинки объекта доступа в зависимости от типа этого объекта.
//
// Параметры:
//  ОбъектДоступа  - Булево, СправочникСсылка.Пользователи, СправочникСсылка.ГруппыПользователей - Объект
//                 доступа, для которого определяется индекс картинки.
//
// Возвращаемое значение:
//   Число   - Индекс картинки.
//
&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьИндексКартинкиОбъектаДоступа(ОбъектДоступа)

	ОбъектДоступаТипЗнч = ТипЗнч(ОбъектДоступа);
	
	ИндексКартинки = 0;	
	Если ОбъектДоступаТипЗнч = Тип("Булево") Тогда
		ИндексКартинки = 2;
	ИначеЕсли ОбъектДоступаТипЗнч = Тип("СправочникСсылка.Пользователи") Тогда
		ИндексКартинки = 1;
	ИначеЕсли ОбъектДоступаТипЗнч = Тип("СправочникСсылка.ГруппыПользователей") Тогда
		ИндексКартинки = 0;
	КонецЕсли; 
	
	Возврат ИндексКартинки;
	
КонецФункции // ПолучитьИндексКартинкиОбъектаДоступа()

// Процедура добавляет выбранный объект доступа в список объектов.
//
// Параметры:
//  ОбъектДоступа  - Булево, СправочникСсылка.Пользователи, СправочникСсылка.ГруппыПользователей -
//                 выбранный объект доступа.
//
&НаКлиенте
Процедура ДобавитьОбъектОбщегоДоступа(ОбъектДоступа)
	
	Если ОбъектДоступа <> Неопределено Тогда
		
		мСтроки = Объект.ОбщийДоступ.НайтиСтроки(Новый Структура("ОбъектДоступа", ОбъектДоступа));
		Если мСтроки.Количество() = 0 Тогда
			
			НоваяСтрока = Объект.ОбщийДоступ.Добавить();
			НоваяСтрока.ОбъектДоступа = ОбъектДоступа;
			ОбработатьДанныеСтрокПриИзмененииОбъектаДоступа(НоваяСтрока);
			
			Модифицированность = Истина;
			
		Иначе
			Элементы.ОбщийДоступ.ТекущаяСтрока = мСтроки[0].ПолучитьИдентификатор();
		КонецЕсли; 
		
	КонецЕсли; 

КонецПроцедуры // ДобавитьОбъектОбщегоДоступа()

// Процедура обратного вызова для обработки результата выбора вида объекта доступа.
//
// Параметры:
//  ВыбранныйЭлемент  - Строка - Выбранный вид объектов доступа.
//  ДополнительныеПараметры  - Произвольный - Не используется.
//
&НаКлиенте
Процедура ОбработатьВыборВидаОбъектаДоступа(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт

	Если ВыбранныйЭлемент <> Неопределено Тогда
	
		ВидОбъектовДоступа = ВыбранныйЭлемент.Значение;
		ОбъектДоступа = Неопределено;
		
		Если ВидОбъектовДоступа = "Все" Тогда
			ДобавитьОбъектОбщегоДоступа(Истина);
		Иначе
			
			стПараметры = Новый Структура;
			Если ВидОбъектовДоступа = "ГруппыПользователей" Тогда
				стПараметры.Вставить("Отбор", ПолучитьПараметрыОтбораГруппПользователейОбщегоДоступа());
			КонецЕсли; 
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьВыборОбъектаДоступа", ЭтотОбъект);
			ОткрытьФорму("Справочник." + ВидОбъектовДоступа + ".ФормаВыбора", стПараметры,,,,, ОписаниеОповещения);
		
		КонецЕсли;
	
	КонецЕсли; 

КонецПроцедуры // ОбработатьВыборВидаОбъектаДоступа()

// Процедура обратного вызова для обработки результата выбора объекта доступа.
//
// Параметры:
//  ОбъектДоступа  - Неопределено, Булево, СправочникСсылка.Пользователи,
//                 СправочникСсылка.ГруппыПользователей -выбранный объект доступа.
//                 Если пользователь отказался от выбора будет передано значение Неопределено.
//  ДополнительныеПараметры  - Произвольный - Не используется.
//
&НаКлиенте
Процедура ОбработатьВыборОбъектаДоступа(ОбъектДоступа, ДополнительныеПараметры) Экспорт

	Если ОбъектДоступа <> Неопределено Тогда
		ДобавитьОбъектОбщегоДоступа(ОбъектДоступа);
	КонецЕсли; 

КонецПроцедуры // ОбработатьВыборОбъектаДоступа()

// Возвращает параметры отбора для выбора групп пользователей в качестве объектов общего доступа.
//
// Возвращаемое значение:
//   Структура   - Параметры отбора. Содержит следующие свойства:
//                 Предопределенный  - Булево - Признак отбора предопределённых элементов.
//
&НаКлиенте
Функция ПолучитьПараметрыОтбораГруппПользователейОбщегоДоступа()

	Возврат Новый Структура("Предопределенный", Ложь);

КонецФункции // ПолучитьПараметрыОтбораГруппПользователейОбщегоДоступа()

#КонецОбласти 
