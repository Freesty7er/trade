////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ СОХРАНЕНИЯ НАСТРОЕК

// Процедура сохраняет выбранный элемент в настройки.
//
&НаСервере
Процедура УстановитьОсновнойЭлемент()
	
	Если Объект.Ссылка <> УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("СостояниеНовогоЗаказаПокупателя") Тогда
		УправлениеНебольшойФирмойСервер.УстановитьНастройкуПользователя(Объект.Ссылка, "СостояниеНовогоЗаказаПокупателя");	
		Элементы.ФормаКомандаУстановитьОсновнойЭлемент.Заголовок = "Используется при создании новых заказов";
		Элементы.ФормаКомандаУстановитьОсновнойЭлемент.Доступность = Ложь;
	КонецЕсли; 
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура ПриСозданииНаСервере
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Цвет = Объект.Ссылка.Цвет.Получить();
		Если Объект.Ссылка = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("СостояниеНовогоЗаказаПокупателя") Тогда
			Элементы.ФормаКомандаУстановитьОсновнойЭлемент.Заголовок = "Используется при создании новых заказов";
			Элементы.ФормаКомандаУстановитьОсновнойЭлемент.Доступность = Ложь;
		КонецЕсли; 
	Иначе
		ЗначениеКопирования = Неопределено;
		Параметры.Свойство("ЗначениеКопирования", ЗначениеКопирования);
		Если ЗначениеКопирования <> Неопределено Тогда
			Цвет = ЗначениеКопирования.Цвет.Получить();
		Иначе
			Цвет = Новый Цвет(0, 0, 0);	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

// Процедура ПередЗаписьюНаСервере
//
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.Цвет = Новый ХранилищеЗначения(Цвет);
	
КонецПроцедуры // ПередЗаписьюНаСервере()

// Процедура - обработчик выполнения команды УстановитьОсновнойЭлемент.
//
&НаКлиенте
Процедура КомандаУстановитьОсновнойЭлемент(Команда)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		УстановитьОсновнойЭлемент();
		Оповестить("ИзмененыНастройкиПользователя");
	Иначе
		Предупреждение(НСтр("ru='Элемент необходимо вначале записать!'"));
	КонецЕсли;
	
КонецПроцедуры // КомандаУстановитьОсновнойЭлемент()

// Процедура ПередЗаписью
//
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_СостоянияЗаказовПокупателей");
	
КонецПроцедуры // ПослеЗаписи()

