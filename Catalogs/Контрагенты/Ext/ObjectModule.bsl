
#Область ОбработчикиСобытий

Процедура ПередЗаписью(отказ)
	
	Если Не ОбменДанными.Загрузка Тогда
		
		Если Не ЭтоГруппа Тогда
			
			Если Поставщик И НЕ РольДоступна("ИзменениеПоставщиков") Тогда
				
				Отказ = Истина;
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = НСтр("ru = 'Не достаточно прав для изменения Поставщиков'");
				Сообщение.Поле = "Поставщик";
				Сообщение.УстановитьДанные(ЭтотОбъект);
				Сообщение.Сообщить();
				
			КонецЕсли;
			
			Для Каждого строкаМенеджеры Из Менеджеры Цикл
				
				Если ПустаяСтрока(строкаМенеджеры.ИД) Тогда
					строкаМенеджеры.ИД = Новый УникальныйИдентификатор();
				КонецЕсли;
				
				Если Найти(строкаМенеджеры.Представление, СокрЛП(КД_Контрагент)) = 0 Тогда
					строкаМенеджеры.Представление = СтрШаблон("%1 (%2)",СокрЛП(КД_Контрагент), СокрЛП(строкаМенеджеры.ГруппаПродукта));
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(отказ, проверяемыеРеквизиты)
	
	Если Не ЭтоГруппа Тогда
		
		Если Покупатель И Не ЭтоСотрудник Тогда
			
			проверяемыеРеквизиты.Добавить("КД_Контрагент");
			проверяемыеРеквизиты.Добавить("КД_ЮридическоеЛицо");
			проверяемыеРеквизиты.Добавить("КД_КаналСбыта");
			проверяемыеРеквизиты.Добавить("КД_Состояние");
			проверяемыеРеквизиты.Добавить("КД_ТипСтандартаПрисутствия");
			проверяемыеРеквизиты.Добавить("КД_Категория");
			проверяемыеРеквизиты.Добавить("ВалютаРасчетов");
			
			ОбработкаТабличныхЧастейСервер.ПроверитьДублиСтрокТабличнойЧасти(Отказ, ЭтотОбъект, "Менеджеры", Новый Структура("Менеджер", "Менеджер"));
			ОбработкаТабличныхЧастейСервер.ПроверитьДублиСтрокТабличнойЧасти(Отказ, ЭтотОбъект, "Менеджеры", Новый Структура("ГруппаПродукта", "ГруппаПродукта"));
			ОбработкаТабличныхЧастейСервер.ПроверитьДублиСтрокТабличнойЧасти(Отказ, ЭтотОбъект, "Менеджеры", Новый Структура("ОсновнаяОрганизация", "ОсновнаяОрганизация"));
			
		КонецЕсли;
		
		Если Поставщик Тогда
			проверяемыеРеквизиты.Добавить("ТипЦен");	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(объектКопирования)
	
	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеСкопированногоСправочника(ЭтотОбъект, объектКопирования);
	
	// очистим идентификаторы по строке "менеджеры"
	таблицаМенеджеры = Менеджеры.Выгрузить();
	таблицаМенеджеры.ЗаполнитьЗначения("", "ИД");
	
	Менеджеры.Загрузить(таблицаМенеджеры);
	
КонецПроцедуры

#КонецОбласти

// Функция - Получить список менеджеров
// 
// Возвращаемое значение:
//   - (массив)
//
Функция ПолучитьСписокМенеджеров() Экспорт
	
	результат = Новый Массив;
	
	Для каждого строкаМенеджеры Из Менеджеры Цикл
	
		результат.Добавить(строкаМенеджеры.Менеджер);
	
	КонецЦикла; 
	
	Если результат.Количество() = 0 Тогда
		результат.Добавить(Справочники.Менеджеры.БезМенеджера);
	КонецЕсли;
	
	Возврат результат;
	
КонецФункции	// ПолучитьСписокМенеджеров()