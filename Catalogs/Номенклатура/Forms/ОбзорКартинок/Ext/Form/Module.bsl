
#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(отказ, стандартнаяОбработка)
	
	Номенклатура = Параметры.Номенклатура;
	КаталогКартинок = БПАГ.БПАГПолучитьНастройку("1СКаталогОбмена") + "Images\";
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураКартинкиПриАктивизацииСтроки(элемент)
	
	ПодключитьОбработчикОжидания("ПоказатьКартинку", 0.2, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ДополнительныеПроцедурыИФункции

// Обработчик ожидания для показа выбранной картинки из списка картинок.
//
&НаКлиенте
Процедура ПоказатьКартинку()

	текущиеДанные = Элементы.НоменклатураКартинки.ТекущиеДанные;
	Если текущиеДанные = Неопределено Тогда
		АдресКартинки = "";
	Иначе
		ПоместитьКартинкуВоВременноеХранилище(текущиеДанные.Имя);
	КонецЕсли; 

КонецПроцедуры // ПоказатьКартинку()

// Помещает картинку с указанным именем во временное хранилище для показа на форме.
// Если файл не существует, будет показана пустая картинка.
//
// Параметры:
//  имяФайла  - Строка - Имя файла картинки, выбранное в списке.
//
&НаСервере
Процедура ПоместитьКартинкуВоВременноеХранилище(имяФайла)

	Если ЭтоАдресВременногоХранилища(АдресКартинки) Тогда
		УдалитьИзВременногоХранилища(АдресКартинки);
	КонецЕсли; 
	
	полноеИмяФайла = КаталогКартинок + имяФайла;
	
	файл = Новый Файл(полноеИмяФайла);
	Если файл.Существует() Тогда
		АдресКартинки = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(полноеИмяФайла), УникальныйИдентификатор);
	Иначе
		АдресКартинки = "";
	КонецЕсли; 

КонецПроцедуры // ПоместитьКартинкуВоВременноеХранилище()

#КонецОбласти