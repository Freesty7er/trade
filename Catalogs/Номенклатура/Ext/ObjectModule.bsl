
#Область ОбработчикиСобытий

Процедура ПередЗаписью(отказ)
	
	Если  Не ОбменДанными.Загрузка Тогда
		
		Если Не ЭтоГруппа Тогда
			
			Если ЭтоНовый() Тогда
				ДатаРегистрации = ТекущаяДата();
			КонецЕсли;
			
			Поставщик 	= Родитель.Поставщик;
			СчетУчета	= Родитель.СчетУчета;
			
			НеАктуально = Не КД_Активность;
			
			//х.з. в чем разница между весовым товаром и делимым для РесПОСа
			//Р_Делимый	= ЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.Кг;
			Если ЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.Шт Тогда
				
				Если Не Цел(БПАГКратностьУпаковки) = БПАГКратностьУпаковки Тогда
					БПАГКратностьУпаковки = Цел(БПАГКратностьУпаковки);
				КонецЕсли;
				
				Если БПАГКратностьУпаковки = 0 Тогда
					БПАГКратностьУпаковки = 1;
				КонецЕсли;
				
			КонецЕсли;
		Иначе
			
			Если Не СчетУчета = Ссылка.СчетУчета Или Не Поставщик = Ссылка.Поставщик Тогда
				ДополнительныеСвойства.Вставить("ФоновоеОбновлениеПодчиненныхЭлементов", Истина);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры	// ПередЗаписью()

Процедура ОбработкаПроверкиЗаполнения(отказ, проверяемыеРеквизиты)
	
	Если  ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если СчетУчета = ПланыСчетов.Внутренний.Товары Тогда
		
		проверяемыеРеквизиты.Добавить("КД_Группа");
		
		Если Не ЭтоГруппа Тогда
			
			проверяемыеРеквизиты.Добавить("КД_Вид");
			проверяемыеРеквизиты.Добавить("КД_Тип");
			//проверяемыеРеквизиты.Добавить("БПАГКратностьУпаковки");
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		ПроверяемыеРеквизиты.Добавить("СчетУчета");
	КонецЕсли;
	
КонецПроцедуры	// ОбработкаПроверкиЗаполнения()

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеНовогоСправочника(ЭтотОбъект, ДанныеЗаполнения);
	
	Если СчетУчета.Пустая() Тогда
		СчетУчета	= Родитель.СчетУчета;	
	КонецЕсли;
	
	Если Не ЭтоГруппа Тогда
		// Заполнение по умолчанию
		Если Вес = 0 Тогда
			Вес = 1;
		КонецЕсли;
		
		Если ЕдиницаИзмерения.Пустая() Тогда
			
			Если Родитель.СчетУчета = ПланыСчетов.Внутренний.Товары Тогда
				ЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.Кг;
			Иначе
				ЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.Шт;
			КонецЕсли;
			
		КонецЕсли;
		
		КД_Активность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры	// ОбработкаЗаполнения()

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	фоновоеОбновлениеПодчиненныхЭлементов = Неопределено;
	
	Если ДополнительныеСвойства.Свойство("ФоновоеОбновлениеПодчиненныхЭлементов", фоновоеОбновлениеПодчиненныхЭлементов)
		И фоновоеОбновлениеПодчиненныхЭлементов Тогда
		
		//СправочникНоменклатураСервер.ФоноваяПроцедураОбновленияРеквизитовПодчиненныхЭлементовСправочника(Ссылка);
		
		параметрыФоновогоЗадания = Новый Массив;
		параметрыФоновогоЗадания.Добавить(Ссылка); 
		
		ФоновыеЗадания.Выполнить("СправочникНоменклатураСервер.ФоноваяПроцедураОбновленияРеквизитовПодчиненныхЭлементовСправочника",
			параметрыФоновогоЗадания, Новый УникальныйИдентификатор, "Фоновое обновление подчиненных элементов");
		
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти