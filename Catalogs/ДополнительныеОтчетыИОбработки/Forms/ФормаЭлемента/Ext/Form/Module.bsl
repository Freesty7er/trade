#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	АдресДанныхОбработки = ПоместитьВоВременноеХранилище(
			ТекущийОбъект.ХранилищеОбработки.Получить(),
			УникальныйИдентификатор);
	
КонецПроцедуры
		
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ДвоичныеДанныеОбработки = ПолучитьИзВременногоХранилища(АдресДанныхОбработки);
	ТекущийОбъект.ХранилищеОбработки = Новый ХранилищеЗначения(ДвоичныеДанныеОбработки, Новый СжатиеДанных(9));
	
КонецПроцедуры		
	
#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьФайлОбработкиОтчета(Команда)
	
	описаниеОповещения = Новый ОписаниеОповещения("ОбработатьВыборФайла", ЭтаФорма);
	
	диалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	диалогОткрытияФайла.МножественныйВыбор = Ложь;
	диалогОткрытияФайла.Фильтр = НСтр("ru = 'Внешняя обработка (*.epf)|*.epf|Внешний отчет (*.erf)|*.erf'");
	
	НачатьПомещениеФайлов(описаниеОповещения, , диалогОткрытияФайла, Истина, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьФайлОбработкиОтчета(Команда)
	
	описаниеОповещения = Новый ОписаниеОповещения("ОбработкаСохраненияФайлов", ЭтаФорма);
	
	файл = Новый ОписаниеПередаваемогоФайла(Объект.ИмяФайла, АдресДанныхОбработки);
	расширение = ВРег(Прав(Объект.ИмяФайла, 3));
	
	получаемыеФайлы = Новый Массив;
	получаемыеФайлы.Добавить(файл);
	
	диалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	диалогОткрытияФайла.МножественныйВыбор = Ложь;
	
	Если расширение = "EPF" Тогда
		фильтр = НСтр("ru = 'Внешняя обработка (*.epf)|*.epf'");
	Иначе
		фильтр = НСтр("ru = 'Внешний отчет (*.erf)|*.erf'");
	КонецЕсли;

	диалогОткрытияФайла.Фильтр = фильтр;
	
	НачатьПолучениеФайлов(описаниеОповещения, получаемыеФайлы, диалогОткрытияФайла, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыборФайла(помещенныеФайлы, дополнительныеПараметры) Экспорт
	
	Если помещенныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	переданныйФайл = помещенныеФайлы[0];
	
	выбранноеИмяФайла = переданныйФайл.Имя;
	
	МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(выбранноеИмяФайла, "\");
	
	Объект.ИмяФайла = МассивПодстрок.Получить(МассивПодстрок.ВГраница());
		
	АдресДанныхОбработки = ПереданныйФайл.Хранение;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаСохраненияФайлов(получаемыеФайлы, дополнительныеПараметры) Экспорт
	
	двоичныеДанныеОбработки = ПолучитьИзВременногоХранилища(АдресДанныхОбработки);
	
	двоичныеДанныеОбработки.Записать(получаемыеФайлы[0].Имя);
	
КонецПроцедуры

#КонецОбласти 