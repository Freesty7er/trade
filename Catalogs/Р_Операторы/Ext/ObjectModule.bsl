
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Владелец) Тогда
		Сообщить("Не заполнено поле: ""Торговая точка""!", СтатусСообщения.Важное);
		Отказ = Истина;	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Р_Операторы.Ссылка
	|ИЗ
	|	Справочник.Р_Операторы КАК Р_Операторы
	|ГДЕ
	|	Р_Операторы.Ссылка <> &Ссылка
	|	И Р_Операторы.Пароль = &Пароль
	|	И Р_Операторы.Владелец = &Терминал";
	
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("Пароль", Пароль);
	Запрос.УстановитьПараметр("Терминал", Владелец);
	Если Запрос.Выполнить().Выбрать().Количество() <> 0 Тогда
		Сообщить("Для текущего терминала уже есть оператор с паролем """ + Строка(Пароль) + """. Дублирование паролей недопустимо!", СтатусСообщения.Важное);
		Отказ = Истина;
	КонецЕсли;
	
	Если (НЕ Отказ) И ОбменДанными.Получатели.АвтоЗаполнение Тогда
		ТабУзлов = Р_МодульРегистрацииИзмененийПОС.ПолучитьУзлыОбменаАктивныхПОСТорговойТочки(Владелец);
		Если ЗначениеЗаполнено(ТабУзлов) Тогда
			Для Каждого СтрУзел Из ТабУзлов Цикл
				ОбменДанными.Получатели.Добавить(СтрУзел.Узел);
			КонецЦикла
		КонецЕсли
	КонецЕсли
	
КонецПроцедуры
