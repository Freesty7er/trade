
#Область ОбработчикиСобытий

Процедура ПередЗаписью(отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	запрос = Новый Запрос;
	
	#Область ТекстЗапрос
	
	запрос.Текст =
	"ВЫБРАТЬ
	|	РазделителиУчётаКонтрагентыПоставщики.Ссылка,
	|	РазделителиУчётаКонтрагентыПоставщики.Контрагент КАК Объект,
	|	""КонтрагентыПоставщики.Контрагент"" КАК ИмяТабличнойЧасти
	|ИЗ
	|	Справочник.РазделителиУчёта.КонтрагентыПоставщики КАК РазделителиУчётаКонтрагентыПоставщики
	|ГДЕ
	|	РазделителиУчётаКонтрагентыПоставщики.Контрагент В(&Контрагент)
	|	И РазделителиУчётаКонтрагентыПоставщики.Ссылка <> &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РазделителиУчётаСклады.Ссылка,
	|	РазделителиУчётаСклады.Склад,
	|	""Склады.Склад""
	|ИЗ
	|	Справочник.РазделителиУчёта.Склады КАК РазделителиУчётаСклады
	|ГДЕ
	|	РазделителиУчётаСклады.Склад В(&Склад)
	|	И РазделителиУчётаСклады.Ссылка <> &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РазделителиУчётаМенеджеры.Ссылка,
	|	РазделителиУчётаМенеджеры.Менеджер,
	|	""Менеджеры.Менеджер""
	|ИЗ
	|	Справочник.РазделителиУчёта.Менеджеры КАК РазделителиУчётаМенеджеры
	|ГДЕ
	|	РазделителиУчётаМенеджеры.Менеджер В(&Менеджер)
	|	И РазделителиУчётаМенеджеры.Ссылка <> &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РазделителиУчётаДенежныеСчета.Ссылка,
	|	РазделителиУчётаДенежныеСчета.ДенежныйСчет,
	|	""ДенежныеСчета.ДенежныйСчет""
	|ИЗ
	|	Справочник.РазделителиУчёта.ДенежныеСчета КАК РазделителиУчётаДенежныеСчета
	|ГДЕ
	|	РазделителиУчётаДенежныеСчета.ДенежныйСчет В(&ДенежныйСчет)
	|	И РазделителиУчётаДенежныеСчета.Ссылка <> &Ссылка";
	
	#КонецОбласти
	
	запрос.УстановитьПараметр("Ссылка", Ссылка);
	запрос.УстановитьПараметр("Контрагент", КонтрагентыПоставщики.ВыгрузитьКолонку("Контрагент"));
	запрос.УстановитьПараметр("Склад", Склады.ВыгрузитьКолонку("Склад"));
	запрос.УстановитьПараметр("Менеджер", Менеджеры.ВыгрузитьКолонку("Менеджер"));
	запрос.УстановитьПараметр("ДенежныйСчет", ДенежныеСчета.ВыгрузитьКолонку("ДенежныйСчет"));
	
	результатЗапроса = запрос.Выполнить();
	Если Не результатЗапроса.Пустой() Тогда
		
		выборка = результатЗапроса.Выбрать();
		Пока выборка.Следующий() Цикл
			
			текстСообщения = СтрШаблон("Значение: %1, уже используется в разделителе: %2", выборка.Объект, выборка.Ссылка);
			
			ПроверкаДанныхКлиентСервер.СообщитьОбОшибке(отказ, текстСообщения, ЭтотОбъект, выборка.ИмяТабличнойЧасти);
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(отказ, проверяемыеРеквизиты)
	
	ОбработкаТабличныхЧастейСервер.ПроверитьДублиСтрокТабличнойЧасти(отказ, ЭтотОбъект, "КонтрагентыПоставщики", Новый Структура("Контрагент", "Контрагент"));
	ОбработкаТабличныхЧастейСервер.ПроверитьДублиСтрокТабличнойЧасти(отказ, ЭтотОбъект, "Склады", Новый Структура("Склад", "Склад"));
	ОбработкаТабличныхЧастейСервер.ПроверитьДублиСтрокТабличнойЧасти(отказ, ЭтотОбъект, "Менеджеры", Новый Структура("Менеджер", "Менеджер"));
	ОбработкаТабличныхЧастейСервер.ПроверитьДублиСтрокТабличнойЧасти(отказ, ЭтотОбъект, "ДенежныеСчета", Новый Структура("ДенежныйСчет", "ДенежныйСчет"));

КонецПроцедуры

#КонецОбласти