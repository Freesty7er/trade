
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ФОРМЫ

Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ИмяМакета, КоллекцияПечатныхФорм)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АктСписания";
	
	ПервыйДокумент = Истина;
	
	Для Каждого ТекущийДокумент Из МассивОбъектов Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если ИмяМакета = "АктСписания" Тогда
			
			Запрос = Новый Запрос();
			Запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	АктСписания.Номер,
			|	АктСписания.Дата,
			|	АктСписания.СтруктурнаяЕдиница КАК Склад,
			|	АктСписания.Комментарий,
			|	АктСписания.Запасы.(
			|		Номенклатура.Код,
			|		НомерСтроки КАК НомерСтроки,
			|		Номенклатура,
			|		ЕдиницаИзмерения,
			|		Количество,
			|		Цена,
			|		Сумма,
			|		Номенклатура.Наименование,
			|		Номенклатура.Вес
			|	)
			|ИЗ
			|	Документ.АктСписания КАК АктСписания
			|ГДЕ
			|	АктСписания.Ссылка = &ТекущийДокумент
			|
			|УПОРЯДОЧИТЬ ПО
			|	НомерСтроки";
			
			Шапка = Запрос.Выполнить().Выбрать();
			Шапка.Следующий();
			
			НомерДокумента = Шапка.Номер;
			
			ВыборкаСтрокЗапасы = Шапка.Запасы.Выбрать();
			
			ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_АктСписания_АктСписания";
			
			
			Макет = УправлениеПечатью.ПолучитьМакет("Документ.АктСписания.АктСписания");
			
			
			КодЯзыкаПечать = Локализация.ПолучитьЯзыкФормированияПечатныхФорм();	
			Макет.КодЯзыкаМакета = КодЯзыкаПечать;
			
			ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
			
			ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, Шапка);
			ОбластьМакета.Параметры.Дата = Формат(Шапка.Дата, "ДЛФ=DD");
			
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ИтКоличество	= 0;
			ИтСумма			= 0;
			ОбщийВес		= 0;
			
			ОбластьМакета = Макет.ПолучитьОбласть("Строка");

			Пока ВыборкаСтрокЗапасы.Следующий() Цикл
				
				ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокЗапасы);
				
				ОбластьМакета.Параметры.НоменклатураКод = Прав(ВыборкаСтрокЗапасы.НоменклатураКод, 5);
				
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
				ИтКоличество	= ИтКоличество + ВыборкаСтрокЗапасы.Количество;
				ИтСумма			= ИтСумма + ВыборкаСтрокЗапасы.Сумма;
				ОбщийВес		= ОбщийВес + ВыборкаСтрокЗапасы.Количество * ВыборкаСтрокЗапасы.НоменклатураВес;
				
			КонецЦикла;
			
			ОбластьМакета = Макет.ПолучитьОбласть("Итого");

			
			
			ОбластьМакета.Параметры.Сумма 		= ИтСумма;
			ОбластьМакета.Параметры.Количество	= ИтКоличество;
				
			ТабличныйДокумент.Вывести(ОбластьМакета);
				
				
			ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
				
			ТабличныйДокумент.Вывести(ОбластьМакета);
				
			ТабличныйДокумент.ОриентацияСтраницы=ОриентацияСтраницы.Портрет;
			
			
		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ТекущийДокумент);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатнаяФорма()

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов,
				 ПараметрыПечати,
				 КоллекцияПечатныхФорм,
				 ОбъектыПечати,
				 ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктСписания") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктСписания", "Акт списания", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "АктСписания", КоллекцияПечатныхФорм));
	КонецЕсли;
	
	
КонецПроцедуры


