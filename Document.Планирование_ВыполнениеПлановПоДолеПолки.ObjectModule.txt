
#Область ОбработчикиСобытий

Процедура ПриКопировании(объектКопирования)
	
	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеСкопированногоДокумента(ЭтотОбъект, объектКопирования);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(данныеЗаполнения, стандартнаяОбработка)
	
	заполненНаОснованииДокумента = Ложь;

	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеНовогоДокумента(ЭтотОбъект, данныеЗаполнения);
	
	// Заполнение по умолчанию
	Если Подразделение.Пустая() Тогда
		Подразделение = ПользователиСервер.ПолучитьЗначениеНастройки("ОсновноеПодразделение");
	КонецЕсли; 
	
	Если Ответственный.Пустая() Тогда
		Ответственный = ПользователиСервер.ПолучитьЗначениеНастройки("ОсновнойОтветственный");
	КонецЕсли;
	
	// Ввод на основании.
	типДанныхЗаполнения = ТипЗнч(данныеЗаполнения);
	
	Если типДанныхЗаполнения = Тип("Структура") Тогда
		
		ЗаполнитьДокументПоОтбору(данныеЗаполнения);
		
	ИначеЕсли типДанныхЗаполнения = Тип("ДокументСсылка.Планирование_УстановкаПлановПоДолеПолки") Тогда
		
		ЗаполнитьДокументНаОснованииУстановкиПлановПоДолеПолки(данныеЗаполнения);
		заполненНаОснованииДокумента = Истина;
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаПроведения(отказ, режимПроведения)
	
	Движения.ВзаиморасчетыССотрудниками.Записывать = Истина;
	Движения.ФинансовыйРезультат.Записывать = Истина;
	Движения.РасчетыПоОплатеТруда.Записывать = Истина;
	
	запрос = Новый Запрос;
	
	#Область ТекстЗапроса
	
	запрос.Текст =
	"ВЫБРАТЬ
	|	Планирование_ВыполнениеПлановПоДолеПолкиРасчетЗарплаты.Ссылка.Подразделение,
	|	НАЧАЛОПЕРИОДА(Планирование_ВыполнениеПлановПоДолеПолкиРасчетЗарплаты.Ссылка.Дата, МЕСЯЦ) КАК ПериодРегистрации,
	|	Планирование_ВыполнениеПлановПоДолеПолкиРасчетЗарплаты.Ссылка.Дата КАК Период,
	|	Планирование_ВыполнениеПлановПоДолеПолкиРасчетЗарплаты.Сотрудник,
	|	Планирование_ВыполнениеПлановПоДолеПолкиРасчетЗарплаты.ВидРасчета,
	|	СУММА(Планирование_ВыполнениеПлановПоДолеПолкиРасчетЗарплаты.Результат) КАК Результат,
	|	Планирование_ВыполнениеПлановПоДолеПолкиРасчетЗарплаты.Сотрудник.КатегорияРаботника.СтатьяЗатратПоЗарплате КАК СтатьяЗатрат
	|ПОМЕСТИТЬ ВТ_ДанныеДокумента
	|ИЗ
	|	Документ.Планирование_ВыполнениеПлановПоДолеПолки.РасчетЗарплаты КАК Планирование_ВыполнениеПлановПоДолеПолкиРасчетЗарплаты
	|ГДЕ
	|	Планирование_ВыполнениеПлановПоДолеПолкиРасчетЗарплаты.Ссылка = &Ссылка
	|	И Планирование_ВыполнениеПлановПоДолеПолкиРасчетЗарплаты.Результат <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	Планирование_ВыполнениеПлановПоДолеПолкиРасчетЗарплаты.Ссылка.Подразделение,
	|	НАЧАЛОПЕРИОДА(Планирование_ВыполнениеПлановПоДолеПолкиРасчетЗарплаты.Ссылка.Дата, МЕСЯЦ),
	|	Планирование_ВыполнениеПлановПоДолеПолкиРасчетЗарплаты.Сотрудник,
	|	Планирование_ВыполнениеПлановПоДолеПолкиРасчетЗарплаты.ВидРасчета,
	|	Планирование_ВыполнениеПлановПоДолеПолкиРасчетЗарплаты.Сотрудник.КатегорияРаботника.СтатьяЗатратПоЗарплате,
	|	Планирование_ВыполнениеПлановПоДолеПолкиРасчетЗарплаты.Ссылка.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.Сотрудник,
	|	ЗНАЧЕНИЕ(ПланСчетов.Внутренний.Зарплата) КАК СчетУчета,
	|	ВТ_ДанныеДокумента.Результат КАК Сумма
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.СтатьяЗатрат,
	|	СУММА(ВТ_ДанныеДокумента.Результат) КАК СуммаРасходов
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.СтатьяЗатрат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.Сотрудник,
	|	ВТ_ДанныеДокумента.ПериодРегистрации,
	|	ВТ_ДанныеДокумента.ВидРасчета,
	|	ВТ_ДанныеДокумента.Результат
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента";
	
	#КонецОбласти
	
	запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	результатЗапроса = запрос.ВыполнитьПакет();
	
	Движения.ВзаиморасчетыССотрудниками.Загрузить(результатЗапроса[1].Выгрузить());
	Движения.ФинансовыйРезультат.Загрузить(результатЗапроса[2].Выгрузить());
	Движения.РасчетыПоОплатеТруда.Загрузить(результатЗапроса[3].Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеПоОснованию

Процедура ЗаполнитьДокументПоОтбору(Знач данныеЗаполнения)
	
КонецПроцедуры	

Процедура ЗаполнитьДокументНаОснованииУстановкиПлановПоДолеПолки(Знач данныеЗаполнения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Заполнение реквизитов документа.
	ЗаполнениеОбъектовСервер.ЗаполнитьРеквизитыДокументаПриВводеНаОсновании(ЭтотОбъект, данныеЗаполнения);
	
		запрос = Новый Запрос;
	
	#Область ТекстЗапроса
	
	запрос.Текст =
	"ВЫБРАТЬ
	|	Планирование_УстановкаПлановПоДолеПолкиСостав.КатегорияТРТ.Родитель.ГруппаПродукта КАК ГруппаПродукта,
	|	Планирование_УстановкаПлановПоДолеПолкиСостав.Ссылка.Подразделение
	|ПОМЕСТИТЬ ВТ_ГруппыПродукта
	|ИЗ
	|	Документ.Планирование_УстановкаПлановПоДолеПолки.Состав КАК Планирование_УстановкаПлановПоДолеПолкиСостав
	|ГДЕ
	|	Планирование_УстановкаПлановПоДолеПолкиСостав.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Планирование_УстановкаПлановПоДолеПолкиСостав.КатегорияТРТ.Родитель.ГруппаПродукта,
	|	Планирование_УстановкаПлановПоДолеПолкиСостав.Ссылка.Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БПАГСторЧек.Менеджер,
	|	БПАГСторЧек.КД_ГруппаПродукта,
	|	БПАГСторЧек.Подразделение
	|ПОМЕСТИТЬ ВТ_Менеджеры
	|ИЗ
	|	Документ.БПАГСторЧек КАК БПАГСторЧек
	|ГДЕ
	|	БПАГСторЧек.Проведен = ИСТИНА
	|	И БПАГСторЧек.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И БПАГСторЧек.Подразделение В
	|			(ВЫБРАТЬ
	|				ВТ_ГруппыПродукта.Подразделение
	|			ИЗ
	|				ВТ_ГруппыПродукта КАК ВТ_ГруппыПродукта)
	|	И БПАГСторЧек.КД_ГруппаПродукта В
	|			(ВЫБРАТЬ
	|				ВТ_ГруппыПродукта.ГруппаПродукта
	|			ИЗ
	|				ВТ_ГруппыПродукта КАК ВТ_ГруппыПродукта)
	|
	|СГРУППИРОВАТЬ ПО
	|	БПАГСторЧек.Менеджер,
	|	БПАГСторЧек.КД_ГруппаПродукта,
	|	БПАГСторЧек.Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Менеджеры.Менеджер
	|ИЗ
	|	ВТ_ГруппыПродукта КАК ВТ_ГруппыПродукта,
	|	ВТ_Менеджеры КАК ВТ_Менеджеры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Менеджеры.Менеджер,
	|	КД_НазначенияТорговыхПредставителейСрезПоследних.Сотрудник
	|ИЗ
	|	ВТ_Менеджеры КАК ВТ_Менеджеры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КД_НазначенияТорговыхПредставителей.СрезПоследних(&ДатаОкончания, ) КАК КД_НазначенияТорговыхПредставителейСрезПоследних
	|		ПО ВТ_Менеджеры.Подразделение = КД_НазначенияТорговыхПредставителейСрезПоследних.Подразделение
	|			И ВТ_Менеджеры.Менеджер = КД_НазначенияТорговыхПредставителейСрезПоследних.Менеджер
	|ГДЕ
	|	КД_НазначенияТорговыхПредставителейСрезПоследних.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыСотрудников.Уволен)";
	
	#КонецОбласти
	
	запрос.УстановитьПараметр("Ссылка", данныеЗаполнения);
	запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(данныеЗаполнения.Дата));
	запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(данныеЗаполнения.Дата));
	
	результатЗапроса = запрос.ВыполнитьПакет();
	
	ЭтотОбъект.ИсходныеДанные.Загрузить(результатЗапроса[2].Выгрузить());
	ЭтотОбъект.БазыРасчетаЗарплаты.Загрузить(результатЗапроса[3].Выгрузить());

	
КонецПроцедуры

#КонецОбласти

#КонецОбласти