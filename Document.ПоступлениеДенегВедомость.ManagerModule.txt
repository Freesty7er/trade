
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ФОРМЫ

Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ИмяМакета, КоллекцияПечатныхФорм)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АктСписания";
	
	ПервыйДокумент = Истина;
	
	Для Каждого ТекущийДокумент Из МассивОбъектов Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если ИмяМакета = "Ведомость" Тогда
			
			Запрос = Новый Запрос();
			Запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПоступлениеДенегВедомость.Номер,
			|	ПоступлениеДенегВедомость.Дата,
			|	ПоступлениеДенегВедомость.Маршрут,
			|	ПоступлениеДенегВедомость.СтрокиДокумента.(
			|		НомерСтроки КАК НомерСтроки,
			|		Контрагент,
			|		КредДокумент,
			|		Сумма,
			|		СуммаСкидки
			|	)
			|ИЗ
			|	Документ.ПоступлениеДенегВедомость КАК ПоступлениеДенегВедомость
			|ГДЕ
			|	ПоступлениеДенегВедомость.Ссылка = &ТекущийДокумент
			|
			|УПОРЯДОЧИТЬ ПО
			|	НомерСтроки";
			
			Шапка = Запрос.Выполнить().Выбрать();
			Шапка.Следующий();
			
			НомерДокумента = Шапка.Номер;
			
			ВыборкаСтрокДокумента = Шапка.СтрокиДокумента.Выбрать();
			
			ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеДенегВедомость_Ведомость";
			
			
			Макет = УправлениеПечатью.ПолучитьМакет("Документ.ПоступлениеДенегВедомость.Ведомость");
			
			
			КодЯзыкаПечать = Локализация.ПолучитьЯзыкФормированияПечатныхФорм();	
			Макет.КодЯзыкаМакета = КодЯзыкаПечать;
			
			ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
			
			ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, Шапка);
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ИтСумма			= 0;
			ИтСуммаСкидки	= 0;
			
			ОбластьМакета = Макет.ПолучитьОбласть("Строка");

			Пока ВыборкаСтрокДокумента.Следующий() Цикл
				
				ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокДокумента);
				
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
				ИтСуммаСкидки	= ИтСуммаСкидки + ВыборкаСтрокДокумента.СуммаСкидки;
				ИтСумма			= ИтСумма + ВыборкаСтрокДокумента.Сумма;
				
			КонецЦикла;
			
			ОбластьМакета = Макет.ПолучитьОбласть("Подвал");

			
			
			ОбластьМакета.Параметры.Сумма 		= ИтСумма;
			ОбластьМакета.Параметры.СуммаСкидки	= ИтСуммаСкидки;
				
			ТабличныйДокумент.Вывести(ОбластьМакета);
				
				
			ТабличныйДокумент.ОриентацияСтраницы=ОриентацияСтраницы.Портрет;
			
			
		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ТекущийДокумент);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатнаяФорма()

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов,
				 ПараметрыПечати,
				 КоллекцияПечатныхФорм,
				 ОбъектыПечати,
				 ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Ведомость") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Ведомость", "Кассовая ведомость", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "Ведомость", КоллекцияПечатныхФорм));
	КонецЕсли;
	
	
КонецПроцедуры


