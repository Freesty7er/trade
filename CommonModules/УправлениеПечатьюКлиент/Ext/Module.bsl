// Выполнить команду печати, которая открывает результат в форме печати документов
Процедура ВыполнитьКомандуПечати(ИмяМенеджераПечати, ИменаМакетов, ПараметрКоманды, ВладелецФормы, ПараметрыПечати = Неопределено, КоличествоЭкземпляров = 2) Экспорт
	
	Если ТипЗнч(ПараметрКоманды) = Тип("Массив") Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияТипаИзМассива(ПараметрКоманды, Тип("СтрокаГруппировкиДинамическогоСписка"));
	КонецЕсли;
	
	// Проверим количество объектов
	Если НЕ ПроверитьКоличествоПереданныхОбъектов(ПараметрКоманды) Тогда
		Возврат;
	КонецЕсли;
	
	// Получим ключ уникальности открываемой формы
	КлючУникальности = Строка(Новый УникальныйИдентификатор);
	
	ПараметрыОткрытия = Новый Структура("ИмяМенеджераПечати,ИменаМакетов,ПараметрКоманды,ПараметрыПечати,КоличествоЭкземпляров");
	ПараметрыОткрытия.ИмяМенеджераПечати    = ИмяМенеджераПечати;
	ПараметрыОткрытия.ИменаМакетов		    = ИменаМакетов;
	ПараметрыОткрытия.ПараметрКоманды	    = ПараметрКоманды;
	ПараметрыОткрытия.ПараметрыПечати	    = ПараметрыПечати;
	ПараметрыОткрытия.КоличествоЭкземпляров	= КоличествоЭкземпляров;
	
	// Откроем форму печати документов
	ОткрытьФорму("ОбщаяФорма.ПечатьДокументов", ПараметрыОткрытия, ВладелецФормы, КлючУникальности);
	
КонецПроцедуры

 Функция ПроверитьКоличествоПереданныхОбъектов(ПараметрКоманды)
	
	Если ТипЗнч(ПараметрКоманды) = Тип("Массив") И ПараметрКоманды.Количество() = 0 Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции


// Выполняет подготовку и проверку переданных объектов для печати и выводит их сформированные печатные
// формы в специальной диалоговой форме.
//
// Параметры
//  ОбъектыПечати  - Массив, Произвольная ссылка - Ссылка на объект или массив ссылок на
//                 объекты, печатные формы которых необходимо сформировать. Если передаётся
//                 массив, то все объекты должны быть одного типа.
//  ИмяМакета  - Строка - Имя макета объекта, на основании которого будут формироваться
//                 печатные формы.
//  стДополнительныеПараметры  - Структура, Неопределено - Дополнительные параметры для формирования печатной
//                 формы. Если значение не указано, в процедуру формирования макета печатной формы будет
//                 передан пустой набор дополнительных параметров.
//
Процедура ВыполнитьКомандуПечатиНовый(ОбъектыПечати, ИмяМакета, стДополнительныеПараметры = Неопределено) Экспорт
	
	// СДЕЛАТЬ: После перевода печати всех объектов на новый механизм переименовать процедуру в ВыполнитьКомандуПечати().
	
	// Проверим возможность печати переданных объектов и сформируем необходимые наборы объектов
	// для дальнейшей обработки.
	НаборыОбъектовДляПечати = УправлениеПечатьюСервер.ВыполнитьПодготовительныеОперацииПередПечатью(ОбъектыПечати, ИмяМакета);
	Если НаборыОбъектовДляПечати = Неопределено Тогда
		Возврат;
	КонецЕсли; 

	соПараметрыОбъектовПечати = Новый Соответствие;	// Набор объектов печати. В качестве ключа используется ссылка на объект, а в качестве значения - параметры повторной печати.
	мПроверенныеОбъекты = НаборыОбъектовДляПечати.ПроверенныеОбъекты;	// Массив объектов, прошедших проверку.
	мПропущенныеОбъекты = НаборыОбъектовДляПечати.ПропущенныеОбъекты;	// Массив объектов, не прошедших проверку.
	
	Если мПроверенныеОбъекты.Количество() > 0 Тогда
		
		// Здесь печатаемые объекты можно дополнительно обработать и заполнить для них дополнительные
		// параметры печати как значение соответствия соПараметрыОбъектовПечати;
		
		// Сейчас нет необходимости в дополнительной обработке, поэтому печатаем все возможные объекты.
		Для каждого ПроверенныйОбъектПечати Из мПроверенныеОбъекты Цикл
			соПараметрыОбъектовПечати.Вставить(ПроверенныйОбъектПечати, Неопределено);
		КонецЦикла; 
		
	КонецЕсли; 	
	
	Если стДополнительныеПараметры = Неопределено Тогда
		стДополнительныеПараметры = Новый Структура;
	КонецЕсли; 
	
	// Открываем форму печати объектов.
	стПараметры = Новый Структура;
	стПараметры.Вставить("ИмяМакета", ИмяМакета);
	стПараметры.Вставить("ПараметрыОбъектовПечати", соПараметрыОбъектовПечати);
	стПараметры.Вставить("ПропущенныеОбъекты", мПропущенныеОбъекты);
	стПараметры.Вставить("ДополнительныеПараметры", стДополнительныеПараметры);
	
	ОткрытьФорму("ОбщаяФорма.ПечатьОбъектов", стПараметры,, Истина);

КонецПроцедуры // ВыполнитьКомандуПечатиНовый()
