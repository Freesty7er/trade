
#Область РасчётНДС

// Возвращает числовое значение переданной ставки НДС.
//
// Параметры
//  СтавкаНДС  - Число, ПеречислениеСсылка.СтавкиНДС, СправочникСсылка.СтавкиНДС - Ставка НДС,
//                 для которой необходимо получить числовое значение.
//
// Возвращаемое значение:
//   Число   - Числовое значение ставки НДС.
//
Функция ПолучитьЗначениеСтавкиНДС(СтавкаНДС) Экспорт

	Если ТипЗнч(СтавкаНДС) = Тип("ПеречислениеСсылка.СтавкиНДС") Тогда
	
		Если СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20") Тогда
			Возврат 20;
		Иначе
			Возврат 0;
		КонецЕсли; 
		
	ИначеЕсли ТипЗнч(СтавкаНДС) = Тип("Строка") Тогда
		
		// СДЕЛАТЬ: Эта затычка сделана по причине отсутствия в конфигурации нормального
		// механизма работы с НДС. Эту ветвь условия следует в дальнейшем удалить.
		// Ответственность за корректное значение ставки НДС (долно быть число в виде строкового
		// значения) возлагается на вызывающий код.
		Если ПустаяСтрока(СтавкаНДС) Тогда
			Возврат 0;
		Иначе
			Возврат Число(СтавкаНДС);
		КонецЕсли; 
		
	ИначеЕсли ТипЗнч(СтавкаНДС) = Тип("СправочникСсылка.СтавкиНДС") Тогда
		Возврат ЦенообразованиеСервер.ПолучитьЗначениеСтавкиНДС(СтавкаНДС);
	Иначе
		Возврат СтавкаНДС;
	КонецЕсли; 

КонецФункции // ПолучитьЗначениеСтавкиНДС()

// Рассчитывает сумму без НДС исходя из суммы и ставки НДС.
//
// Параметры
//  Сумма  - Число - Сумма, от которой будет рассчитана сумма без НДС.
//  СтавкаНДС  - Число, ПеречислениеСсылка.СтавкиНДС - Ставка НДС для рассчёта суммы без НДС.
//
// Возвращаемое значение:
//   Число   - Рассчитанная сумма без НДС.
//
Функция РассчитатьСуммуБезНДС(Сумма, СтавкаНДС) Экспорт

	Возврат Сумма * 100 / (ПолучитьЗначениеСтавкиНДС(СтавкаНДС) + 100);

КонецФункции // РассчитатьСуммуБезНДС()

// Рассчитывает сумму с НДС исходя из суммы и ставки НДС.
//
// Параметры
//  Сумма  - Число - Сумма, от которой будет рассчитана сумма с НДС.
//  СтавкаНДС  - Число, ПеречислениеСсылка.СтавкиНДС - Ставка НДС для рассчёта суммы с НДС.
//
// Возвращаемое значение:
//   Число   - Рассчитанная сумма с НДС.
//
Функция РассчитатьСуммуСНДС(Сумма, СтавкаНДС) Экспорт

	Возврат Сумма * (ПолучитьЗначениеСтавкиНДС(СтавкаНДС) + 100) / 100;
	
КонецФункции // РассчитатьСуммуСНДС()

// Рассчитывает сумму НДС исходя из суммы, ставки НДС и признака
// включения НДС в сумму.
//
// Параметры
//  Сумма  - Число - Сумма, от которой будет рассчитана сумма НДС.
//  СуммаВключаетНДС  - Булево - Признак включения НДС в сумму:
//                 Истина - НДС необходимо извлечь из суммы;
//                 Ложь - НДС будет насчитан на сумму;
//  СтавкаНДС  - Число, ПеречислениеСсылка.СтавкиНДС - Ставка НДС для рассчёта суммы НДС.
//
// Возвращаемое значение:
//   Число   - Рассчитанная сумма НДС.
//
Функция РассчитатьСуммуНДС(Сумма, СуммаВключаетНДС, СтавкаНДС) Экспорт

	ЗначениеСтавкиНДС = ПолучитьЗначениеСтавкиНДС(СтавкаНДС);
	
	Если СуммаВключаетНДС Тогда
		СуммаНДС = Сумма * ЗначениеСтавкиНДС / (100 + ЗначениеСтавкиНДС);
	Иначе
		СуммаНДС = Сумма * ЗначениеСтавкиНДС / 100;
	КонецЕсли;
	
	Возврат СуммаНДС;

КонецФункции // РассчитатьСуммуНДС()

#КонецОбласти 
