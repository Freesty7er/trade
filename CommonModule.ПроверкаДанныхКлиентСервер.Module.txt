
// Выводит сообщение об ошибке и выставляет параметр Отказ в "Истина". В случае работы на клиенте
// или на сервере выводит в окно сообщений, в случае внешнего соединения вызывает исключение.
//
// Параметры
//  Отказ  - Булево - Признак отказа от выполняемой операции.
//  ТекстСообщения  - Строка - Текст выводимого сообщения.
//  Объект  - Произвольный - Объект, с которым должно быть связано сообщение.
//  ИмяРеквизита  - Строка - Содержит путь к реквизиту формы, для которого было выведено сообщение, или к данным объекта.
//  стОписаниеСтрокиСписка  - Структура - Содержит информацию, необходимую для формирования пути к данным реквизита списка.
//                 Используется совместно с параметром ИмяРеквизита. Состав свойств описан в функции
//                 ПроверкаДанныхСервер.ПолучитьОписаниеСтрокиТабличнойЧасти. Если в описании строки задано представление
//                 списка, в конце сообщения об ошибке будет добавлен текст " в строке <НомерСтроки> списка
//                 "<ПредставлениеСписка>".", поэтому ТекстСообщения необходимо составлять с учётом данной особенности.
//                 Если параметр не задан, это означает что сообщение выводится для реквизита объекта, а не табличной части.
//
Процедура СообщитьОбОшибке(отказ, текстСообщения, объект = Неопределено, имяРеквизита = "", стОписаниеСтрокиСписка = Неопределено, идентификаторНазначения = Неопределено) Экспорт
	
	отказ = Истина;
	
	#Если ВнешнееСоединение Тогда
		ВызватьИсключение текстСообщения;
	#Иначе
		
		сообщение = Новый СообщениеПользователю;
		сообщение.Текст = текстСообщения + ПолучитьПредставлениеСтрокиСпискаПоОписанию(стОписаниеСтрокиСписка);
		сообщение.Поле = ПолучитьПутьКРеквизитуПоОписанию(имяРеквизита, стОписаниеСтрокиСписка);
		сообщение.УстановитьДанные(Объект);
		
		Если Не идентификаторНазначения = Неопределено Тогда
			сообщение.ИдентификаторНазначения = идентификаторНазначения;
		КонецЕсли;
		
		сообщение.Сообщить(); 
		
	#КонецЕсли
	
КонецПроцедуры // СообщитьОбОшибке()

// Возвращает строковое представление строки списка вида " в строке 2 списка "Номенклатурный состав". Используется для
// вывода различных информационных сообщений, привязанных к спискам.
//
// Параметры
//  стОписаниеСтрокиСписка  - Структура - Содержит информацию, необходимую для формирования пути к данным реквизита
//                 списка. Состав свойств описан в функции ПроверкаДанныхСервер.ПолучитьОписаниеСтрокиТабличнойЧасти().
//                 Если параметр равен Неопределено, будет возвращена пустая строка.
//
// Возвращаемое значение:
//   Строка   - Строковое представление строки списка.
//
Функция ПолучитьПредставлениеСтрокиСпискаПоОписанию(стОписаниеСтрокиСписка) Экспорт

	ПредставлениеСтроки = "";
	
	Если стОписаниеСтрокиСписка <> Неопределено Тогда
	
		ПредставлениеСписка = "";
		Если стОписаниеСтрокиСписка.Свойство("ПредставлениеСписка", ПредставлениеСписка) И ЗначениеЗаполнено(ПредставлениеСписка) Тогда
			
			ИмяСписка = "";
			НомерСтроки = 0;
			
			Если Не стОписаниеСтрокиСписка.Свойство("ИмяСписка", ИмяСписка) Или Не ЗначениеЗаполнено(ИмяСписка) Тогда
				ВызватьИсключение НСтр("ru = 'В процедуре ПроверкаДанныхКлиентСервер.ПоучитьПредставлениеСтрокиСпискаПоОписанию() не указано имя списка в описании строки списка.'");
			КонецЕсли; 
			Если Не стОписаниеСтрокиСписка.Свойство("НомерСтроки", НомерСтроки) Тогда
				ВызватьИсключение НСтр("ru = 'В процедуре ПроверкаДанныхКлиентСервер.ПоучитьПредставлениеСтрокиСпискаПоОписанию() не указан номер строки в описании строки списка.'");
			КонецЕсли; 
			
			ПредставлениеСтроки = СтрШаблон(НСтр("ru = ' в строке %1 списка ""%2""'"), НомерСтроки, ПредставлениеСписка);
			
		Иначе
			ПредставлениеСтроки = "";
		КонецЕсли; 
	
	КонецЕсли; 
	
	Возврат ПредставлениеСтроки;

КонецФункции // ПолучитьПредставлениеСтрокиСпискаПоОписанию()

// Возвращает путь к реквизиту объекта. Для реквизитов списков (табличных частей) возвращает значение вида
// "НоменклатурныйСостав[1].Количество". Используется для вывода различных информационных сообщений,
// привязанных к спискам.
//
// Параметры
//  ИмяРеквизита  - Строка - Содержит путь к реквизиту формы, для которого было выведено сообщение, или к
//                 данным объекта.
//  стОписаниеСтрокиСписка  - Структура - Содержит информацию, необходимую для формирования пути к данным реквизита
//                 списка. Состав свойств описан в функции ПроверкаДанныхСервер.ПолучитьОписаниеСтрокиТабличнойЧасти().
//                 Если параметр равен Неопределено, будет возвращено ИмяРеквизита.
//
// Возвращаемое значение:
//   <Тип.Вид>   - <описание возвращаемого значения>
//
Функция ПолучитьПутьКРеквизитуПоОписанию(имяРеквизита, стОписаниеСтрокиСписка) Экспорт

	Если стОписаниеСтрокиСписка = Неопределено Тогда
		путьКРеквизиту = имяРеквизита;
	Иначе
	
		имяСписка = "";
		номерСтроки = 0;
		
		Если Не стОписаниеСтрокиСписка.Свойство("ИмяСписка", имяСписка) Или Не ЗначениеЗаполнено(имяСписка) Тогда
			ВызватьИсключение НСтр("ru = 'В процедуре ПроверкаДанныхКлиентСервер.ПоучитьПредставлениеСтрокиСпискаПоОписанию() не указано имя списка в описании строки списка.'");
		КонецЕсли; 
		Если Не стОписаниеСтрокиСписка.Свойство("НомерСтроки", номерСтроки) Тогда
			ВызватьИсключение НСтр("ru = 'В процедуре ПроверкаДанныхКлиентСервер.ПоучитьПредставлениеСтрокиСпискаПоОписанию() не указан номер строки в описании строки списка.'");
		КонецЕсли; 
		
		путьКРеквизиту = имяСписка + "[" + (номерСтроки - 1) + "]." + имяРеквизита;
	
	КонецЕсли; 
	
	Возврат путьКРеквизиту;

КонецФункции // ПолучитьПутьКРеквизитуСтрокиСпискаПоОписанию()
