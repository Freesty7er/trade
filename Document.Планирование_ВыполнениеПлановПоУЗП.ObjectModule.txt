
#Область ОбработчикиСобытий

Процедура ПриКопировании(объектКопирования)
	
	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеСкопированногоДокумента(ЭтотОбъект, объектКопирования);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(данныеЗаполнения, стандартнаяОбработка)
	
	заполненНаОснованииДокумента = Ложь;

	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеНовогоДокумента(ЭтотОбъект, данныеЗаполнения);
	
	// Заполнение по умолчанию
	Если Подразделение.Пустая() Тогда
		Подразделение = ПользователиСервер.ПолучитьЗначениеНастройки("ОсновноеПодразделение");
	КонецЕсли; 
	
	Если Ответственный.Пустая() Тогда
		Ответственный = ПользователиСервер.ПолучитьЗначениеНастройки("ОсновнойОтветственный");
	КонецЕсли;
	
	// Ввод на основании.
	типДанныхЗаполнения = ТипЗнч(данныеЗаполнения);
	
	Если типДанныхЗаполнения = Тип("Структура") Тогда
		
		ЗаполнитьДокументПоОтбору(данныеЗаполнения);
		
	ИначеЕсли типДанныхЗаполнения = Тип("ДокументСсылка.Планирование_УстановкаПлановПоУЗП") Тогда
		
		ЗаполнитьДокументНаОснованииУстановкиПлановПоУЗП(данныеЗаполнения);
		заполненНаОснованииДокумента = Истина;
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаПроведения(отказ, режимПроведения)
	
	Движения.ВзаиморасчетыССотрудниками.Записывать = Истина;
	Движения.ФинансовыйРезультат.Записывать = Истина;
	Движения.РасчетыПоОплатеТруда.Записывать = Истина;
	
	запрос = Новый Запрос;
	
	#Область ТекстЗапроса
	
	запрос.Текст =
	"ВЫБРАТЬ
	|	Планирование_ВыполнениеПлановПоУЗПРасчетЗарплаты.Ссылка.Подразделение,
	|	НАЧАЛОПЕРИОДА(Планирование_ВыполнениеПлановПоУЗПРасчетЗарплаты.Ссылка.Дата, МЕСЯЦ) КАК ПериодРегистрации,
	|	Планирование_ВыполнениеПлановПоУЗПРасчетЗарплаты.Ссылка.Дата КАК Период,
	|	Планирование_ВыполнениеПлановПоУЗПРасчетЗарплаты.Сотрудник,
	|	Планирование_ВыполнениеПлановПоУЗПРасчетЗарплаты.ВидРасчета,
	|	СУММА(Планирование_ВыполнениеПлановПоУЗПРасчетЗарплаты.Результат) КАК Результат,
	|	Планирование_ВыполнениеПлановПоУЗПРасчетЗарплаты.Сотрудник.КатегорияРаботника.СтатьяЗатратПоЗарплате КАК СтатьяЗатрат
	|ПОМЕСТИТЬ ВТ_ДанныеДокумента
	|ИЗ
	|	Документ.Планирование_ВыполнениеПлановПоУЗП.РасчетЗарплаты КАК Планирование_ВыполнениеПлановПоУЗПРасчетЗарплаты
	|ГДЕ
	|	Планирование_ВыполнениеПлановПоУЗПРасчетЗарплаты.Ссылка = &Ссылка
	|	И Планирование_ВыполнениеПлановПоУЗПРасчетЗарплаты.Результат <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	Планирование_ВыполнениеПлановПоУЗПРасчетЗарплаты.Ссылка.Подразделение,
	|	НАЧАЛОПЕРИОДА(Планирование_ВыполнениеПлановПоУЗПРасчетЗарплаты.Ссылка.Дата, МЕСЯЦ),
	|	Планирование_ВыполнениеПлановПоУЗПРасчетЗарплаты.Сотрудник,
	|	Планирование_ВыполнениеПлановПоУЗПРасчетЗарплаты.ВидРасчета,
	|	Планирование_ВыполнениеПлановПоУЗПРасчетЗарплаты.Сотрудник.КатегорияРаботника.СтатьяЗатратПоЗарплате,
	|	Планирование_ВыполнениеПлановПоУЗПРасчетЗарплаты.Ссылка.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.Сотрудник,
	|	ЗНАЧЕНИЕ(ПланСчетов.Внутренний.Зарплата) КАК СчетУчета,
	|	ВТ_ДанныеДокумента.Результат КАК Сумма
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.СтатьяЗатрат,
	|	СУММА(ВТ_ДанныеДокумента.Результат) КАК СуммаРасходов
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.СтатьяЗатрат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.Сотрудник,
	|	ВТ_ДанныеДокумента.ПериодРегистрации,
	|	ВТ_ДанныеДокумента.ВидРасчета,
	|	ВТ_ДанныеДокумента.Результат
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента";
	
	#КонецОбласти
	
	запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	результатЗапроса = запрос.ВыполнитьПакет();
	
	Движения.ВзаиморасчетыССотрудниками.Загрузить(результатЗапроса[1].Выгрузить());
	Движения.ФинансовыйРезультат.Загрузить(результатЗапроса[2].Выгрузить());
	Движения.РасчетыПоОплатеТруда.Загрузить(результатЗапроса[3].Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеПоОснованию

Процедура ЗаполнитьДокументПоОтбору(Знач данныеЗаполнения)
	
КонецПроцедуры	

Процедура ЗаполнитьДокументНаОснованииУстановкиПлановПоУЗП(Знач данныеЗаполнения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Заполнение реквизитов документа.
	ЗаполнениеОбъектовСервер.ЗаполнитьРеквизитыДокументаПриВводеНаОсновании(ЭтотОбъект, данныеЗаполнения);
	
	запрос = Новый Запрос;
	
	#Область ТекстЗапроса
	
	запрос.Текст =
	"ВЫБРАТЬ
	|	Планирование_УстановкаПлановПоУЗПСостав.Подразделение,
	|	Планирование_УстановкаПлановПоУЗПСостав.СегментПартнеров,
	|	0 КАК Факт,
	|	0 КАК НеВыполненНорматив
	|ПОМЕСТИТЬ ВТ_ИсодныеДанные
	|ИЗ
	|	Документ.Планирование_УстановкаПлановПоУЗП.Состав КАК Планирование_УстановкаПлановПоУЗПСостав
	|ГДЕ
	|	Планирование_УстановкаПлановПоУЗПСостав.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ИсодныеДанные.Подразделение,
	|	ВТ_ИсодныеДанные.СегментПартнеров,
	|	ВТ_ИсодныеДанные.Факт,
	|	ВТ_ИсодныеДанные.НеВыполненНорматив
	|ИЗ
	|	ВТ_ИсодныеДанные КАК ВТ_ИсодныеДанные";
	
	#КонецОбласти
	
	запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	запрос.УстановитьПараметр("ДокументОснование", данныеЗаполнения);
	
	результатЗапроса = запрос.ВыполнитьПакет();
		
	ЭтотОбъект.ИсходныеДанные.Загрузить(результатЗапроса[1].Выгрузить());

	
КонецПроцедуры

#КонецОбласти

#КонецОбласти