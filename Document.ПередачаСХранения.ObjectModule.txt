
#Область ОбработчикиСобытий

Процедура ПриКопировании(объектКопирования)
	
	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеСкопированногоДокумента(ЭтотОбъект, объектКопирования);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(данныеЗаполнения, стандартнаяОбработка)
	
	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеНовогоДокумента(ЭтотОбъект, данныеЗаполнения);
	
	Если Подразделение.Пустая() Тогда
		Подразделение = ПользователиСервер.ПолучитьЗначениеНастройки("ОсновноеПодразделение");
	КонецЕсли; 
	
	Если Ответственный.Пустая() Тогда
		Ответственный = ПользователиСервер.ПолучитьЗначениеНастройки("ОсновнойОтветственный");
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ВзаиморасчетыСПокупателями.Записывать = Истина;
	Движения.ФинансовыйРезультат.Записывать = Истина;
	Движения.ТоварыНаОтветственномХранении.Записывать = Истина;
	
	запрос = Новый Запрос;
	
	#Область ТекстЗапроса
	// 1 - "ВзаиморасчетыСПокупателями"
	// 2 - "ФинансовыйРезультат"
	// 3 - "ТоварыНаОтветственномХранении"
	
	запрос.Текст =
	"ВЫБРАТЬ
	|	ПередачаСХраненияСостав.Ссылка.Дата КАК Период,
	|	ПередачаСХраненияСостав.Ссылка.Подразделение,
	|	ПередачаСХраненияСостав.Ссылка.Отправитель КАК МестоХранения,
	|	ПередачаСХраненияСостав.Ярлык,
	|	ПередачаСХраненияСостав.Количество,
	|	ПередачаСХраненияСостав.Ссылка.Получатель.Контрагент КАК Контрагент,
	|	ВалютаУчета.Значение КАК Валюта
	|ПОМЕСТИТЬ ВТ_ДанныеДокумента
	|ИЗ
	|	Документ.ПередачаСХранения.Состав КАК ПередачаСХраненияСостав,
	|	Константа.ВалютаУчета КАК ВалютаУчета
	|ГДЕ
	|	ПередачаСХраненияСостав.Ссылка = &ТекущийДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.Менеджеры.БезМенеджера) КАК Менеджер,
	|	ВТ_ДанныеДокумента.Контрагент,
	|	ВТ_ДанныеДокумента.Валюта,
	|	СУММА(ВТ_ДанныеДокумента.Количество) * &ТарифНаОбслуживание КАК Сумма
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|ГДЕ
	|	ВТ_ДанныеДокумента.Контрагент <> ЗНАЧЕНИЕ(справочник.Контрагенты.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.Контрагент,
	|	ВТ_ДанныеДокумента.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ЗНАЧЕНИЕ(справочник.СтатьиДоходов.УслугиОтветственногоХранения) КАК СтатьяДоходов,
	|	СУММА(ВТ_ДанныеДокумента.Количество) * &ТарифНаОбслуживание КАК СуммаДоходов
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|ГДЕ
	|	ВТ_ДанныеДокумента.Контрагент <> ЗНАЧЕНИЕ(справочник.Контрагенты.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.МестоХранения,
	|	ВТ_ДанныеДокумента.Ярлык,
	|	ВТ_ДанныеДокумента.Количество
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента";
	
	#КонецОбласти
	
	запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);
	
	// определимся с тарифом
	Если Дата >= Дата("20190701") Тогда
		
		тарифНаОбслуживание = 0.3;	
		
	Иначе
		
		Если Получатель.Контрагент.Код = "F00023560" Тогда
			// "Сыророб-Ответхранение"
			тарифНаОбслуживание = 0.3;
		Иначе
			// "Вивальди-Ответхранение"
			тарифНаОбслуживание = 0.25;
		КонецЕсли;
		
	КонецЕсли;
	
	запрос.УстановитьПараметр("ТарифНаОбслуживание", тарифНаОбслуживание);
	
	результатЗапроса = запрос.ВыполнитьПакет();
	
	Движения.ВзаиморасчетыСПокупателями.Загрузить(результатЗапроса[1].Выгрузить());
	Движения.ФинансовыйРезультат.Загрузить(результатЗапроса[2].Выгрузить());
	Движения.ТоварыНаОтветственномХранении.Загрузить(результатЗапроса[3].Выгрузить());
	
КонецПроцедуры

#КонецОбласти