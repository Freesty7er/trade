Функция СформироватьТаблицыДляПроведения(ссылка,  дополнительныеСвойства) Экспорт
	
	запрос = Новый Запрос;
	запрос.МенеджерВременныхТаблиц = дополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	#Область ТекстЗапроса
	//	0 - "ВзаиморасчетыСПоставщиками"
	//	1 - "ПартиииТоваровНаСкладах"
	//	2 - "ТоварыВРознице"
	//	3 - "Имущество"
	//	4 - "ЗаказыПоставщикам"
		
	запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВозвратПоставщику.Дата КАК Период,
	|	ВозвратПоставщику.Подразделение,
	|	ВозвратПоставщику.ЦФО,
	|	ВозвратПоставщику.Контрагент,
	|	-ВозвратПоставщику.СуммаДокумента КАК Сумма
	|ИЗ
	|	Документ.ВозвратПоставщику КАК ВозвратПоставщику
	|ГДЕ
	|	ВозвратПоставщику.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВозвратПоставщикуЗапасы.Ссылка.Дата КАК Период,
	|	ВозвратПоставщикуЗапасы.Ссылка.Подразделение,
	|	ВозвратПоставщикуЗапасы.Ссылка.ЦФО,
	|	ВозвратПоставщикуЗапасы.Ссылка.СтруктурнаяЕдиница КАК Склад,
	|	ВозвратПоставщикуЗапасы.Номенклатура,
	|	-ВозвратПоставщикуЗапасы.Количество КАК Количество,
	|	-1 * (ВозвратПоставщикуЗапасы.Сумма * (1 - ВозвратПоставщикуЗапасы.Ссылка.Скидка / 100)) КАК Стоимость,
	|	ВЫБОР
	|		КОГДА ПараметрыУчетаПоПодразделениямСрезПоследних.ЭтоРозница = ИСТИНА
	|			ТОГДА -ВозвратПоставщикуЗапасы.Количество * ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПродСтоимость,
	|	-1 * (ВозвратПоставщикуЗапасы.Сумма * (1 - ВозвратПоставщикуЗапасы.Ссылка.Скидка / 100)) КАК СтоимостьВал,
	|	ВалютаУчета.Значение КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыХраненияЗапасов.НаСкладе) КАК ВидХранения,
	|	ВозвратПоставщикуЗапасы.Номенклатура.СчетУчета КАК СчетУчета,
	|	ЗНАЧЕНИЕ(ПланСчетов.Внутренний.ВзаиморасчетыСПоставщикамиЗаТовар) КАК КорСчет
	|ИЗ
	|	Документ.ВозвратПоставщику.Запасы КАК ВозвратПоставщикуЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыУчетаПоПодразделениям.СрезПоследних(&МоментВремени, ) КАК ПараметрыУчетаПоПодразделениямСрезПоследних
	|		ПО ВозвратПоставщикуЗапасы.Ссылка.Подразделение = ПараметрыУчетаПоПодразделениямСрезПоследних.СтруктурнаяЕдиница
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&МоментВремени, ) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО ВозвратПоставщикуЗапасы.Ссылка.Подразделение = ЦеныНоменклатурыСрезПоследних.Подразделение
	|			И ВозвратПоставщикуЗапасы.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|			И ВозвратПоставщикуЗапасы.Ссылка.ЦФО.Магазин.ТипЦен = ЦеныНоменклатурыСрезПоследних.ТипЦен,
	|	Константа.ВалютаУчета КАК ВалютаУчета
	|ГДЕ
	|	ВозвратПоставщикуЗапасы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВозвратПоставщикуЗапасы.Ссылка.Дата КАК Период,
	|	ВозвратПоставщикуЗапасы.Ссылка.Подразделение,
	|	ВозвратПоставщикуЗапасы.Ссылка.ЦФО КАК ОтделМагазина,
	|	ВозвратПоставщикуЗапасы.Номенклатура,
	|	-ВозвратПоставщикуЗапасы.Количество КАК Количество,
	|	-ВозвратПоставщикуЗапасы.Количество * ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) КАК Сумма,
	|	-(ВозвратПоставщикуЗапасы.Количество * ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) - ВозвратПоставщикуЗапасы.Сумма * (1 - ВозвратПоставщикуЗапасы.Ссылка.Скидка / 100)) КАК Наценка
	|ИЗ
	|	Документ.ВозвратПоставщику.Запасы КАК ВозвратПоставщикуЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыУчетаПоПодразделениям.СрезПоследних(&МоментВремени, ) КАК ПараметрыУчетаПоПодразделениямСрезПоследних
	|		ПО ВозвратПоставщикуЗапасы.Ссылка.Подразделение = ПараметрыУчетаПоПодразделениямСрезПоследних.СтруктурнаяЕдиница
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&МоментВремени, ) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО ВозвратПоставщикуЗапасы.Ссылка.Подразделение = ЦеныНоменклатурыСрезПоследних.Подразделение
	|			И ВозвратПоставщикуЗапасы.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|			И ВозвратПоставщикуЗапасы.Ссылка.ЦФО.Магазин.ТипЦен = ЦеныНоменклатурыСрезПоследних.ТипЦен
	|ГДЕ
	|	ВозвратПоставщикуЗапасы.Ссылка = &Ссылка
	|	И ПараметрыУчетаПоПодразделениямСрезПоследних.ЭтоРозница = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВозвратПоставщикуИмущество.Ссылка.Дата КАК Период,
	|	ВозвратПоставщикуИмущество.Ссылка.Подразделение КАК Подразделение,
	|	ВозвратПоставщикуИмущество.Номенклатура,
	|	ВозвратПоставщикуИмущество.Номенклатура.СчетУчета КАК СчетУчета,
	|	ВозвратПоставщикуИмущество.Количество,
	|	ВозвратПоставщикуИмущество.Сумма КАК Стоимость,
	|	ИмуществоОстатки.МОЛ КАК МОЛ,
	|	ВозвратПоставщикуИмущество.ЦФО
	|ИЗ
	|	Документ.ВозвратПоставщику.Имущество КАК ВозвратПоставщикуИмущество
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Имущество.Остатки(&МоментВремени, ) КАК ИмуществоОстатки
	|		ПО ВозвратПоставщикуИмущество.Номенклатура = ИмуществоОстатки.Номенклатура
	|			И ВозвратПоставщикуИмущество.ЦФО = ИмуществоОстатки.ЦФО
	|			И ВозвратПоставщикуИмущество.Ссылка.Подразделение = ИмуществоОстатки.Подразделение
	|			И (ИмуществоОстатки.КоличествоОстаток > 0)
	|ГДЕ
	|	ВозвратПоставщикуИмущество.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВозвратПоставщикуЗапасы.Ссылка.Дата КАК Период,
	|	ВозвратПоставщикуЗапасы.Ссылка.Подразделение,
	|	ВозвратПоставщикуЗапасы.Ссылка.Контрагент,
	|	ВозвратПоставщикуЗапасы.Номенклатура.Родитель.Склад КАК Склад,
	|	ВозвратПоставщикуЗапасы.Номенклатура,
	|	ВозвратПоставщикуЗапасы.Ссылка.ДокОсн КАК ЗаказПоставщику,
	|	-ВозвратПоставщикуЗапасы.Количество КАК Получено
	|ИЗ
	|	Документ.ВозвратПоставщику.Запасы КАК ВозвратПоставщикуЗапасы
	|ГДЕ
	|	ВозвратПоставщикуЗапасы.Ссылка = &Ссылка
	|	И ВозвратПоставщикуЗапасы.Ссылка.ДокОсн <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	И ВозвратПоставщикуЗапасы.Количество <> 0";
	
	#КонецОбласти
	
	запрос.УстановитьПараметр("Ссылка", ссылка);
	запрос.УстановитьПараметр("МоментВремени", дополнительныеСвойства.ДляПроведения.МоментВремени);
	
	Возврат запрос.ВыполнитьПакет();
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ФОРМЫ

Функция ПечатнаяФорма(массивОбъектов, объектыПечати, имяМакета, коллекцияПечатныхФорм)
	
	УстановитьПривилегированныйРежим(Истина);
	
	табличныйДокумент = Новый ТабличныйДокумент;
	табличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ВозвратПоставщику";
	
	первыйДокумент = Истина;
	
	Для Каждого текущийДокумент Из массивОбъектов Цикл
		
		Если Не первыйДокумент Тогда
			табличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		первыйДокумент = Ложь;
		
		номерСтрокиНачало = табличныйДокумент.ВысотаТаблицы + 1;
		
		Если имяМакета = "Накладная" Тогда
			
			запрос = Новый Запрос();
			
			#Область ТектсЗапроса
			
			запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ВозвратПоставщику.Ссылка,
			|	ВЫБОР
			|		КОГДА ВозвратПоставщику.Организация.ФинУчет
			|			ТОГДА """"
			|		ИНАЧЕ ВозвратПоставщику.СтруктурнаяЕдиница.Наименование
			|	КОНЕЦ КАК Склад,
			|	ВозвратПоставщику.Контрагент.ПолнНаим КАК КонтрагентПолноеНаименование,
			|	ВЫБОР
			|		КОГДА ВозвратПоставщику.Организация.ФинУчет
			|			ТОГДА """"
			|		ИНАЧЕ ВозвратПоставщику.Комментарий
			|	КОНЕЦ КАК Основание,
			|	ВозвратПоставщику.Номер КАК НомерДокумента,
			|	ВозвратПоставщику.Дата КАК ДатаДокумента,
			|	ВозвратПоставщику.Контрагент.ПолнНаим КАК ПоставщикНаименование,
			|	ВЫБОР
			|		КОГДА ВозвратПоставщику.Организация.ФинУчет = ИСТИНА
			|			ТОГДА ВозвратПоставщику.Организация.Наименование
			|		ИНАЧЕ ВозвратПоставщику.Подразделение.Наименование
			|	КОНЕЦ КАК ПокупательНаименование,
			|	ВозвратПоставщику.Организация,
			|	ВозвратПоставщику.Организация.ЮрАдрес,
			|	ВозвратПоставщику.Организация.ОКПО,
			|	ВозвратПоставщику.Организация.ИНН,
			|	ВЫБОР
			|		КОГДА ОсновныеЗначенияПодчинённыхДанных.ОсновноеЗначение ЕСТЬ NULL 
			|			ТОГДА ЕСТЬNULL(ПараметрыУчетаПоПодразделениямСрезПоследних.ОсновнойБанковскийСчет.НаименованиеПолное, """")
			|		ИНАЧЕ ОсновныеЗначенияПодчинённыхДанных.ОсновноеЗначение.НаименованиеПолное
			|	КОНЕЦ КАК РасчетныйСчетСтр,
			|	ВЫБОР
			|		КОГДА ВозвратПоставщику.Организация.ФинУчет
			|				И ВозвратПоставщику.Организация.ОКПО <> """"
			|			ТОГДА &ОрганизацияТекстОКПО
			|		ИНАЧЕ """"
			|	КОНЕЦ КАК ОрганизацияТекстОКПО,
			|	ВЫБОР
			|		КОГДА ВозвратПоставщику.Организация.ФинУчет
			|				И ВозвратПоставщику.Организация.ОКПО <> """"
			|			ТОГДА &ОрганизацияЗапятаяПробел
			|		ИНАЧЕ """"
			|	КОНЕЦ КАК ОрганизацияЗапятаяПробел,
			|	ВозвратПоставщику.Контрагент.ЮрАдрес,
			|	ВозвратПоставщику.Контрагент.ОКПО,
			|	ВозвратПоставщику.Контрагент.ИНН,
			|	ВЫБОР
			|		КОГДА ВозвратПоставщику.Организация.ФинУчет
			|				И ВозвратПоставщику.Контрагент.ОКПО <> """"
			|			ТОГДА &КонтрагентТекстОКПО
			|		ИНАЧЕ """"
			|	КОНЕЦ КАК КонтрагентТекстОКПО,
			|	ВЫБОР
			|		КОГДА ВозвратПоставщику.Организация.ФинУчет
			|				И ВозвратПоставщику.Контрагент.ОКПО <> """"
			|			ТОГДА &КонтрагентЗапятаяПробел
			|		ИНАЧЕ """"
			|	КОНЕЦ КАК КонтрагентЗапятаяПробел,
			|	ВЫБОР
			|		КОГДА ОсновныеЗначенияПодчинённыхДанныхДляКонтрагент.ОсновноеЗначение ЕСТЬ NULL 
			|			ТОГДА """"
			|		ИНАЧЕ ОсновныеЗначенияПодчинённыхДанныхДляКонтрагент.ОсновноеЗначение.НаименованиеПолное
			|	КОНЕЦ КАК КонтрагентРасчетныйСчетСтр
			|ИЗ
			|	Документ.ВозвратПоставщику КАК ВозвратПоставщику
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеЗначенияПодчинённыхДанных КАК ОсновныеЗначенияПодчинённыхДанных
			|		ПО ВозвратПоставщику.Подразделение = ОсновныеЗначенияПодчинённыхДанных.Подразделение
			|			И ВозвратПоставщику.Организация = ОсновныеЗначенияПодчинённыхДанных.Объект
			|			И (ОсновныеЗначенияПодчинённыхДанных.Вид = ЗНАЧЕНИЕ(Перечисление.ВидыОсновныхЗначенийПодчинённыхДанных.БанковскийСчётОрганизации))
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыУчетаПоПодразделениям.СрезПоследних КАК ПараметрыУчетаПоПодразделениямСрезПоследних
			|		ПО ВозвратПоставщику.Подразделение = ПараметрыУчетаПоПодразделениямСрезПоследних.СтруктурнаяЕдиница
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеЗначенияПодчинённыхДанных КАК ОсновныеЗначенияПодчинённыхДанныхДляКонтрагент
			|		ПО ВозвратПоставщику.Подразделение = ОсновныеЗначенияПодчинённыхДанныхДляКонтрагент.Подразделение
			|			И ВозвратПоставщику.Контрагент = ОсновныеЗначенияПодчинённыхДанныхДляКонтрагент.Объект
			|			И (ОсновныеЗначенияПодчинённыхДанныхДляКонтрагент.Вид = ЗНАЧЕНИЕ(Перечисление.ВидыОсновныхЗначенийПодчинённыхДанных.БанковскийСчётКонтрагента))
			|ГДЕ
			|	ВозвратПоставщику.Ссылка = &ТекущийДокумент
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВозвратПоставщикуЗапасы.НомерСтроки КАК НомерСтроки,
			|	ВозвратПоставщикуЗапасы.Номенклатура.Код,
			|	ВозвратПоставщикуЗапасы.Номенклатура.ПолнНаим КАК НоменклатураНаименование,
			|	ВозвратПоставщикуЗапасы.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	ВозвратПоставщикуЗапасы.Количество,
			|	ВозвратПоставщикуЗапасы.Цена КАК ЦенаСНДС,
			|	ВозвратПоставщикуЗапасы.Сумма КАК СуммаСНДС,
			|	ВЫРАЗИТЬ(ВозвратПоставщикуЗапасы.Сумма / 6 КАК ЧИСЛО(10, 2)) КАК СуммаНДС
			|ИЗ
			|	Документ.ВозвратПоставщику.Запасы КАК ВозвратПоставщикуЗапасы
			|ГДЕ
			|	ВозвратПоставщикуЗапасы.Ссылка = &ТекущийДокумент
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ВозвратПоставщикуИмущество.НомерСтроки,
			|	ВозвратПоставщикуИмущество.Номенклатура.Код,
			|	ВозвратПоставщикуИмущество.Номенклатура.Наименование,
			|	ВозвратПоставщикуИмущество.Номенклатура.ЕдиницаИзмерения,
			|	ВозвратПоставщикуИмущество.Количество,
			|	ВозвратПоставщикуИмущество.Цена,
			|	ВозвратПоставщикуИмущество.Сумма,
			|	0
			|ИЗ
			|	Документ.ВозвратПоставщику.Имущество КАК ВозвратПоставщикуИмущество
			|ГДЕ
			|	ВозвратПоставщикуИмущество.Ссылка = &ТекущийДокумент
			|
			|УПОРЯДОЧИТЬ ПО
			|	НомерСтроки";
			
			#КонецОбласти
			
			запрос.УстановитьПараметр("ТекущийДокумент", текущийДокумент);
			
			запрос.УстановитьПараметр("ОрганизацияТекстОКПО", "код за ЄДРПОУ ");
			запрос.УстановитьПараметр("ОрганизацияЗапятаяПробел", ", ");
			запрос.УстановитьПараметр("КонтрагентТекстОКПО", "код за ЄДРПОУ ");
			запрос.УстановитьПараметр("КонтрагентЗапятаяПробел", ", ");
			
			результатЗапроса = запрос.ВыполнитьПакет();
			
			Если результатЗапроса[0].Пустой() Тогда
				Продолжить;
			КонецЕсли;
			
			шапка = результатЗапроса[0].Выгрузить()[0];
			
			номерДокумента = Шапка.НомерДокумента;
			
			выборкаСтрокДокумента = результатЗапроса[1].Выбрать();
			
			табличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ВозвратПоставщику_Накладная";
			
			
			макет = УправлениеПечатью.ПолучитьМакет("Документ.ВозвратПоставщику.Накладная_20170801");
			
			
			кодЯзыкаПечать = Локализация.ПолучитьЯзыкФормированияПечатныхФорм(шапка.Организация);
			макет.КодЯзыкаМакета = КодЯзыкаПечать;
			
			областьМакета = макет.ПолучитьОбласть("Шапка");
			
			ЗаполнитьЗначенияСвойств(областьМакета.Параметры, шапка);
			областьМакета.Параметры.ДатаДокумента = Формат(шапка.ДатаДокумента, "Л="+Локализация.ОпределитьКодЯзыкаДляФормат(кодЯзыкаПечать)+"; ДЛФ=DD");
			
			табличныйДокумент.Вывести(областьМакета);
			
			областьМакета = макет.ПолучитьОбласть("ШапкаТаблицы");
			табличныйДокумент.Вывести(областьМакета);
			
			итСумма			= 0;
			итКоличество	= 0;
			итСуммаНДС 		= 0;
			количествоСтрок = 0;
			
			областьМакета = макет.ПолучитьОбласть("СтрокаТаблицы");

			Пока выборкаСтрокДокумента.Следующий() Цикл
				
				областьМакета.Параметры.Заполнить(выборкаСтрокДокумента);
				//областьМакета.Параметры.НоменклатураКод = Прав(ВыборкаСтрокДокумента.НоменклатураКод, 5);
				
				табличныйДокумент.Вывести(областьМакета);
				
				итКоличество	= итКоличество + выборкаСтрокДокумента.Количество;
				итСумма			= итСумма + выборкаСтрокДокумента.СуммаСНДС;
				итСуммаНДС		= итСуммаНДС + выборкаСтрокДокумента.СуммаНДС;
				количествоСтрок = количествоСтрок + 1;
				
			КонецЦикла;
			
			областьМакета = макет.ПолучитьОбласть("Подвал");
			
			областьМакета.Параметры.ИтВсегоПрописью		= ОбщегоНазначенияСервер.СформироватьСуммуПрописью(итСумма);
			областьМакета.Параметры.ИтСуммаНДСПрописью  = ОбщегоНазначенияСервер.СформироватьСуммуПрописью(итСуммаНДС);
			
			областьМакета.Параметры.Количество = итКоличество;
			областьМакета.Параметры.СуммаВсего = итСумма;
			областьМакета.Параметры.СуммаНДС = итСуммаНДС;
			областьМакета.Параметры.КоличествоСтрок = количествоСтрок;
			
			табличныйДокумент.Вывести(областьМакета);
				
				
			табличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
			
		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(табличныйДокумент, номерСтрокиНачало, объектыПечати, текущийДокумент);
		
	КонецЦикла;
	
	табличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат табличныйДокумент;
	
КонецФункции // ПечатнаяФорма()

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(массивОбъектов,
				 параметрыПечати,
				 коллекцияПечатныхФорм,
				 объектыПечати,
				 параметрыВывода) Экспорт
	
	параметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(коллекцияПечатныхФорм, "Накладная") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(коллекцияПечатныхФорм, "Накладная", "Возврат поставщику", печатнаяФорма(массивОбъектов, объектыПечати, "Накладная", коллекцияПечатныхФорм));
	КонецЕсли;
	
	
КонецПроцедуры


