
#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КлючОбъекта = Параметры.КлючОбъекта;
	КлючТекущихНастроек = Параметры.КлючТекущихНастроек;
	
	СписокНастроек.Параметры.УстановитьЗначениеПараметра("КлючОбъекта", КлючОбъекта);
	СписокНастроек.Параметры.УстановитьЗначениеПараметра("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	
	Если ЗначениеЗаполнено(КлючТекущихНастроек) Тогда
		Элементы.СписокНастроек.ТекущаяСтрока = КлючТекущихНастроек;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНастроекВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбратьНастройкуОтчёта();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНастроекПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокНастроекПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	мНастройкиОтчёта = Новый Массив;
	Для каждого ВыделеннаяСтрока Из Элемент.ВыделенныеСтроки Цикл
	
		Если ТипЗнч(ВыделеннаяСтрока) <> Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
			мНастройкиОтчёта.Добавить(ВыделеннаяСтрока);
		КонецЕсли; 
	
	КонецЦикла; 
	
	Если мНастройкиОтчёта.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выделено ни одного элемента который можно удалить.'"));
	Иначе
		ОтчётыКлиент.ПоказатьВопросОбУдаленииНастроекОтчётов(мНастройкиОтчёта, Элемент);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ДействияКоманд

&НаКлиенте
Процедура Выбрать(Команда)
	ВыбратьНастройкуОтчёта();
КонецПроцедуры

#КонецОбласти

#Область ДополнительныеПроцедурыИФункции

// Инициирует выбор настройки отчёта из информационной базы.
//
&НаКлиенте
Процедура ВыбратьНастройкуОтчёта()

	ТекущаяСтрока = Элементы.СписокНастроек.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Закрыть();
	ИначеЕсли ТипЗнч(ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда 
		Возврат;	// Ничего не делаем.
	Иначе
		Закрыть(Новый ВыборНастроек(ТекущаяСтрока));
	КонецЕсли; 

КонецПроцедуры // ВыбратьНастройкуОтчёта()

#КонецОбласти
