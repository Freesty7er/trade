&НаСервере
Функция ПолучитьДатуИзИмениФайла(ИмяФайла)
	
	ДатаРезультат = Неопределено;
	
	Год = Число(Сред(ИмяФайла, 1, 4));
	Месяц = Число(Сред(ИмяФайла, 6, 2)); 
	День = Число(Сред(ИмяФайла, 9, 2)); 
	
	ДатаРезультат = Дата(Год,Месяц, День);
	
	Возврат ДатаРезультат; 
	
КонецФункции


&НаСервере
Процедура ПрочитатьФайлПротоколаОбмена()
	
	ПодробныйЛог = Новый ТекстовыйДокумент;                                           
	
	КаталогЖурналаОбмена = Константы.Р_КаталогХраненияЖурналовОбменаПОС.Получить()+ "\" + СокрЛП(ПОС.ИД_ПОС) + "\" + Строка(ТипПО) +"\";
	МассивНайденныхФайлов = НайтиФайлы(КаталогЖурналаОбмена, "*.txt", Истина);
	
	ФайлыЖурнала = Новый ТаблицаЗначений;
	ФайлыЖурнала.Колонки.Добавить("Файл");
	ФайлыЖурнала.Колонки.Добавить("ИмяФайла", ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(100));
	
	Для Каждого ТекСтрока Из МассивНайденныхФайлов Цикл 
		
		ИмяФайла  = ТекСтрока.ИмяБезРасширения;
		ДатаФайла = ПолучитьДатуИзИмениФайла(ИмяФайла);
		
		Если ЗначениеЗаполнено(НачПериода) И ЗначениеЗаполнено(КонПериода) Тогда
			Если (ДатаФайла < НачПериода) ИЛИ (ДатаФайла > КонПериода) Тогда
				Продолжить;
			КонецЕсли;
		ИначеЕсли ЗначениеЗаполнено(НачПериода) И Не ЗначениеЗаполнено(КонПериода) Тогда
			Если ДатаФайла < НачПериода Тогда
				Продолжить;
			КонецЕсли;
		ИначеЕсли ЗначениеЗаполнено(КонПериода) Тогда
			Если ДатаФайла > КонПериода Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		НоваяСтрока = ФайлыЖурнала.Добавить();
		НоваяСтрока.Файл = ТекСтрока;
		НоваяСтрока.ИмяФайла = ИмяФайла;
		
	КонецЦикла;
	
	ПолеТекстовогоДокументаПОС.Очистить();
	
	Если ФайлыЖурнала.Количество() = 0 Тогда
		//Если СообщатьНетПротокола Тогда
		//	Предупреждение("Протокол обмена за выбранный период не найден.", 3);
		//КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ФлажокСначалаПоследниеСобытия Тогда
		ФайлыЖурнала.Сортировать("ИмяФайла Убыв");
	Иначе
		ФайлыЖурнала.Сортировать("ИмяФайла Возр");
	КонецЕсли;
	
	СписокОтложенныхСтрок = Новый СписокЗначений;
	
	Для Каждого ТекСтрокаЖ Из ФайлыЖурнала Цикл
		
		ИмяФайлаПодробногоЛога = ТекСтрокаЖ.Файл.ПолноеИмя;
		
		ПодробныйЛог.Прочитать(ИмяФайлаПодробногоЛога);
		
		КвоСтрокЛога = ПодробныйЛог.КоличествоСтрок();
		
		Если ФлажокСначалаПоследниеСобытия Тогда
			НомСтрЛога = КвоСтрокЛога;
		Иначе
			НомСтрЛога = 1;
		КонецЕсли;
		
		Пока (НомСтрЛога <= КвоСтрокЛога) И (НомСтрЛога > 0) Цикл
			
			СтрокаЛога = ПодробныйЛог.ПолучитьСтроку(НомСтрЛога);
			
			Если ФлажокСначалаПоследниеСобытия Тогда
				НомСтрЛога = НомСтрЛога - 1;
			Иначе
				НомСтрЛога = НомСтрЛога + 1;
			КонецЕсли;
			
			СтрДатаЛога = СокрЛП(Лев(СтрокаЛога, 19));
			Если СтрДатаЛога = "" Тогда
				Если ФлажокСначалаПоследниеСобытия Тогда
					СписокОтложенныхСтрок.Добавить(СтрокаЛога);
					Продолжить;
				КонецЕсли
			Иначе
				Попытка
					ДатаЗаписи = Дата(СтрДатаЛога);
					Если ЗначениеЗаполнено(НачПериода) И ДатаЗаписи < НачПериода Тогда
						Продолжить;
					КонецЕсли;
					Если ЗначениеЗаполнено(КонПериода) И ДатаЗаписи > КонПериода Тогда
						Продолжить;
					КонецЕсли
				Исключение
				КонецПопытки
			КонецЕсли;
			
			Если ФлажокСначалаПоследниеСобытия  И (СписокОтложенныхСтрок.Количество() > 0) Тогда
				НомОтлСтр = СписокОтложенныхСтрок.Количество();
				Пока НомОтлСтр > 0 Цикл
					НомОтлСтр = НомОтлСтр - 1;
					ОтлСтрокаЛога = СписокОтложенныхСтрок[НомОтлСтр];
					ПолеТекстовогоДокументаПОС.ДобавитьСтроку(ОтлСтрокаЛога);
				КонецЦикла;
				СписокОтложенныхСтрок.Очистить();
			КонецЕсли;
			
			ПолеТекстовогоДокументаПОС.ДобавитьСтроку(СтрокаЛога);
				
		КонецЦикла
		
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(НачПериода) И Не ЗначениеЗаполнено(КонПериода) Тогда
 		ПолеТекстовогоДокументаПОС.ВставитьСтроку(1, "Журнал обмена за период: <без периода>" );
	ИначеЕсли Не ЗначениеЗаполнено(КонПериода) Тогда
 		ПолеТекстовогоДокументаПОС.ВставитьСтроку(1, "Журнал обмена за период: с " + Строка(НачПериода) + " по текущий момент времени");
	Иначе
 		ПолеТекстовогоДокументаПОС.ВставитьСтроку(1, "Журнал обмена за период: с " + Строка(НачПериода) + " по " + Строка(КонПериода));
	КонецЕсли;
	
	ПолеТекстовогоДокументаПОС.ВставитьСтроку(2, "");
	ПолеТекстовогоДокументаПОС.ВставитьСтроку(3, "Идентификатор POS-терминала: " + СокрЛП(ПОС.ИД_ПОС) + " ("+СокрЛП(ПОС.Наименование) + "), тип ПО """ + Строка(ТипПО) + """");
	ПолеТекстовогоДокументаПОС.ВставитьСтроку(4, "");
	ПолеТекстовогоДокументаПОС.ВставитьСтроку(5, "");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПрочитать(Команда)
	
	Если Не ЗначениеЗаполнено(ПОС) Тогда 
		Предупреждение("Не выбран терминал!", 3);
		Возврат
	КонецЕсли;
	
	ПрочитатьФайлПротоколаОбмена();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаНастройки(Команда)
	
	ОткрытьФорму("Обработка.Р_УправлениеЖурналомОбменаПОС.Форма.ФормаНастроекОбменаУправляемая", , ЭтаФорма, , );
	
КонецПроцедуры
