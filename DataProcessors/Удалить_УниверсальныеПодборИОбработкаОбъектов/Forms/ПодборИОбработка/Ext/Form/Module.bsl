&НаКлиенте
Перем ОбработкаПеретаскивание;

&НаСервере
Функция СформироватьСтруктуруПараметров()
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Настройка",  Неопределено);
	СтруктураПараметров.Вставить("Настройки",  Новый Массив);
	СтруктураПараметров.Вставить("ОбъектПоиска",  ОбъектПоиска);
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Выбрать", Истина);
	СтруктураПараметров.Вставить("НайденныеОбъекты",  НайденныеОбъекты.Выгрузить(СтруктураОтбора, "Объект").ВыгрузитьКолонку("Объект"));
	
	Возврат СтруктураПараметров;
КонецФункции

&НаКлиенте
Функция ПроверитьДоступностьОбработки()
	ИндексСтроки = Элементы.ДоступныеОбработки.ТекущаяСтрока;
	ТекущаяСтрока = ДоступныеОбработки.НайтиПоИдентификатору(ИндексСтроки);
	
	Родитель = ТекущаяСтрока.ПолучитьРодителя();
	Если Родитель = Неопределено Тогда
		Возврат ОбработкаДоступна(ОбъектПоиска.Тип, ТекущаяСтрока.ИмяФормы);
	КонецЕсли;
	
	Возврат ОбработкаДоступна(ОбъектПоиска.Тип, Родитель.ИмяФормы);
КонецФункции

&НаСервере
Процедура СоздатьКолонки(ТаблицаРезультата, МассивРеквизитовПоУмолчанию = Неопределено) Экспорт
	
	ТаблицаЭлемент = Элементы.НайденныеОбъекты;
	
	//очистка
	Для Каждого ДобавленныйЭлемент Из ДобавленныеЭлементы Цикл
		Элементы.Удалить(Элементы[ДобавленныйЭлемент.Значение]);
	КонецЦикла;
	ДобавленныеЭлементы.Очистить();
	
	РеквизитыОбъекта = Метаданные.НайтиПоПолномуИмени(ОбъектПоиска.ПолноеИмя).Реквизиты;
		
	//добавляем реквизиты
	МассивРеквизитов = Новый Массив;
	Для Каждого Колонка Из ТаблицаРезультата.Колонки Цикл
		Если МассивРеквизитовПоУмолчанию <> Неопределено 
			И МассивРеквизитовПоУмолчанию.Найти(Колонка.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТипКолонки = Колонка.ТипЗначения;
		Если Колонка.Имя = "Представление" ИЛИ ТипКолонки = Тип("ХранилищеЗначения") Тогда
			Продолжить;
		КонецЕсли;
		//СистемныеПоля
		Если Колонка.Имя = "СистемныеПоляНомерПоПорядку"  Тогда
			ЗаголовокПоля = "№ п/п";
		Иначе
			Реквизит = РеквизитыОбъекта.Найти(Колонка.Имя);
			Если Реквизит <> Неопределено Тогда
				//ЗаголовокПоля = ?(ЗначениеЗаполнено(Реквизит.Синоним), Реквизит.Синоним + " (" + Реквизит.Имя + ")", Реквизит.Имя);
				ЗаголовокПоля = ?(ЗначениеЗаполнено(Реквизит.Синоним), Реквизит.Синоним, Реквизит.Имя);
			Иначе                  
				ЗаголовокПоля = Колонка.Имя;
			КонецЕсли;  //  
		КонецЕсли; 
		
		РеквизитФормы = Новый РеквизитФормы(Колонка.Имя, ТипКолонки, ТаблицаЭлемент.Имя, ЗаголовокПоля);
		МассивРеквизитов.Добавить(РеквизитФормы);
	КонецЦикла;
	
	ИзменитьРеквизиты(МассивРеквизитов, ДобавленныеРеквизиты.ВыгрузитьЗначения());
	ДобавленныеРеквизиты.Очистить();
	
	//добавляем элементы управления
	Для Каждого Реквизит Из МассивРеквизитов Цикл
		Попытка
			ДобавленныеРеквизиты.Добавить(Реквизит.Путь + "." + Реквизит.Имя);
			
			Элемент = Элементы.Добавить(ТаблицаЭлемент.Имя + Реквизит.Имя, Тип("ПолеФормы"), ТаблицаЭлемент);
			Элемент.Вид = ВидПоляФормы.ПолеВвода;
			Элемент.ПутьКДанным = ТаблицаЭлемент.Имя + "." + Реквизит.Имя;
			Элемент.ТолькоПросмотр = Истина;
			Элемент.РастягиватьПоГоризонтали = Ложь;
			Если Реквизит.Имя = "СистемныеПоляНомерПоПорядку" Тогда
				Элемент.ФиксацияВТаблице = ФиксацияВТаблице.Лево;
				Элемент.Ширина = 3;
			КонецЕсли; 
			
			ДобавленныеЭлементы.Добавить(Элемент.Имя);
		Исключение
		КонецПопытки; 
	КонецЦикла;
	
	//заполнение данными
	НайденныеОбъекты.Очистить();
	Для Каждого СтрокаТаблицы ИЗ ТаблицаРезультата Цикл
		НоваяСтрока = НайденныеОбъекты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

// Возвращает полный путь к форме внешней обработки, отчета или любого объекта метаданных в виде
// ВнешняяОбработка.ИмяВнешнейОбработки.Форма.ИмяФормы
// ВнешнийОтчет.ИмяВнешнегоОтчета.Форма.ИмяФормы
// Документ.ИмяОбъекта.Форма.ИмяФормы
//
// Параметры
//  ИмяФормы  - <Строка> - имя формы объекта
//
// Возвращаемое значение:
//   <Строка>   - полный путь к форме
//
&НаКлиенте
Функция ПолучитьПолноеИмяФормы(ИмяФормы)
	
	СимволТочка = ".";
	ПозицияТочки = СтрДлина(ЭтаФорма.ИмяФормы);
	Пока Сред(ЭтаФорма.ИмяФормы, ПозицияТочки, 1) <> СимволТочка Цикл ПозицияТочки = ПозицияТочки - 1; КонецЦикла; // 
	Возврат Лев(ЭтаФорма.ИмяФормы, ПозицияТочки) + ИмяФормы;
	
КонецФункции

&НаСервере
Процедура ОбработатьРезультатОтбора(РезультатОтбора)
	
	ОтборДанных = РезультатОтбора.Настройки;
	СтрокаПоиска = РезультатОтбора.СтрокаПоиска;
	Элементы.СтрокаПоиска.СписокВыбора.ЗагрузитьЗначения(РезультатОтбора.СтрокаПоискаСписок);
	ПараметрыЗапроса.Загрузить(РезультатОтбора.ПараметрыЗапроса.Выгрузить());
	
	ТекстЗапроса = РезультатОтбора.ТекстЗапроса;
	ТекстПроизвольногоЗапроса = РезультатОтбора.ТекстПроизвольногоЗапроса;
	РежимПоиска = РезультатОтбора.РежимПоиска;
	
	ОбъектыПоиска.Вставить(ОбъектПоиска.Тип + ОбъектПоиска.Имя, ОтборДанных);
	ОбъектыПоиска.Вставить(ОбъектПоиска.Тип + ОбъектПоиска.Имя + "СтрокаПоиска", СтрокаПоиска);
	
КонецПроцедуры

&НаКлиенте
Функция ДобавитьСтроку(ТекСтрока)
		
	НоваяСтрока = ТекСтрока.ПолучитьЭлементы().Добавить();
	
	Настройка = Новый Структура;
	Настройка.Вставить("Обработка", ТекСтрока.Обработка);
	Настройка.Вставить("Прочее", Неопределено);
	
	НоваяСтрока.Настройка.Добавить(Настройка);
	
	Элементы.ДоступныеОбработки.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	Элементы.ДоступныеОбработки.ИзменитьСтроку();
	
	Возврат НоваяСтрока;
КонецФункции

&НаКлиенте
Функция СформироватьНастройки(ТекСтрока)
	
	МассивНастроек = Новый Массив;
	Для Каждого Стр Из ТекСтрока.ПолучитьЭлементы() Цикл
		Если Стр.Настройка[0].Значение = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		МассивНастроек.Добавить(Стр.Настройка[0].Значение);
	КонецЦикла;
	
	Возврат МассивНастроек;
КонецФункции

&НаКлиенте
Процедура ВыбратьОбработки(ИмяТаблицы, Выбор)
	Таблица = ЭтаФорма[ИмяТаблицы];
	Для Каждого Стр Из Таблица Цикл
		Стр.Выбрать = Выбор;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция ОбработкаДоступна(ПроверяемыйТипОбъекта = "", ИмяОбработки)

	Если ПустаяСтрока(ПроверяемыйТипОбъекта) Тогда
		Возврат Ложь;
	КонецЕсли;

	Попытка
		ТипыОбрабатываемыхОбъектов = ПолучитьФорму(ПолучитьПолноеИмяФормы(ИмяОбработки)).мТипыОбрабатываемыхОбъектов;
	Исключение
		Предупреждение(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;

	Если ТипыОбрабатываемыхОбъектов = Неопределено Тогда
    	Возврат Истина;
	Иначе
		Если Найти(ТипыОбрабатываемыхОбъектов, ПроверяемыйТипОбъекта) Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура УстановитьКартинкиОбработок()
	Для Каждого Стр Из ДоступныеОбработки.ПолучитьЭлементы() Цикл
		Стр.Картинка = БиблиотекаКартинок.Обработка;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЭлементы(Выбор)
	Для Каждого Стр Из НайденныеОбъекты Цикл
		Стр.Выбрать = Выбор;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииОбъектаПоиска(ВосстанавливатьНастройки = Истина)
	
	Если ОбъектПоиска = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	
	ТекстЗапроса = ПолучитьТекстЗапроса(ИменаПолейТЧ, ОбъектПоиска);
	ТекстПроизвольногоЗапроса = ТекстЗапроса + Символы.ПС + "АВТОУПОРЯДОЧИВАНИЕ";
	Если ВосстанавливатьНастройки И ОбъектыПоиска.Свойство(ОбъектПоиска.Тип + ОбъектПоиска.Имя) Тогда
		ОтборДанных = ОбъектыПоиска[ОбъектПоиска.Тип + ОбъектПоиска.Имя];
		Если ОбъектыПоиска.Свойство(ОбъектПоиска.Тип + ОбъектПоиска.Имя + "СтрокаПоиска") Тогда
			СтрокаПоиска = ОбъектыПоиска[ОбъектПоиска.Тип + ОбъектПоиска.Имя + "СтрокаПоиска"];
		КонецЕсли; 
	Иначе
		ОтборДанных = Неопределено;
	КонецЕсли; 
	ПараметрыЗапроса.Очистить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьУсловиеПоискаПоСтроке(СтрокаПоиска, ОбъектПоиска)
	УсловиеПоискаПоСтроке = "";

	Если СтрокаПоиска <> "" Тогда
		ИскомыйОбъект = ОбъектПоиска;
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИскомыйОбъект.ПолноеИмя);
		
		УсловиеПоискаПоСтроке = "";

		СтрокаДляПоиска = СтрЗаменить(СтрокаПоиска, """", """""");

		Если ИскомыйОбъект.Тип = "Справочник" Тогда
			Если ОбъектМетаданных.ДлинаНаименования <> 0 Тогда
				Если УсловиеПоискаПоСтроке <> "" Тогда
					УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + " ИЛИ ";
				КонецЕсли;
				УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + " Наименование ПОДОБНО ""%"
									  + СтрокаДляПоиска + "%""";
			КонецЕсли;

			Если ОбъектМетаданных.ДлинаКода <> 0 И ОбъектМетаданных.ТипКода = 
				Метаданные.СвойстваОбъектов.ТипКодаСправочника.Строка Тогда
				Если УсловиеПоискаПоСтроке <> "" Тогда
					УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + " ИЛИ ";
				КонецЕсли;
				УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + " Код ПОДОБНО ""%"
									  + СтрокаДляПоиска + "%""";
			КонецЕсли;
		ИначеЕсли ИскомыйОбъект.Тип = "ПланВидовХарактеристик" ИЛИ ИскомыйОбъект.Тип = "ПланВидовРасчета" ИЛИ ИскомыйОбъект.Тип = "ПланОбмена" Тогда
			Если ОбъектМетаданных.ДлинаНаименования <> 0 Тогда
				Если УсловиеПоискаПоСтроке <> "" Тогда
					УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + " ИЛИ ";
				КонецЕсли;
				УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + " Наименование ПОДОБНО ""%"
									  + СтрокаДляПоиска + "%""";
			КонецЕсли;

			Если ОбъектМетаданных.ДлинаКода <> 0 Тогда
				Если УсловиеПоискаПоСтроке <> "" Тогда
					УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + " ИЛИ ";
				КонецЕсли;
				УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + " Код ПОДОБНО ""%"
									  + СтрокаДляПоиска + "%""";
			КонецЕсли;
		ИначеЕсли ИскомыйОбъект.Тип = "Документ" ИЛИ ИскомыйОбъект.Тип = "БизнесПроцесс" ИЛИ ИскомыйОбъект.Тип = "Задача" Тогда
			Если ОбъектМетаданных.ТипНомера = Метаданные.СвойстваОбъектов.ТипНомераДокумента.Строка И ОбъектМетаданных.ДлинаНомера <> 0 Тогда
				Если УсловиеПоискаПоСтроке <> "" Тогда
					УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + " ИЛИ ";
				КонецЕсли;
				УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + " Номер ПОДОБНО ""%"
									  + СтрокаДляПоиска + "%""";
			КонецЕсли;
			Если ОбъектМетаданных.ТипНомера = Метаданные.СвойстваОбъектов.ТипНомераБизнесПроцесса.Строка И ОбъектМетаданных.ДлинаНомера <> 0 Тогда
				Если УсловиеПоискаПоСтроке <> "" Тогда
					УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + " ИЛИ ";
				КонецЕсли;
				УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + " Номер ПОДОБНО ""%"
									  + СтрокаДляПоиска + "%""";
			КонецЕсли;
			Если ОбъектМетаданных.ТипНомера = Метаданные.СвойстваОбъектов.ТипНомераЗадачи.Строка И ОбъектМетаданных.ДлинаНомера <> 0 Тогда
				Если УсловиеПоискаПоСтроке <> "" Тогда
					УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + " ИЛИ ";
				КонецЕсли;
				УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + " Номер ПОДОБНО ""%"
									  + СтрокаДляПоиска + "%""";
			КонецЕсли;
			Если ИскомыйОбъект.Тип = "Задача" И ОбъектМетаданных.ДлинаНаименования <> 0 Тогда
				Если УсловиеПоискаПоСтроке <> "" Тогда
					УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + " ИЛИ ";
				КонецЕсли;
				УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + " Наименование ПОДОБНО ""%"
				                      + СтрокаДляПоиска + "%""";
			КонецЕсли;
		КонецЕсли;

		Для Каждого Реквизит Из ОбъектМетаданных.Реквизиты Цикл
			Если Реквизит.Тип.Типы().Количество() = 1 И Реквизит.Тип.СодержитТип(Тип("Строка")) Тогда
				Если УсловиеПоискаПоСтроке <> "" Тогда
					УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + " ИЛИ ";
				КонецЕсли;
				УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + Реквизит.Имя + " ПОДОБНО ""%"
									  + СтрокаДляПоиска + "%""";
			КонецЕсли;
		КонецЦикла;
		
		Для каждого ТЧ Из ОбъектМетаданных.ТабличныеЧасти Цикл
			Для каждого ТЧР Из ТЧ.Реквизиты Цикл
				Если Реквизит.Тип.СодержитТип(Тип("Строка")) Тогда
					Если УсловиеПоискаПоСтроке <> "" Тогда
						УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + " ИЛИ ";
					КонецЕсли;
					УсловиеПоискаПоСтроке = УсловиеПоискаПоСтроке + Реквизит.Имя + " ПОДОБНО ""%"
										  + СтрокаДляПоиска + "%""";
				КонецЕсли;
			КонецЦикла; 
		КонецЦикла;
		
	КонецЕсли;

	Возврат УсловиеПоискаПоСтроке;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьТекстЗапроса(ИменаПолейТЧ, ОбъектПоиска)
    	
	ИскомыйОбъект = ОбъектПоиска;
	ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИскомыйОбъект.ПолноеИмя);
	Условие = "";

	ТекстЗапроса = "ВЫБРАТЬ 
	|	Ссылка КАК Объект, 
	|	Представление";
	
	Если ИскомыйОбъект.Тип = "Справочник" Тогда
		Если ОбъектМетаданных.ДлинаНаименования <> 0 Тогда
			ТекстЗапроса = ТекстЗапроса + ", 
			|	Ссылка.Наименование КАК Наименование";
		КонецЕсли;
		Если ОбъектМетаданных.ОсновноеПредставление <> Метаданные.СвойстваОбъектов.ОсновноеПредставлениеСправочника.ВВидеНаименования Тогда
			Если ОбъектМетаданных.ДлинаКода <> 0 Тогда
				Условие = "Код";
			КонецЕсли;
		КонецЕсли;
		Если ОбъектМетаданных.ДлинаКода <> 0 Тогда
			ТекстЗапроса = ТекстЗапроса + ",
			|	Ссылка.Код КАК Код";
		КонецЕсли;
		Если ОбъектМетаданных.ОсновноеПредставление <> Метаданные.СвойстваОбъектов.ОсновноеПредставлениеСправочника.ВВидеКода Тогда
			Если ОбъектМетаданных.ДлинаНаименования <> 0 Тогда
				Условие = "Наименование";
			КонецЕсли;
		КонецЕсли;
		Если ОбъектМетаданных.Иерархический Тогда
				ТекстЗапроса = ТекстЗапроса + ",
				|	Ссылка.Родитель КАК Родитель";
				Условие = Условие + ", " + Символы.ПС + "Родитель.*";
		КонецЕсли;
		Если ОбъектМетаданных.Владельцы.Количество() > 0 Тогда
				ТекстЗапроса = ТекстЗапроса + ",
				|	Ссылка.Владелец КАК Владелец";
				Условие = Условие + ", " + Символы.ПС + "Владелец.*";
		КонецЕсли;
	ИначеЕсли ИскомыйОбъект.Тип =  "ПланВидовХарактеристик"  Тогда
		Если ОбъектМетаданных.ДлинаНаименования <> 0 Тогда
			ТекстЗапроса = ТекстЗапроса + ", 
			|	Ссылка.Наименование КАК Наименование";
		КонецЕсли;
		Если ОбъектМетаданных.ОсновноеПредставление <> Метаданные.СвойстваОбъектов.ОсновноеПредставлениеВидаХарактеристики.ВВидеНаименования Тогда
			Если ОбъектМетаданных.ДлинаКода <> 0 Тогда
				Условие = "Код";
			КонецЕсли;
		КонецЕсли;
		Если ОбъектМетаданных.ДлинаКода <> 0 Тогда
			ТекстЗапроса = ТекстЗапроса + ",
			|	Ссылка.Код КАК Код";
		КонецЕсли;
		Если ОбъектМетаданных.ОсновноеПредставление <> Метаданные.СвойстваОбъектов.ОсновноеПредставлениеВидаХарактеристики.ВВидеКода Тогда
			Если ОбъектМетаданных.ДлинаНаименования <> 0 Тогда
				Условие = "Наименование";
			КонецЕсли;
		КонецЕсли;
		Если ОбъектМетаданных.Иерархический Тогда
				ТекстЗапроса = ТекстЗапроса + ",
				|	Ссылка.Родитель КАК Родитель";
		КонецЕсли;
	ИначеЕсли ИскомыйОбъект.Тип =  "ПланВидовРасчета"  Тогда
		Если ОбъектМетаданных.ДлинаНаименования <> 0 Тогда
			ТекстЗапроса = ТекстЗапроса + ", 
			|	Ссылка.Наименование КАК Наименование";
		КонецЕсли;
		Если ОбъектМетаданных.ОсновноеПредставление <> Метаданные.СвойстваОбъектов.ОсновноеПредставлениеВидаРасчета.ВВидеНаименования Тогда
			Если ОбъектМетаданных.ДлинаКода <> 0 Тогда
				Условие = "Код";
			КонецЕсли;
		КонецЕсли;
		Если ОбъектМетаданных.ДлинаКода <> 0 Тогда
			ТекстЗапроса = ТекстЗапроса + ",
			|	Ссылка.Код КАК Код";
		КонецЕсли;
		Если ОбъектМетаданных.ОсновноеПредставление <> Метаданные.СвойстваОбъектов.ОсновноеПредставлениеВидаРасчета.ВВидеКода Тогда
			Если ОбъектМетаданных.ДлинаНаименования <> 0 Тогда
				Условие = "Наименование";
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ИскомыйОбъект.Тип =  "ПланОбмена"  Тогда
		Если ОбъектМетаданных.ДлинаНаименования <> 0 Тогда
			ТекстЗапроса = ТекстЗапроса + ", 
			|	Ссылка.Наименование КАК Наименование";
		КонецЕсли;
		Если ОбъектМетаданных.ОсновноеПредставление <> Метаданные.СвойстваОбъектов.ОсновноеПредставлениеПланаОбмена.ВВидеНаименования Тогда
			Если ОбъектМетаданных.ДлинаКода <> 0 Тогда
				Условие = "Код";
			КонецЕсли;
		КонецЕсли;
		Если ОбъектМетаданных.ДлинаКода <> 0 Тогда
			ТекстЗапроса = ТекстЗапроса + ",
			|	Ссылка.Код КАК Код";
		КонецЕсли;
		Если ОбъектМетаданных.ОсновноеПредставление <> Метаданные.СвойстваОбъектов.ОсновноеПредставлениеПланаОбмена.ВВидеКода Тогда
			Если ОбъектМетаданных.ДлинаНаименования <> 0 Тогда
				Условие = "Наименование";
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ИскомыйОбъект.Тип = "Документ" ИЛИ ИскомыйОбъект.Тип = "БизнесПроцесс" Тогда
		Если ОбъектМетаданных.ДлинаНомера <> 0 Тогда
			ТекстЗапроса = ТекстЗапроса + ", 
			|	Ссылка.Дата КАК Дата,
			|	Ссылка.Номер КАК Номер";
			Условие = "Номер";
		Иначе                  
			ТекстЗапроса = ТекстЗапроса + ", 
			|	Ссылка.Дата КАК Дата";
		КонецЕсли;  //  
	ИначеЕсли ИскомыйОбъект.Тип = "Задача" Тогда
		Если ОбъектМетаданных.ДлинаНомера <> 0 Тогда
			ТекстЗапроса = ТекстЗапроса + ", 
			|	Ссылка.Наименование КАК Наименование,
			|	Ссылка.Номер КАК Номер";
			Условие = "Номер, Наименование";
		Иначе                  
			ТекстЗапроса = ТекстЗапроса + ", 
			|	Ссылка.Наименование КАК Наименование";
			Условие = "Наименование";
		КонецЕсли;  //  
	КонецЕсли;
		
	Если ИскомыйОбъект.Тип = "Справочник" ИЛИ ИскомыйОбъект.Тип = "ПланВидовХарактеристик" Тогда
		Если ОбъектМетаданных.Иерархический И ОбъектМетаданных.ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов Тогда
			ТекстЗапроса = ТекстЗапроса + ", 
			|	ВЫБОР КОГДА Ссылка.ЭтоГруппа ТОГДА 
			|		ВЫБОР КОГДА Ссылка.ПометкаУдаления ТОГДА 3 ИНАЧЕ 0 КОНЕЦ 
			|	ИНАЧЕ 
			|		ВЫБОР КОГДА Ссылка.ПометкаУдаления ТОГДА 4 ИНАЧЕ 1 КОНЕЦ 
			|	КОНЕЦ КАК Картинка";
		Иначе
			ТекстЗапроса = ТекстЗапроса + ", 
			|	ВЫБОР КОГДА Ссылка.ПометкаУдаления ТОГДА 4 ИНАЧЕ 1 КОНЕЦ КАК Картинка";
		КонецЕсли;
	ИначеЕсли ИскомыйОбъект.Тип = "ПланВидовРасчета" ИЛИ ИскомыйОбъект.Тип = "ПланОбмена" Тогда
		ТекстЗапроса = ТекстЗапроса + ", 
		|	ВЫБОР КОГДА Ссылка.ПометкаУдаления ТОГДА 4 ИНАЧЕ 1 КОНЕЦ  КАК Картинка";
	ИначеЕсли ИскомыйОбъект.Тип = "Документ" Тогда
		ТекстЗапроса = ТекстЗапроса + ", 
		|	ВЫБОР КОГДА Ссылка.Проведен ТОГДА 
		|		7 
		|	ИНАЧЕ 
		|		ВЫБОР КОГДА Ссылка.ПометкаУдаления ТОГДА 8 ИНАЧЕ 6 КОНЕЦ 
		|	КОНЕЦ КАК Картинка";
	Иначе
		ТекстЗапроса = ТекстЗапроса + ", 
		|	ВЫБОР КОГДА Ссылка.ПометкаУдаления ТОГДА 8 ИНАЧЕ 6 КОНЕЦ  КАК Картинка";
	КонецЕсли;
		
	ТипХранилище = Тип("ХранилищеЗначения");
	Для Каждого Реквизит Из ОбъектМетаданных.Реквизиты Цикл
		Если НЕ Реквизит.Тип.СодержитТип(ТипХранилище) Тогда
			ТекстЗапроса = ТекстЗапроса + ",
			|	" + Реквизит.Имя;
		КонецЕсли; 
	КонецЦикла; 

	ИменаПолейТЧ.Очистить();
	Если ОбъектМетаданных.ТабличныеЧасти.Количество() > 0 Тогда
		
		ТипЧисло = Тип("Число");
		АгрегатныеФункции = Новый Массив;
		АгрегатныеФункции.Добавить("Сумма");
		АгрегатныеФункции.Добавить("Минимум");
		АгрегатныеФункции.Добавить("Максимум");
		АгрегатныеФункции.Добавить("Среднее");
		
		Для каждого ТабличнаяЧасть Из ОбъектМетаданных.ТабличныеЧасти Цикл
			
			ИмяПоля = ТабличнаяЧасть.Имя + "КоличествоСтрок";
			ТекстЗапроса = ТекстЗапроса + ",
			|	ТЧ" + ТабличнаяЧасть.Имя + ".КоличествоСтрок КАК " + ИмяПоля;
			ИменаПолейТЧ.Добавить(Новый ПолеКомпоновкиДанных(ИмяПоля));
			
			Для каждого Реквизит Из ТабличнаяЧасть.Реквизиты Цикл    
				Если Реквизит.Тип.Типы().Количество() = 1 И Реквизит.Тип.СодержитТип(ТипЧисло) Тогда
					Для каждого ЭлементМассива Из АгрегатныеФункции Цикл
						ИмяПоля = ТабличнаяЧасть.Имя + Реквизит.Имя + ЭлементМассива;
						ТекстЗапроса = ТекстЗапроса + ",
						|	ТЧ" + ТабличнаяЧасть.Имя + "."+ Реквизит.Имя + ЭлементМассива + " КАК " + ИмяПоля;
						ИменаПолейТЧ.Добавить(Новый ПолеКомпоновкиДанных(ИмяПоля));
					КонецЦикла; //Для каждого ЭлементМассива Из  
				КонецЕсли; 
			КонецЦикла; //Для каждого РеквизитТЧ Из   
			
		КонецЦикла;
		
	КонецЕсли; 

	ТекстЗапроса = ТекстЗапроса + Символы.ПС + "ИЗ" + Символы.ПС;
	ТекстЗапроса = ТекстЗапроса + "	" + ИскомыйОбъект.ПолноеИмя + " КАК _Таблица" + Символы.ПС;
	
	Если ОбъектМетаданных.ТабличныеЧасти.Количество() > 0 Тогда
		ШаблонСтрокиДляСоединенияТЧ = "
			|ЛЕВОЕ СОЕДИНЕНИЕ 
			|	(ВЫБРАТЬ
			|		ОбъектТЧ.Ссылка КАК %ТЧ%Ссылка,
			|		КОЛИЧЕСТВО(ОбъектТЧ.НомерСтроки) КАК КоличествоСтрок //АгрегатныеПоля
			|	ИЗ
			|		%Тип%.%Имя%.%ТЧ% КАК ОбъектТЧ
			|	
			|	СГРУППИРОВАТЬ ПО
			|		ОбъектТЧ.Ссылка
			|	) КАК ТЧ%ТЧ%
			|	ПО (ТЧ%ТЧ%.%ТЧ%Ссылка = _Таблица.Ссылка)	
			|";
		
		Для каждого ТабличнаяЧасть Из ОбъектМетаданных.ТабличныеЧасти Цикл
			ТекстЗапроса = ТекстЗапроса + ШаблонСтрокиДляСоединенияТЧ;
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ТЧ%", ТабличнаяЧасть.Имя);
			
			СтрокаДляАгрегатныхПолей = "";
			Для каждого Реквизит Из ТабличнаяЧасть.Реквизиты Цикл    
				Если Реквизит.Тип.Типы().Количество() = 1 И Реквизит.Тип.СодержитТип(ТипЧисло) Тогда
					Для каждого ЭлементМассива Из АгрегатныеФункции Цикл
						СтрокаДляАгрегатныхПолей = СтрокаДляАгрегатныхПолей + ",
						|		" + ВРег(ЭлементМассива) + "(ОбъектТЧ." + Реквизит.Имя + ") КАК " + Реквизит.Имя + ЭлементМассива;
					КонецЦикла;
				КонецЕсли; 
			КонецЦикла; //Для каждого РеквизитТЧ Из   
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//АгрегатныеПоля", СтрокаДляАгрегатныхПолей);
		КонецЦикла;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//АгрегатныеПоля", "");
	КонецЕсли; 
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%Тип%", ИскомыйОбъект.Тип);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%Имя%", ОбъектМетаданных.Имя);
	
	Возврат ТекстЗапроса;
КонецФункции

&НаСервере
Процедура НайтиСсылкиПоОтбору()
	
	ЭтотОбъект = ЭтотОбъект();
	
	МассивРеквизитов = Новый Массив;
	
	Если РежимПоиска = 1 Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстПроизвольногоЗапроса;
		Для Каждого СтрокаПараметров Из ПараметрыЗапроса Цикл
			Если СтрокаПараметров.ЭтоВыражение Тогда
				Запрос.УстановитьПараметр(СтрокаПараметров.ИмяПараметра, Вычислить(СтрокаПараметров.ЗначениеПараметра));
			Иначе
				Запрос.УстановитьПараметр(СтрокаПараметров.ИмяПараметра, СтрокаПараметров.ЗначениеПараметра);
			КонецЕсли;
		КонецЦикла;
		Попытка
			ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
		Исключение
			Сообщить(ОписаниеОшибки());
			Возврат;
		КонецПопытки;
		
	Иначе
		
		ТекстЗапросаСКД = ТекстЗапроса;
		УсловиеПоискаПоСтроке = СформироватьУсловиеПоискаПоСтроке(СтрокаПоиска, ОбъектПоиска);
		СписокУсловий = УсловиеПоискаПоСтроке;
	
		Если СписокУсловий <> "" Тогда
			
			ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ОбъектПоиска.ПолноеИмя);
			Для каждого ТЧ Из ОбъектМетаданных.ТабличныеЧасти Цикл
				СписокУсловий = СтрЗаменить(СписокУсловий, "_Таблица." + ТЧ.Имя + "КоличествоСтрок", "ТЧ" + ТЧ.Имя + ".КоличествоСтрок");
			КонецЦикла;
			
			ТекстЗапросаСКД = ТекстЗапросаСКД + "
			|ГДЕ 
			|	" + СписокУсловий;
			
		КонецЕсли;
		
		СКД = Новый СхемаКомпоновкиДанных;
		ИсточникДанныхСКД = СКД.ИсточникиДанных.Добавить();
		ИсточникДанныхСКД.ТипИсточникаДанных = "local";
		ИсточникДанныхСКД.Имя = "ИсточникДанных";
		
		НаборДанных = СКД.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
		НаборДанных.Имя = "НаборДанныхЗапроса";
		НаборДанных.Запрос = ТекстЗапросаСКД;
		НаборДанных.ИсточникДанных = ИсточникДанныхСКД.Имя;
		НаборДанных.АвтоЗаполнениеДоступныхПолей = Истина;
		
		ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(СКД);
		КомпоновщикНастроек.Инициализировать(ИсточникНастроек); 
		
		Если ОтборДанных <> Неопределено Тогда
			НастройкиСКД = ОтборДанных;
		Иначе
			НастройкиСКД = СКД.НастройкиПоУмолчанию;
		КонецЕсли;
		
		КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиСКД);
		КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.Полное);
		КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
		
		КомпоновщикНастроек.Настройки.Структура.Очистить();
		ГруппировкаСКД = КомпоновщикНастроек.Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
		ГруппировкаСКД.Использование = Истина;
		
		ЕстьПолеОбъект = Ложь;
		ПолеОбъект = Новый ПолеКомпоновкиДанных("Объект");
		ЕстьПолеКартинка = Ложь;
		ПолеКартинка = Новый ПолеКомпоновкиДанных("Картинка");
		
		Если КомпоновщикНастроек.Настройки.Выбор.Элементы.Количество() = 0 Тогда
			
			ВыбранныеПоля = ГруппировкаСКД.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
			ВыбранныеПоля.Использование = Истина;
			
			НовыйЭлемент = КомпоновщикНастроек.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			НовыйЭлемент.Использование = Истина;
			НовыйЭлемент.Поле = Новый ПолеКомпоновкиДанных("СистемныеПоля.НомерПоПорядку"); //SystemFields.GroupSerialNumber
			
			Для каждого ЭлементДоступногоВыбора Из КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.Элементы Цикл    
				Если НЕ ЭлементДоступногоВыбора.Папка Тогда
					Если ИменаПолейТЧ.НайтиПоЗначению(ЭлементДоступногоВыбора.Поле) = Неопределено Тогда
						НовыйЭлемент = КомпоновщикНастроек.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
						ЗаполнитьЗначенияСвойств(НовыйЭлемент, ЭлементДоступногоВыбора);
					КонецЕсли; 
				КонецЕсли; 
			КонецЦикла; //Для каждого ЭлементДоступногоВыбора Из   
			
			Если ИменаПолейТЧ.Количество() > 0 Тогда
				ГруппаЭлементов = КомпоновщикНастроек.Настройки.Выбор.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
				ГруппаЭлементов.Использование = Истина;
				ГруппаЭлементов.Заголовок = НСтр("ru = 'Агрегатные поля'");
				
				Для каждого ЭлементМассива Из ИменаПолейТЧ Цикл    
					Если КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.НайтиПоле(ЭлементМассива.Значение) <> Неопределено Тогда
						НовыйЭлемент = ГруппаЭлементов.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
					    НовыйЭлемент.Использование = Истина;
					    НовыйЭлемент.Поле = ЭлементМассива.Значение;
					КонецЕсли; 
				КонецЦикла; //Для каждого ЭлементМассива Из  
			КонецЕсли; 
			
		Иначе
			
			Для каждого ПолеВыбора Из КомпоновщикНастроек.Настройки.Выбор.Элементы Цикл    
				Если ПолеВыбора.Использование Тогда
					ВыбранноеПоле = ГруппировкаСКД.Выбор.Элементы.Добавить(ТипЗнч(ПолеВыбора));
					ЗаполнитьЗначенияСвойств(ВыбранноеПоле, ПолеВыбора);
					Если ТипЗнч(ПолеВыбора) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
						Для каждого ПолеГруппы Из ПолеВыбора.Элементы Цикл
							ВыбранноеПолеГруппы = ВыбранноеПоле.Элементы.Добавить(ТипЗнч(ПолеГруппы));
							ЗаполнитьЗначенияСвойств(ВыбранноеПолеГруппы, ПолеГруппы);
						КонецЦикла;
					КонецЕсли;  //  
					Если НЕ ЕстьПолеОбъект И ПолеВыбора.Поле = ПолеОбъект Тогда
						ЕстьПолеОбъект = Истина;
					КонецЕсли; 
					Если НЕ ЕстьПолеКартинка И ПолеВыбора.Поле = ПолеКартинка Тогда
						ЕстьПолеКартинка = Истина;
					КонецЕсли; 
				КонецЕсли; 
			КонецЦикла; //Для каждого ПолеВыбора Из  
			
			Если НЕ ЕстьПолеОбъект Тогда
				ВыбранноеПоле = ГруппировкаСКД.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
				ВыбранноеПоле.Использование = Истина;
				ВыбранноеПоле.Поле = ПолеОбъект;
			КонецЕсли; 
			Если НЕ ЕстьПолеКартинка Тогда
				ВыбранноеПоле = ГруппировкаСКД.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
				ВыбранноеПоле.Использование = Истина;
				ВыбранноеПоле.Поле = ПолеКартинка;
			КонецЕсли; 
			
		КонецЕсли; 
		
		Для каждого ЭлементДоступногоПорядка Из КомпоновщикНастроек.Настройки.Порядок.Элементы Цикл
			Если ЭлементДоступногоПорядка.Использование Тогда
				НовыйПорядок = ГруппировкаСКД.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
				ЗаполнитьЗначенияСвойств(НовыйПорядок, ЭлементДоступногоПорядка);
			КонецЕсли; 
		КонецЦикла; //Для каждого ЭлементДоступногоПорядка Из   
		
		Попытка
			
			// Подготовка компоновщика макета компоновки данных.
			КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
			МакетКомпоновки = КомпоновщикМакета.Выполнить(СКД, КомпоновщикНастроек.ПолучитьНастройки(),,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));

			ПроцессорКД = Новый ПроцессорКомпоновкиДанных;
			ПроцессорКД.Инициализировать(МакетКомпоновки);

			ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
			ПроцессорВывода.ОтображатьПроцентВывода = Истина;
			ТаблицаРезультата = Новый ТаблицаЗначений;
			ПроцессорВывода.УстановитьОбъект(ТаблицаРезультата);
			ПроцессорВывода.Вывести(ПроцессорКД);
			
		Исключение
			Сообщить(ОписаниеОшибки());
			Возврат;
		КонецПопытки;
		
	КонецЕсли;
	
	МассивРеквизитов.Добавить("Выбрать");
	МассивРеквизитов.Добавить("Картинка");
	МассивРеквизитов.Добавить("Объект");
	Если ТаблицаРезультата.Колонки.Найти("Выбрать") = Неопределено Тогда
		ТаблицаРезультата.Колонки.Вставить(0, "Выбрать");
		ТаблицаРезультата.ЗаполнитьЗначения(Истина, "Выбрать");
	КонецЕсли;
	
	СоздатьКолонки(ТаблицаРезультата, МассивРеквизитов);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборОбъектаМетаданных()
	
	ПараметрФормы = Новый Структура;
	Если ОбъектПоиска <> Неопределено Тогда
		ПараметрФормы.Вставить("ТекущаяСтрока", ОбъектПоиска.ПолноеИмя);
	КонецЕсли;  //  
	
	Результат = ОткрытьФормуМодально(ПолучитьПолноеИмяФормы("ВыборОбъектаМетаданных"), ПараметрФормы, ЭтаФорма);
	Если ТипЗнч(Результат) = Тип("Структура") Тогда 
		
		ПолноеИмяОбъекта = Результат.Имя;
		Если ТипЗнч(ОбъектПоиска) = Тип("Структура") И ОбъектПоиска.Свойство("ПолноеИмя") И ПолноеИмяОбъекта = ОбъектПоиска.ПолноеИмя Тогда
			Возврат;
		КонецЕсли; 
		
		ПозицияТочки = Найти(ПолноеИмяОбъекта, ".");
		ТипОбъекта = Лев(ПолноеИмяОбъекта, ПозицияТочки - 1);
		ИмяОбъекта = Сред(ПолноеИмяОбъекта, ПозицияТочки + 1);
		ОбъектПоиска = ЗаполнитьСтруктуруОбъектаПоиска(ТипОбъекта, ИмяОбъекта, Результат.Синоним, Ложь);
		ОбъектПоискаПредставление = ОбъектПоиска.Представление;
		ПриИзмененииОбъектаПоиска();
		
		СписокВыбора = Элементы.ПолеОбъектПоиска.СписокВыбора;
		Для каждого ЭлементСписка Из СписокВыбора Цикл
			Если ЭлементСписка.Значение.ПолноеИмя = ОбъектПоиска.ПолноеИмя И СписокВыбора.Количество() > 1 Тогда
				СписокВыбора.Сдвинуть(ЭлементСписка, - СписокВыбора.Индекс(ЭлементСписка));
				Возврат;
			КонецЕсли; 
  		КонецЦикла;
		СписокВыбора.Вставить(0, ОбъектПоиска, ОбъектПоискаПредставление);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступность()
	Если ОбъектПоиска = Неопределено Тогда
		Элементы.НайтиОбъекты.Доступность = Ложь;
		Элементы.НайденныеОбъектыОткрытьФормуСписка.Доступность = Ложь;
	Иначе
		Элементы.НайтиОбъекты.Доступность = Истина;
		Элементы.НайденныеОбъектыОткрытьФормуСписка.Доступность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НайтиОбъектыКлиент()
	
	Состояние("Поиск ссылок...");
	НайтиСсылкиПоОтбору();
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНайденныеОбъекты;
	
КонецПроцедуры

&НаСервере
Функция СохранитьНастройкиСервер(ИмяФайла)

	Перем Настройка;
	Настройка = Новый Соответствие();

	ВыбранныеОбработкиДЗ = РеквизитФормыВЗначение("ВыбранныеОбработки");
	ДоступныеОбработкиДЗ = РеквизитФормыВЗначение("ДоступныеОбработки");
	
	Настройка.Вставить("ДоступныеОбработки", ДоступныеОбработкиДЗ);
	Настройка.Вставить("ВыбранныеОбработки", ВыбранныеОбработкиДЗ);

	Если НЕ ЗначениеВФайл(ИмяФайла, Настройка) Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
	
КонецФункции

// Возвращает экземпляр текущего объекта
//
//
&НаСервере
Функция ЭтотОбъект()

	Обработка = РеквизитФормыВЗначение("Объект");
	Возврат Обработка;

КонецФункции // ЭтотОбъект()

&НаСервере
Процедура ЗагрузитьНастройкиСервер(ИмяФайла)

	Перем ДоступныеОбработки;
	Перем ВыбранныеОбработки;
	
	Настройка = ЗначениеИзФайла(ИмяФайла);
	ДоступныеОбработки            = Настройка["ДоступныеОбработки"];
	ВыбранныеОбработки = Настройка["ВыбранныеОбработки"];

	ЗначениеВРеквизитФормы(ДоступныеОбработки, "ДоступныеОбработки");
	ЗначениеВРеквизитФормы(ВыбранныеОбработки, "ВыбранныеОбработки");
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьНастройкиОбработокСервер()
	
	ЭтотОбъект().ЗагрузитьОбработки(ЭтаФорма, ДоступныеОбработки, ВыбранныеОбработки, СтруктураФорм);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗаполнитьСтруктуруОбъектаПоиска(ТипОбъекта, ИмяОбъекта, СинонимОбъекта, ДобавлятьИмяОбъекта = Истина)

	Структура = Новый Структура;
	Структура.Вставить("Тип", ТипОбъекта);
	Структура.Вставить("Имя", ИмяОбъекта);
	Структура.Вставить("ПолноеИмя", ТипОбъекта + "." + ИмяОбъекта);
	Структура.Вставить("Представление", СинонимОбъекта + ?(ДобавлятьИмяОбъекта, " (" + ИмяОбъекта + ")", ""));
	Возврат Структура;

КонецФункции // ЗаполнитьСтруктуруОбъектаПоиска()

&НаСервереБезКонтекста
Процедура ЗаполнитьСписокОбъектовМетаданных(Список, ТипОбъекта, ОбъектыМетаданных)

	Для Каждого ОбъектМетаданных Из ОбъектыМетаданных Цикл
		Если ПравоДоступа("Просмотр", ОбъектМетаданных) Тогда
			СинонимОбъекта = ОбъектМетаданных.Синоним;
			Если Не ЗначениеЗаполнено(СинонимОбъекта) Тогда
				СинонимОбъектаа = ОбъектМетаданных.Имя;
			КонецЕсли;
			Структура = ЗаполнитьСтруктуруОбъектаПоиска(ТипОбъекта, ОбъектМетаданных.Имя, СинонимОбъекта);
			Список.Добавить(Структура, СинонимОбъекта, , БиблиотекаКартинок[ТипОбъекта]);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // ЗаполнитьСписокОбъектовМетаданных()

//////////////////////////////////////
//ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбработкаПеретаскивание = Ложь;
	
	ЭтотОбъект().ЗагрузитьОбработки(ЭтаФорма, ДоступныеОбработки, ВыбранныеОбработки, СтруктураФорм);
	
	Элементы.ПолеОбъектПоиска.ВысотаСпискаВыбора = 15;
	Элементы.ПолеОбъектПоиска.СписокВыбора.ТипЗначения = Новый ОписаниеТипов("Структура");
	
	//ЗаполнитьСписокОбъектовМетаданных(СписокОбъектовМетаданных, "Справочник", Метаданные.Справочники);
	//ЗаполнитьСписокОбъектовМетаданных(СписокОбъектовМетаданных, "ПланВидовХарактеристик", Метаданные.ПланыВидовХарактеристик);
	//ЗаполнитьСписокОбъектовМетаданных(СписокОбъектовМетаданных, "ПланВидовРасчета", Метаданные.ПланыВидовРасчета);
	//ЗаполнитьСписокОбъектовМетаданных(СписокОбъектовМетаданных, "ПланОбмена", Метаданные.ПланыОбмена);
	//ЗаполнитьСписокОбъектовМетаданных(СписокОбъектовМетаданных, "Документ", Метаданные.Документы);
	//ЗаполнитьСписокОбъектовМетаданных(СписокОбъектовМетаданных, "БизнесПроцесс", Метаданные.БизнесПроцессы);
	//ЗаполнитьСписокОбъектовМетаданных(СписокОбъектовМетаданных, "Задача", Метаданные.Задачи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимостьДоступность();
	УстановитьКартинкиОбработок();
	Объект.ОбрабатыватьВТранзакции = ОбрабатыватьВТранзакции;
	Объект.ИспользоватьРежимЗагрузкиОбменаДанными = ИспользоватьРежимЗагрузкиОбменаДанными;
		
	Если ТипЗнч(ОбъектПоиска) = Тип("Структура") И НЕ ОбъектПоиска.Свойство("ПолноеИмя") Тогда
		ОбъектПоиска.Вставить("ПолноеИмя", ОбъектПоиска.Тип + "." + ОбъектПоиска.Имя);
	КонецЕсли; 
	
	Если ТипЗнч(ОбъектыПоиска) <> Тип("Структура") Тогда
		ОбъектыПоиска = Новый Структура;
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ОбъектПоиска = Настройки["ОбъектПоиска"];
	ОтборДанных = Настройки["ОтборДанных"];
	ОбъектыПоиска = Настройки["ОбъектыПоиска"];
	
	СписокВыбора = Настройки.Получить("ОбъектПоиска.СписокВыбора");
	Если СписокВыбора <> Неопределено И ТипЗнч(СписокВыбора) = Тип("СписокЗначений") Тогда
		Элементы.ПолеОбъектПоиска.СписокВыбора.Очистить();
		Для каждого ЭлементСписка Из СписокВыбора Цикл    
			Элементы.ПолеОбъектПоиска.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
		КонецЦикла; //Для каждого ЭлементСписка Из  
	КонецЕсли; 
	
	СписокВыбора = Настройки.Получить("СтрокаПоиска.СписокВыбора");
	Если СписокВыбора <> Неопределено И ТипЗнч(СписокВыбора) = Тип("Массив") Тогда
		Элементы.СтрокаПоиска.СписокВыбора.ЗагрузитьЗначения(СписокВыбора);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	Настройки.Вставить("ОбъектПоиска.СписокВыбора", Элементы.ПолеОбъектПоиска.СписокВыбора);
	Настройки.Вставить("СтрокаПоиска.СписокВыбора", Элементы.СтрокаПоиска.СписокВыбора.ВыгрузитьЗначения());
	
КонецПроцедуры

//////////////////////////////////////
//ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ОбъектПоискаПриИзменении(Элемент)
	
	ПриИзмененииОбъектаПоиска();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектПоискаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыборОбъектаМетаданных();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектПоискаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		ОбъектПоискаПредставление = ВыбранноеЗначение.Представление;
		ОбъектПоиска = ВыбранноеЗначение;
	Иначе
		ОбъектПоискаПредставление = "";
		ОбъектПоиска = Неопределено;
	КонецЕсли;
	
	Если ОбъектПоиска <> Неопределено Тогда
		
		ПриИзмененииОбъектаПоиска();
		
		СписокВыбора = Элементы.ПолеОбъектПоиска.СписокВыбора;
		Для каждого ЭлементСписка Из СписокВыбора Цикл
			Если ЭлементСписка.Значение.ПолноеИмя = ОбъектПоиска.ПолноеИмя И СписокВыбора.Количество() > 1 Тогда
				СписокВыбора.Сдвинуть(ЭлементСписка, - СписокВыбора.Индекс(ЭлементСписка));
				Возврат;
			КонецЕсли; 
  		КонецЦикла;
		СписокВыбора.Вставить(0, ОбъектПоиска, ОбъектПоискаПредставление);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
		СписокВыбора = Элементы.СтрокаПоиска.СписокВыбора;
		ПозицияСтроки = СписокВыбора.НайтиПоЗначению(СтрокаПоиска);
		Если ПозицияСтроки = Неопределено Тогда
			СписокВыбора.Вставить(0, СтрокаПоиска);
		Иначе
			СписокВыбора.Сдвинуть(ПозицияСтроки, - СписокВыбора.Индекс(ПозицияСтроки));
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеОбработкиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	Если Элемент.ТекущиеДанные.ПолучитьРодителя() = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Настройка = Элемент.ТекущиеДанные.Настройка[0].Значение;
	Настройка.Обработка = Элемент.ТекущиеДанные.Обработка;
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеОбработкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ИндексСтроки = Элементы.ДоступныеОбработки.ТекущаяСтрока;
	ТекущаяСтрока = ДоступныеОбработки.НайтиПоИдентификатору(ИндексСтроки);
	
	СтруктураПараметров = СформироватьСтруктуруПараметров();
	СтруктураПараметров.Настройка = ТекущаяСтрока.Настройка[0].Значение;
	
	Родитель = ТекущаяСтрока.ПолучитьРодителя();
	Если Родитель = Неопределено Тогда
		Если НЕ ОбработкаДоступна(ОбъектПоиска.Тип, ТекущаяСтрока.ИмяФормы) Тогда
			Предупреждение("Данная обработка недоступна для типа <" + ОбъектПоиска.Тип + ">");
			Возврат;
		КонецЕсли;
		
		СтруктураПараметров.Настройки = СформироватьНастройки(Элемент.ТекущиеДанные);
		СтруктураПараметров.Вставить("Родитель", ТекущаяСтрока.ПолучитьИдентификатор());
		СтруктураПараметров.Вставить("ТекущаяСтрока", Неопределено);
		СтруктураПараметров.Вставить("ОбрабатыватьВТранзакции", ОбрабатыватьВТранзакции);
		СтруктураПараметров.Вставить("ИспользоватьРежимЗагрузкиОбменаДанными", ИспользоватьРежимЗагрузкиОбменаДанными);
		
		Обработка = ПолучитьФорму(ПолучитьПолноеИмяФормы(ТекущаяСтрока.ИмяФормы), СтруктураПараметров, ЭтаФорма);
	Иначе
		Если НЕ ОбработкаДоступна(ОбъектПоиска.Тип, Родитель.ИмяФормы) Тогда
			Предупреждение("Данная обработка недоступна для типа <" + ОбъектПоиска.Тип + ">");
			Возврат;
		КонецЕсли;
		
		СтруктураПараметров.Настройки = СформироватьНастройки(Родитель);
		СтруктураПараметров.Вставить("Родитель", Родитель.ПолучитьИдентификатор());
		СтруктураПараметров.Вставить("ТекущаяСтрока", ИндексСтроки);
		СтруктураПараметров.Вставить("ОбрабатыватьВТранзакции", ОбрабатыватьВТранзакции);
		СтруктураПараметров.Вставить("ИспользоватьРежимЗагрузкиОбменаДанными", ИспользоватьРежимЗагрузкиОбменаДанными);
		
		Обработка = ПолучитьФорму(ПолучитьПолноеИмяФормы(Родитель.ИмяФормы), СтруктураПараметров, ЭтаФорма);
	КонецЕсли;

	ОткрытьФормуМодально(Обработка);
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеОбработкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Отказ = Истина;
	КонецЕсли;

	Если Элемент.ТекущиеДанные.ПолучитьРодителя() = Неопределено Тогда
		Если Копирование Тогда
			Отказ = Истина;
		Иначе
			Если НЕ ОбработкаДоступна(ОбъектПоиска.Тип, Элемент.ТекущиеДанные.ИмяФормы) Тогда
				Предупреждение("Данная обработка недоступна для типа <" + ОбъектПоиска.Тип + ">");
				Отказ = Истина;
				Возврат;
			КонецЕсли;
			
			Отказ = НЕ ПолучитьФорму(ПолучитьПолноеИмяФормы(Элемент.ТекущиеДанные.ИмяФормы)).мИспользоватьНастройки;
			Если НЕ Отказ Тогда
				//свое добавление
				Отказ = Истина;
				ДобавитьСтроку(Элемент.ТекущиеДанные);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если НЕ ОбработкаДоступна(ОбъектПоиска.Тип, Элемент.ТекущиеДанные.ПолучитьРодителя().ИмяФормы) Тогда
			Предупреждение("Данная обработка недоступна для типа <" + ОбъектПоиска.Тип + ">");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		Отказ = Истина;
		Если НЕ Копирование Тогда
			Если ПолучитьФорму(ПолучитьПолноеИмяФормы(Элемент.ТекущиеДанные.ПолучитьРодителя().ИмяФормы)).мИспользоватьНастройки Тогда
				ДобавитьСтроку(Элемент.ТекущиеДанные.ПолучитьРодителя());
			КонецЕсли;
		Иначе
			ТекСтрока = Элемент.ТекущиеДанные;
			Родитель = Элемент.ТекущиеДанные.ПолучитьРодителя();
			НоваяСтрока = ДобавитьСтроку(Родитель);
			
			Если НЕ ТекСтрока.Настройка[0].Значение = Неопределено Тогда
				НоваяНастройка = Новый Структура();
				Для Каждого РеквизитНастройки Из ТекСтрока.Настройка[0].Значение Цикл
					Значение = РеквизитНастройки.Значение;
					Выполнить("НоваяНастройка.Вставить(Строка(РеквизитНастройки.Ключ), Значение);");
				КонецЦикла;

				НоваяСтрока.Настройка[0].Значение = НоваяНастройка;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеОбработкиПередНачаломИзменения(Элемент, Отказ)
	Если Элемент.ТекущиеДанные.ПолучитьРодителя() = Неопределено Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеОбработкиПередУдалением(Элемент, Отказ)
	Если Элемент.ТекущиеДанные.ПолучитьРодителя() = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	Если Вопрос("Удалить настройку?", РежимДиалогаВопрос.ОКОтмена,, КодВозвратаДиалога.ОК) = КодВозвратаДиалога.ОК Тогда
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("СтрокаДоступнойОбработки", Элемент.ТекущаяСтрока);
		
		МассивДляУдаления = ВыбранныеОбработки.НайтиСтроки(ПараметрыОтбора);
		Для Индекс = 0 по МассивДляУдаления.Количество() - 1 Цикл
			ВыбранныеОбработки.Удалить(МассивДляУдаления[Индекс]);
		КонецЦикла;
	Иначе
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеОбработкиНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	Если НЕ ПроверитьДоступностьОбработки() Тогда
		СтандартнаяОбработка = Ложь;
		Предупреждение("Данная обработка недоступна для типа <" + ОбъектПоиска.Тип + ">");
		Возврат;
	КонецЕсли;
	
	ОбработкаПеретаскивание = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеОбработкиПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	Если НЕ ОбработкаПеретаскивание Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрВыбранных Из ПараметрыПеретаскивания.Значение Цикл
		СтрДоступных = ДоступныеОбработки.НайтиПоИдентификатору(СтрВыбранных.ПолучитьИдентификатор());
		
		НовСтр = ВыбранныеОбработки.Добавить();
		НовСтр.ОбработкаНастройка = СтрДоступных.Обработка;
		НовСтр.СтрокаДоступнойОбработки = СтрВыбранных;
		НовСтр.Выбрать = Истина;
		НовСтр.Настройка = СтрДоступных.Настройка;
	КонецЦикла;
	
	ОбработкаПеретаскивание = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура НайденныеОбъектыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.НайденныеОбъекты.ТекущиеДанные <> Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьЗначение(Элементы.НайденныеОбъекты.ТекущиеДанные.Объект);
	КонецЕсли;  
	
КонецПроцедуры

//////////////////////////////////////
//ОБРАБОТЧИКИ СОБЫТИЙ КОМАНД

&НаКлиенте
Процедура НайтиОбъекты(Команда)
	
	НайтиОбъектыКлиент();

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	ВыбратьЭлементы(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВыборВсех(Команда)
	ВыбратьЭлементы(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбработку(Команда)
	Для каждого Строка из ВыбранныеОбработки Цикл
		ОбработкаПрерыванияПользователя();
		
		Если НЕ Строка.Выбрать Тогда
			Продолжить;
		КонецЕсли;
		
		Стр = ДоступныеОбработки.НайтиПоИдентификатору(Строка.СтрокаДоступнойОбработки);
		Родитель = Стр.ПолучитьРодителя();
		
		СтруктураПараметров = СформироватьСтруктуруПараметров();
		СтруктураПараметров.Настройка = Стр.Настройка[0].Значение;
		
		Если Родитель = Неопределено Тогда
			ИмяФормыОбработки = Стр.ИмяФормы;
			
			СтруктураПараметров.Настройки = СформироватьНастройки(Стр);
			СтруктураПараметров.Вставить("Родитель", Стр.ПолучитьИдентификатор());
			СтруктураПараметров.Вставить("ТекущаяСтрока", Неопределено);
		Иначе
			ИмяФормыОбработки = Родитель.ИмяФормы;
			
			СтруктураПараметров.Настройки = СформироватьНастройки(Родитель);
			СтруктураПараметров.Вставить("Родитель", Родитель.ПолучитьИдентификатор());
			СтруктураПараметров.Вставить("ТекущаяСтрока", Строка.СтрокаДоступнойОбработки);
		КонецЕсли;
		
		Если НЕ ОбработкаДоступна(ОбъектПоиска.Тип, ИмяФормыОбработки) Тогда
			Сообщить("Обработка " + ИмяФормы + " недоступна для типа <" + ОбъектПоиска.Тип + ">");
			Продолжить;
		КонецЕсли;
		
		Обработка = ПолучитьФорму(ПолучитьПолноеИмяФормы(ИмяФормыОбработки), СтруктураПараметров, ЭтаФорма);
		Обработка.ЗагрузитьНастройку();
		Обработка.ВыполнитьОбработку();
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Отбор(Команда)
	Если ОбъектПоиска = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ТекстЗапроса", ТекстЗапроса);
	СтруктураПараметров.Вставить("ТекстПроизвольногоЗапроса", ТекстПроизвольногоЗапроса);
	СтруктураПараметров.Вставить("СтрокаПоиска", СтрокаПоиска);
	СтруктураПараметров.Вставить("СтрокаПоискаСписок", Элементы.СтрокаПоиска.СписокВыбора.ВыгрузитьЗначения());
	СтруктураПараметров.Вставить("Настройки", ОтборДанных);
	СтруктураПараметров.Вставить("ОбъектПоиска", ОбъектПоиска);
	СтруктураПараметров.Вставить("РежимПоиска", РежимПоиска);
	СтруктураПараметров.Вставить("ПараметрыЗапроса", ПараметрыЗапроса);
	СтруктураПараметров.Вставить("ИменаПолейТЧ", ИменаПолейТЧ);
	
	ФормаОтбора = ПолучитьФорму(ПолучитьПолноеИмяФормы("ФормаОтбора"), СтруктураПараметров, ЭтаФорма);
	РезультатОтбора = ОткрытьФормуМодально(ФормаОтбора);
	Если РезультатОтбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьРезультатОтбора(РезультатОтбора);
	
	НачатьПоиск = Ложь;
	Если РезультатОтбора.Свойство("НачатьПоиск", НачатьПоиск) И НачатьПоиск Тогда
		НайтиОбъектыКлиент();
	КонецЕсли; 
		
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсеОбработки(Команда)
	ВыбратьОбработки("ВыбранныеОбработки", Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВыборВсехОбработок(Команда)
	ВыбратьОбработки("ВыбранныеОбработки", Ложь);
КонецПроцедуры

&НаКлиенте
Процедура НастройкиОбработки(Команда)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ОбрабатыватьВТранзакции", ОбрабатыватьВТранзакции);
	СтруктураПараметров.Вставить("ИспользоватьРежимЗагрузкиОбменаДанными", ИспользоватьРежимЗагрузкиОбменаДанными);
	
	ФормаНастроек = ПолучитьФорму(ПолучитьПолноеИмяФормы("ФормаНастроекУправляемая"), СтруктураПараметров, ЭтаФорма);
	РезультатОткрытия = ОткрытьФормуМодально(ФормаНастроек);
	
	Если ТипЗнч(РезультатОткрытия) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма, РезультатОткрытия);
		ЗаполнитьЗначенияСвойств(Объект, РезультатОткрытия);
	КонецЕсли;
	
КонецПроцедуры

//Процедура-обработчик команды "Проголосовать"
//
&НаКлиенте
Процедура Проголосовать(Команда)
	
	ЗапуститьПриложение("http://infostart.ru/public/122215/?rate=1");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСписка(Команда)
	
	Если ОбъектПоиска <> Неопределено Тогда
		ПараметрыФормы = Новый Структура();
		Если Элементы.НайденныеОбъекты.ТекущиеДанные <> Неопределено Тогда
			ПараметрыФормы.Вставить("ТекущаяСтрока", Элементы.НайденныеОбъекты.ТекущиеДанные.Объект);
		КонецЕсли; 
		ОткрытьФорму(ОбъектПоиска.ПолноеИмя + ".ФормаСписка", ПараметрыФормы);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройки(Команда)
	
	ДиалогВыбораФайла =	Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	
	ДиалогВыбораФайла.Фильтр                      =	"Файл сохраненной настройки (*.sav)|*.sav";
	ДиалогВыбораФайла.Заголовок                   =	НСтр("ru = 'Выберите файл'");
	ДиалогВыбораФайла.ПредварительныйПросмотр     =	Ложь;
	ДиалогВыбораФайла.Расширение                  =	"sav";
	ДиалогВыбораФайла.ИндексФильтра               =	0;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла =	 Истина;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		ИмяФайла = ДиалогВыбораФайла.ПолноеИмяФайла;
		Если НЕ СохранитьНастройкиСервер(ИмяФайла) Тогда
			Предупреждение(НСтр("ru = 'Настройка не сохранена!!!'"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНастройки(Команда)
	
	ДиалогВыбораФайла =	Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораФайла.Фильтр                      =	"Файл сохраненной настройки (*.sav)|*.sav";
	ДиалогВыбораФайла.Заголовок                   =	НСтр("ru = 'Выберите файл'");
	ДиалогВыбораФайла.ПредварительныйПросмотр     =	Ложь;
	ДиалогВыбораФайла.Расширение                  =	"sav";
	ДиалогВыбораФайла.ИндексФильтра               =	0;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла =	 Ложь;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		ИмяФайла = ДиалогВыбораФайла.ПолноеИмяФайла;
		ЗагрузитьНастройкиСервер(ИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьНастройкиОбработок(Команда)
	
	ВыбранныеОбработки.Очистить();
	ДоступныеОбработки.ПолучитьЭлементы().Очистить();
	
	ОчиститьНастройкиОбработокСервер();
	УстановитьКартинкиОбработок();
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьНастройкиПоиска(Команда)
	
	ПриИзмененииОбъектаПоиска(Ложь);
	
КонецПроцедуры