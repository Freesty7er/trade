
Процедура СформироватьОтчетПоНастройке(Параметры, ТабличныйДокумент)
	
	настройкиОтчета								 = Параметры.НастройкиОтчета;
	ключОбъекта									 = настройкиОтчета.КлючОбъекта;
	пользовательскиеНастройкиКомпоновкиДанных	 = настройкиОтчета.ХранилищеНастроек.Получить();
	
	разделитель1 = Ложь;
	разделитель2 = Ложь;
	отчетТип	 = "";
	отчетИмя	 = "";
	отчетВариант = "";
	
	Для номерСимвола = 1 По СтрДлина(ключОбъекта) Цикл
		
		символ = Сред(ключОбъекта, номерСимвола, 1);
		
		Если символ = "." Тогда
			разделитель1 = Истина;
		ИначеЕсли символ = "/" Тогда
			разделитель2 = Истина;
		ИначеЕсли разделитель2 Тогда
			отчетВариант = отчетВариант + символ;
		ИначеЕсли Не разделитель1 Тогда
			отчетТип = отчетТип + символ;
		Иначе
			отчетИмя = отчетИмя + символ;
		КонецЕсли;
		
	КонецЦикла;
	
	СхемаКомпоновкиДанных = Отчеты[отчетИмя].ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	
	КомпоновщикНастроекКомпоновкиДанных = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроекКомпоновкиДанных.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(СхемаКомпоновкиДанных.ВариантыНастроек[отчетВариант].Настройки);
	КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьПользовательскиеНастройки(пользовательскиеНастройкиКомпоновкиДанных);
	
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	МакетКомпоновки = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроекКомпоновкиДанных.ПолучитьНастройки());
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки,,,Истина);
	
	ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент.УстановитьДокумент(ТабличныйДокумент);
	
	ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

Процедура СохранитьРезультатФормированияОтчетаВФайл(ТабличныйДокумент, ПолноеИмяФайла, Параметры)
	
	полноеИмяФайла	 = Параметры.ПутьКФайлу + "\" + Параметры.ИмяФайла + "_" + СтрЗаменить(Формат(ТекущаяДата(), "ДЛФ=Д"),".","") + СтрЗаменить(Формат(ТекущаяДата(), "ДЛФ=В"),":","") + ".xls";
	
	СоздатьКаталог(Параметры.ПутьКФайлу);
	
	полноеИмяФайла = РаботаСФайламиКлиентСервер.СформироватьЭксельТабличныйДокумент(ТабличныйДокумент, полноеИмяФайла);
	
КонецПроцедуры

Процедура ОтправитьФайлРезультатаФормированияОтчетаПоПочте(ПолноеИмяФайла, Параметры)
	
	интернетПочтовоеСообщениеСостав = Новый Структура;
	интернетПочтовоеСообщениеСостав.Вставить("сообщение", Параметры.ШаблонИнтернетПочтовогоСообщения.Сообщение);
	интернетПочтовоеСообщениеСостав.Вставить("вложение", ПолноеИмяФайла);
	нпп = 0;
	Для Каждого получатель Из Параметры.ШаблонИнтернетПочтовогоСообщения.Получатели Цикл
		нпп = нпп + 1;
		интернетПочтовоеСообщениеСостав.Вставить("получатель" + нпп, получатель.АдресИнтернетПочтовогоЯщика);
	КонецЦикла;
	интернетПочтовоеСообщениеСостав.Вставить("тема", Параметры.ШаблонИнтернетПочтовогоСообщения.Тема);
	интернетПочтовоеСообщениеСостав.Вставить("отправитель", Параметры.ШаблонИнтернетПочтовогоСообщения.Отправитель);
	
	#Область изменения_20180531_Карпачев_А_Ю
	интернетПочтовоеСообщениеСостав.Вставить("АдаптироватьПодМашинныйРазбор", Параметры.ШаблонИнтернетПочтовогоСообщения.АдаптироватьПодМашинныйРазбор);
	#КонецОбласти
	
	ОтправкаСообщенийСервер.ИнтернетПочтовоеСообщениеОтправить(интернетПочтовоеСообщениеСостав);
	
КонецПроцедуры

Процедура СформироватьОтправитьОтчетПоНастройке(Параметры) Экспорт
	
	табличныйДокумент = Новый ТабличныйДокумент;
	
	СформироватьОтчетПоНастройке(Параметры, табличныйДокумент);
	
	полноеИмяФайла = "";
	
	СохранитьРезультатФормированияОтчетаВФайл(табличныйДокумент, полноеИмяФайла, Параметры);
	
	ОтправитьФайлРезультатаФормированияОтчетаПоПочте(полноеИмяФайла, Параметры);
	
КонецПроцедуры