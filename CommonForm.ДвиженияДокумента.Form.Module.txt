
#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Документ = Параметры.Документ;
	
	мРеквизиты = Новый Массив;	// Список добавляемых реквизитов формы.
	
	тзСвойстваЭлементов = Новый ТаблицаЗначений;
	тзСвойстваЭлементов.Колонки.Добавить("ПредставлениеРегистра");
	тзСвойстваЭлементов.Колонки.Добавить("ПолноеИмяРегистра");
	тзСвойстваЭлементов.Колонки.Добавить("ТипРегистра");
	тзСвойстваЭлементов.Колонки.Добавить("ИмяРеквизитаФормы");
	тзСвойстваЭлементов.Колонки.Добавить("Реквизиты", Новый ОписаниеТипов("Массив"));
	
	ДокументОбъект = Документ.ПолучитьОбъект();
	Для каждого Движение Из ДокументОбъект.Движения Цикл
	
		РегистрМетаданные = Движение.Метаданные();
	
		Если Не ПравоДоступа("Просмотр", РегистрМетаданные) Тогда
			Продолжить;
		КонецЕсли; 
		
		// Определим доступность регистра по функциональным опциям. На текущий момент
		// обрабатываются только функциональные опции типа "Булево".
		ОтключеноФункциональнойОпцией = Ложь;
		Для каждого ФункциональнаяОпцияМетаданные Из Метаданные.ФункциональныеОпции Цикл
		
			Если ФункциональнаяОпцияМетаданные.Состав.Найти(РегистрМетаданные) <> Неопределено
				И ПолучитьФункциональнуюОпцию(ФункциональнаяОпцияМетаданные.Имя) = Ложь Тогда
				
				ОтключеноФункциональнойОпцией = Истина;
				Прервать;
				
			КонецЕсли; 
		
		КонецЦикла; 
		Если ОтключеноФункциональнойОпцией Тогда
			Продолжить;
		КонецЕсли; 
		
		ИмяРегистра = РегистрМетаданные.Имя;
		ПолноеИмяРегистра = РегистрМетаданные.ПолноеИмя();
		ТипРегистра = Лев(ПолноеИмяРегистра, СтрНайти(ПолноеИмяРегистра, ".") - 1);
		ИмяРеквизитаФормы = ТипРегистра + ИмяРегистра;
		
		// Сохраним свойства элементов для последующей обработки.
		НоваяСтрока = тзСвойстваЭлементов.Добавить();
		НоваяСтрока.ПредставлениеРегистра = РегистрМетаданные.Представление();
		НоваяСтрока.ПолноеИмяРегистра = РегистрМетаданные.ПолноеИмя();
		НоваяСтрока.ТипРегистра = ТипРегистра;
		НоваяСтрока.ИмяРеквизитаФормы = ИмяРеквизитаФормы;
		
		КолонкиРегистра = Движение.ВыгрузитьКолонки().Колонки;
		КолонкиРегистра.Удалить("Регистратор");
		Если КолонкиРегистра.Найти("МоментВремени") <> Неопределено Тогда
			КолонкиРегистра.Удалить("МоментВремени");
		КонецЕсли; 
		
		Для каждого Колонка Из КолонкиРегистра Цикл
			НоваяСтрока.Реквизиты.Добавить(Колонка.Имя);
		КонецЦикла; 
		
		// Создаём новый реквизит формы и добавляем его в список новых реквизитов.
		мРеквизиты.Добавить(Новый РеквизитФормы(ИмяРеквизитаФормы, Новый ОписаниеТипов("ДинамическийСписок")));
	
	КонецЦикла; 
	тзСвойстваЭлементов.Сортировать("ПредставлениеРегистра");
	
	// Изменяем реквизиты формы.
	ИзменитьРеквизиты(мРеквизиты);
	
	// Заполняем свойства реквизитов и создаём элементы формы.
	Для каждого СтрокаСвойстваЭлементов Из тзСвойстваЭлементов Цикл
		
		ИмяРеквизитаФормы = СтрокаСвойстваЭлементов.ИмяРеквизитаФормы;
		
		// Зададим основную таблицу нового реквизита формы, соответствующему определённому регистру.
		ЭтотОбъект[ИмяРеквизитаФормы].ОсновнаяТаблица = СтрокаСвойстваЭлементов.ПолноеИмяРегистра;
		
		// Отбор по регистратору.
		ЭлементОтбора = ЭтотОбъект[ИмяРеквизитаФормы].Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Регистратор");
		ЭлементОтбора.ПравоеЗначение = Документ;
		ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		
		// Группа-страница, на которой будет размещена таблица регистра.
		НоваяГруппа = Элементы.Добавить("Группа" + ИмяРеквизитаФормы, Тип("ГруппаФормы"), Элементы.ГруппаРегистры);
		НоваяГруппа.Заголовок = СтрокаСвойстваЭлементов.ПредставлениеРегистра;
		НоваяГруппа.Картинка = БиблиотекаКартинок[СтрокаСвойстваЭлементов.ТипРегистра];
		
		// Таблица с данными регистра.
		НоваяТаблицаФормы = Элементы.Добавить(ИмяРеквизитаФормы, Тип("ТаблицаФормы"), НоваяГруппа);
		НоваяТаблицаФормы.ПутьКДанным = ИмяРеквизитаФормы;
		НоваяТаблицаФормы.УстановитьДействие("Выбор", "СписокВыбор");
		
		// Колонки таблицы.
		Для каждого Реквизит Из СтрокаСвойстваЭлементов.Реквизиты Цикл
			
			НовоеПолеТаблицы = Элементы.Добавить(ИмяРеквизитаФормы + Реквизит, Тип("ПолеФормы"), НоваяТаблицаФормы);
			НовоеПолеТаблицы.Вид = ВидПоляФормы.ПолеНадписи;
			НовоеПолеТаблицы.ПутьКДанным = ИмяРеквизитаФормы + "." + Реквизит;
		
		КонецЦикла; 
	
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПоказатьЗначение(, Элемент.ТекущиеДанные[СтрЗаменить(Поле.Имя, Элемент.Имя, "")]);
	
КонецПроцедуры

#КонецОбласти 
