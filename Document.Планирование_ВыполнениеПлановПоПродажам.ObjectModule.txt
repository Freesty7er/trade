
#Область ОбработчикиСобытий

Процедура ПриКопировании(объектКопирования)
	
	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеСкопированногоДокумента(ЭтотОбъект, объектКопирования);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(данныеЗаполнения, стандартнаяОбработка)
	
	заполненНаОснованииДокумента = Ложь;

	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеНовогоДокумента(ЭтотОбъект, данныеЗаполнения);
	
	// Заполнение по умолчанию
	Если Подразделение.Пустая() Тогда
		Подразделение = ПользователиСервер.ПолучитьЗначениеНастройки("ОсновноеПодразделение");
	КонецЕсли; 
	
	Если Ответственный.Пустая() Тогда
		Ответственный = ПользователиСервер.ПолучитьЗначениеНастройки("ОсновнойОтветственный");
	КонецЕсли;
	
	// Ввод на основании.
	типДанныхЗаполнения = ТипЗнч(данныеЗаполнения);
	
	Если типДанныхЗаполнения = Тип("Структура") Тогда
		
		ЗаполнитьДокументПоОтбору(данныеЗаполнения);
		
	ИначеЕсли типДанныхЗаполнения = Тип("ДокументСсылка.Планирование_УстановкаПлановПоПродажам") Тогда
		
		ЗаполнитьДокументНаОснованииУстановкиПлановПоПродажам(данныеЗаполнения);
		заполненНаОснованииДокумента = Истина;
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаПроведения(отказ, режимПроведения)
	
	Движения.ВзаиморасчетыССотрудниками.Записывать = Истина;
	Движения.ФинансовыйРезультат.Записывать = Истина;
	Движения.РасчетыПоОплатеТруда.Записывать = Истина;
	
	запрос = Новый Запрос;
	
	#Область ТекстЗапроса
	
	запрос.Текст =
	"ВЫБРАТЬ
	|	Планирование_ВыполнениеПлановПоПродажамРасчетЗарплаты.Ссылка.Подразделение,
	|	НАЧАЛОПЕРИОДА(Планирование_ВыполнениеПлановПоПродажамРасчетЗарплаты.Ссылка.Дата, МЕСЯЦ) КАК ПериодРегистрации,
	|	Планирование_ВыполнениеПлановПоПродажамРасчетЗарплаты.Ссылка.Дата КАК Период,
	|	Планирование_ВыполнениеПлановПоПродажамРасчетЗарплаты.Сотрудник,
	|	Планирование_ВыполнениеПлановПоПродажамРасчетЗарплаты.ВидРасчета,
	|	СУММА(Планирование_ВыполнениеПлановПоПродажамРасчетЗарплаты.Результат) КАК Результат,
	|	Планирование_ВыполнениеПлановПоПродажамРасчетЗарплаты.Сотрудник.КатегорияРаботника.СтатьяЗатратПоЗарплате КАК СтатьяЗатрат
	|ПОМЕСТИТЬ ВТ_ДанныеДокумента
	|ИЗ
	|	Документ.Планирование_ВыполнениеПлановПоПродажам.РасчетЗарплаты КАК Планирование_ВыполнениеПлановПоПродажамРасчетЗарплаты
	|ГДЕ
	|	Планирование_ВыполнениеПлановПоПродажамРасчетЗарплаты.Ссылка = &Ссылка
	|	И Планирование_ВыполнениеПлановПоПродажамРасчетЗарплаты.Результат <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	Планирование_ВыполнениеПлановПоПродажамРасчетЗарплаты.Ссылка.Подразделение,
	|	НАЧАЛОПЕРИОДА(Планирование_ВыполнениеПлановПоПродажамРасчетЗарплаты.Ссылка.Дата, МЕСЯЦ),
	|	Планирование_ВыполнениеПлановПоПродажамРасчетЗарплаты.Сотрудник,
	|	Планирование_ВыполнениеПлановПоПродажамРасчетЗарплаты.ВидРасчета,
	|	Планирование_ВыполнениеПлановПоПродажамРасчетЗарплаты.Сотрудник.КатегорияРаботника.СтатьяЗатратПоЗарплате,
	|	Планирование_ВыполнениеПлановПоПродажамРасчетЗарплаты.Ссылка.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.Сотрудник,
	|	ЗНАЧЕНИЕ(ПланСчетов.Внутренний.Зарплата) КАК СчетУчета,
	|	ВТ_ДанныеДокумента.Результат КАК Сумма
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.СтатьяЗатрат,
	|	СУММА(ВТ_ДанныеДокумента.Результат) КАК СуммаРасходов
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.СтатьяЗатрат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.Сотрудник,
	|	ВТ_ДанныеДокумента.ПериодРегистрации,
	|	ВТ_ДанныеДокумента.ВидРасчета,
	|	ВТ_ДанныеДокумента.Результат
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента";
	
	#КонецОбласти
	
	запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	результатЗапроса = запрос.ВыполнитьПакет();
	
	Движения.ВзаиморасчетыССотрудниками.Загрузить(результатЗапроса[1].Выгрузить());
	Движения.ФинансовыйРезультат.Загрузить(результатЗапроса[2].Выгрузить());
	Движения.РасчетыПоОплатеТруда.Загрузить(результатЗапроса[3].Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеПоОснованию

Процедура ЗаполнитьДокументПоОтбору(Знач данныеЗаполнения)
	
КонецПроцедуры	

Процедура ЗаполнитьДокументНаОснованииУстановкиПлановПоПродажам(Знач данныеЗаполнения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Заполнение реквизитов документа.
	ЗаполнениеОбъектовСервер.ЗаполнитьРеквизитыДокументаПриВводеНаОсновании(ЭтотОбъект, данныеЗаполнения);
	
	запрос = Новый Запрос;
	
	#Область ТекстЗапроса
	
	запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Планирование_УстановкаПлановПоПродажамСостав.Менеджер,
	|	Планирование_УстановкаПлановПоПродажамСостав.ГруппаПродукта,
	|	Планирование_УстановкаПлановПоПродажамСостав.ВидПродукции,
	|	Планирование_УстановкаПлановПоПродажамСостав.Ссылка.Подразделение,
	|	Планирование_УстановкаПлановПоПродажамСостав.Ссылка.ПоказательКПИ
	|ПОМЕСТИТЬ ВТ_ДанныеЗаполнения
	|ИЗ
	|	Документ.Планирование_УстановкаПлановПоПродажам.Состав КАК Планирование_УстановкаПлановПоПродажамСостав
	|ГДЕ
	|	Планирование_УстановкаПлановПоПродажамСостав.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеЗаполнения.Менеджер,
	|	ВТ_ДанныеЗаполнения.ГруппаПродукта КАК ГруппаПродукции,
	|	ВТ_ДанныеЗаполнения.ВидПродукции
	|ИЗ
	|	ВТ_ДанныеЗаполнения КАК ВТ_ДанныеЗаполнения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Менеджеры.Менеджер,
	|	КД_НазначенияТорговыхПредставителейСрезПоследних.Сотрудник
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ВТ_ДанныеЗаполнения.Менеджер КАК Менеджер,
	|		ВТ_ДанныеЗаполнения.Подразделение КАК Подразделение
	|	ИЗ
	|		ВТ_ДанныеЗаполнения КАК ВТ_ДанныеЗаполнения
	|	ГДЕ
	|		ВТ_ДанныеЗаполнения.Менеджер.ЭтоГруппа = ЛОЖЬ) КАК Менеджеры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КД_НазначенияТорговыхПредставителей.СрезПоследних(&ДатаСреза, ) КАК КД_НазначенияТорговыхПредставителейСрезПоследних
	|		ПО Менеджеры.Подразделение = КД_НазначенияТорговыхПредставителейСрезПоследних.Подразделение
	|			И Менеджеры.Менеджер = КД_НазначенияТорговыхПредставителейСрезПоследних.Менеджер";
	
	#КонецОбласти
	
	запрос.УстановитьПараметр("Ссылка", данныеЗаполнения);
	запрос.УстановитьПараметр("ДатаСреза", КонецМесяца(данныеЗаполнения.Дата));
	
	результатЗапроса = запрос.ВыполнитьПакет();
	
	ЭтотОбъект.ИсходныеДанные.Загрузить(результатЗапроса[1].Выгрузить());
	ЭтотОбъект.БазыРасчетаЗарплаты.Загрузить(результатЗапроса[2].Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти