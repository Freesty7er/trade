
&НаСервере
Процедура ЗаполнитьДокументСервер()
	
	Объект.СтрокиДокумента.Очистить();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Менеджер,
	|	СУММА(ВложенныйЗапрос.Продажи1) КАК Продажи1,
	|	СУММА(ВложенныйЗапрос.Продажи2) КАК Продажи2
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПродажиОбороты.Менеджер КАК Менеджер,
	|		ПродажиОбороты.КоличествоОборот * ПродажиОбороты.Номенклатура.Вес КАК Продажи1,
	|		0 КАК Продажи2
	|	ИЗ
	|		РегистрНакопления.Продажи.Обороты(
	|				&НачалоПериода1,
	|				&КонецПериода1,
	|				,
	|				Подразделение = &Подразделение
	|					И Менеджер В ИЕРАРХИИ (&ТорговаяКоманда)
	|					И Номенклатура В ИЕРАРХИИ (&НоменклатурнаяГруппа)) КАК ПродажиОбороты
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПродажиОбороты.Менеджер,
	|		0,
	|		ПродажиОбороты.КоличествоОборот * ПродажиОбороты.Номенклатура.Вес
	|	ИЗ
	|		РегистрНакопления.Продажи.Обороты(
	|				&НачалоПериода2,
	|				&КонецПериода2,
	|				,
	|				Подразделение = &Подразделение
	|					И Менеджер В ИЕРАРХИИ (&ТорговаяКоманда)
	|					И Номенклатура В ИЕРАРХИИ (&НоменклатурнаяГруппа)) КАК ПродажиОбороты) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Менеджер
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВложенныйЗапрос.Менеджер.Наименование");
	
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.УстановитьПараметр("ТорговаяКоманда", Объект.ТорговаяКоманда);
	Запрос.УстановитьПараметр("НоменклатурнаяГруппа", Объект.НоменклатурнаяГруппа);
	
	Запрос.УстановитьПараметр("НачалоПериода1", НачалоМесяца(Объект.Период1));
	Запрос.УстановитьПараметр("КонецПериода1", КонецМесяца(Объект.Период1));
	
	Запрос.УстановитьПараметр("НачалоПериода2", НачалоМесяца(Объект.Период2));
	Запрос.УстановитьПараметр("КонецПериода2", КонецМесяца(Объект.Период2));
	
	Объект.СтрокиДокумента.Загрузить(Запрос.Выполнить().Выгрузить() );
	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДокумент(Команда)
	ЗаполнитьДокументСервер();
КонецПроцедуры
