
&НаСервере
Процедура ПриСозданииНаСервере(отказ, стандартнаяОбработка)
	
	Расписание = Новый РасписаниеРегламентногоЗадания;
	
	ЗаполнитьЗначенияСвойств(Расписание, Объект);
	
	Расписание.ДниНедели = Объект.ДниНедели.Выгрузить().ВыгрузитьКолонку("День");
	Расписание.Месяцы = Объект.Месяцы.Выгрузить().ВыгрузитьКолонку("Месяц");	
	
	
КонецПроцедуры

&НаКлиенте
Процедура РасписаниеНажатие(элемент, стандартнаяОбработка)
	
	стандартнаяОбработка = Ложь;
	
	обработчик = Новый ОписаниеОповещения("ПослеИзмененияРасписания", ЭтотОбъект);
	
	диалогРасписания = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	диалогРасписания.Показать(обработчик);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеИзмененияРасписания(расписаниеРезультат, дополнительныеПараметры) Экспорт
	
	Если расписаниеРезультат <> Неопределено Тогда
		
		Модифицированность = Истина;
		Расписание = РасписаниеРезультат;
		
		ЗаполнитьЗначенияСвойств(Объект, Расписание,,"ДниНедели,Месяцы,ДетальныеРасписанияДня");	
		
		//дни недели
		Объект.ДниНедели.Очистить();
		Для Каждого элемент из Расписание.ДниНедели Цикл
			строкаДниНедели = Объект.ДниНедели.Добавить();
			строкаДниНедели.День = элемент;
		КонецЦикла;		
		
		//месяцы
		Объект.Месяцы.Очистить();
		Для Каждого элемент из Расписание.Месяцы Цикл
			строкаМесяцы = Объект.Месяцы.Добавить();
			строкаМесяцы.Месяц = Элемент;
		КонецЦикла;		
		
	КонецЕсли;
	
КонецПроцедуры

