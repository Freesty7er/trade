
&НаСервере
Функция КомандаЗаполнитьНаСервере()
	
	запрос = Новый Запрос;
	
	#Область ТекстЗапроса
	
	запрос.Текст =
	"ВЫБРАТЬ
	|	ВзаиморасчетыСПоставщикамиОстатки.Подразделение,
	|	ВзаиморасчетыСПоставщикамиОстатки.Контрагент,
	|	ВзаиморасчетыСПоставщикамиОстатки.КредитныйДокумент,
	|	ВзаиморасчетыСПоставщикамиОстатки.ЦФО,
	|	ВзаиморасчетыСПоставщикамиОстатки.СчетУчета,
	|	ВзаиморасчетыСПоставщикамиОстатки.СуммаОстаток КАК Долг
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыСПоставщиками.Остатки(&МоментВремени, Подразделение = &Подразделение) КАК ВзаиморасчетыСПоставщикамиОстатки";
	
	#КонецОбласти
	
	запрос.УстановитьПараметр("МоментВремени", Новый Граница(Объект.Дата, ВидГраницы.Включая));
	запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	
	результатЗапроса = запрос.Выполнить();
	
	Если результатЗапроса.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Объект.СтрокиДокумента.Загрузить(результатЗапроса.Выгрузить());
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура КомандаЗаполнить(команда)
	
	Если Объект.Проведен Тогда
		
		текстПредупреждения = НСтр("ru = 'Необходимо отменить проведение документа!'");
		ПоказатьПредупреждение(, текстПредупреждения, 15);
		
		Возврат;
		
	КонецЕсли;
	
	Если Не КомандаЗаполнитьНаСервере() Тогда
		текстПредупреждения = НСтр("ru = 'Отсутствуют данные для заполнения!'");
		ПоказатьПредупреждение(, текстПредупреждения, 15);
	КонецЕсли;
	
КонецПроцедуры
