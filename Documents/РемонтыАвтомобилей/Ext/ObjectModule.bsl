
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(отказ, режимПроведения)
	
	Движения.ВзаиморасчетыСПоставщиками.Записывать = Истина;
	Движения.ФинансовыйРезультат.Записывать = Истина;
	Движения.ПартииТоваровНаСкладах.Записывать = Истина;
	
	Движения.ВзаиморасчетыСПоставщиками.Очистить();
	Движения.ФинансовыйРезультат.Очистить();
	
	Если ОперацияДокумента = Справочники.ОперацииДокументов.АктНаРемонтАвтомобиля Тогда
		
		запрос = Новый Запрос;
		
		#Область ТекстЗапроса
		
		запрос.Текст =
		"ВЫБРАТЬ
		|	РемонтыАвтомобилейВыполненныеРаботы.Ссылка,
		|	РемонтыАвтомобилейВыполненныеРаботы.Ссылка.Дата КАК Период,
		|	РемонтыАвтомобилейВыполненныеРаботы.Ссылка.Контрагент,
		|	РемонтыАвтомобилейВыполненныеРаботы.Ссылка.Подразделение,
		|	РемонтыАвтомобилейВыполненныеРаботы.Ссылка.Автомобиль КАК Объект,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.РемонтыАвтомобилей) КАК СтатьяЗатрат,
		|	РемонтыАвтомобилейВыполненныеРаботы.Сумма,
		|	ВалютаУчета.Значение КАК Валюта,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыИспользованныхЗапчастей.Покупные) КАК ВидИспользования,
		|	РемонтыАвтомобилейВыполненныеРаботы.ВидРабот КАК Номенклатура,
		|	ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка) КАК Склад,
		|	РемонтыАвтомобилейВыполненныеРаботы.Количество
		|ПОМЕСТИТЬ ВТ_ТаблицаДокумента
		|ИЗ
		|	Документ.РемонтыАвтомобилей.ВыполненныеРаботы КАК РемонтыАвтомобилейВыполненныеРаботы,
		|	Константа.ВалютаУчета КАК ВалютаУчета
		|ГДЕ
		|	РемонтыАвтомобилейВыполненныеРаботы.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РемонтыАвтомобилейИспользованныеЗапчасти.Ссылка,
		|	РемонтыАвтомобилейИспользованныеЗапчасти.Ссылка.Дата,
		|	РемонтыАвтомобилейИспользованныеЗапчасти.Ссылка.Контрагент,
		|	РемонтыАвтомобилейИспользованныеЗапчасти.Ссылка.Подразделение,
		|	РемонтыАвтомобилейИспользованныеЗапчасти.Ссылка.Автомобиль,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.РасходыНаЗапчасти),
		|	РемонтыАвтомобилейИспользованныеЗапчасти.Сумма,
		|	ВалютаУчета.Значение,
		|	РемонтыАвтомобилейИспользованныеЗапчасти.ВидИспользованойЗапчасти,
		|	РемонтыАвтомобилейИспользованныеЗапчасти.Запчасть,
		|	РемонтыАвтомобилейИспользованныеЗапчасти.Запчасть.Родитель.Склад,
		|	РемонтыАвтомобилейИспользованныеЗапчасти.Количество
		|ИЗ
		|	Документ.РемонтыАвтомобилей.ИспользованныеЗапчасти КАК РемонтыАвтомобилейИспользованныеЗапчасти,
		|	Константа.ВалютаУчета КАК ВалютаУчета
		|ГДЕ
		|	РемонтыАвтомобилейИспользованныеЗапчасти.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ВТ_ТаблицаДокумента.Период,
		|	ВТ_ТаблицаДокумента.Подразделение,
		|	ВТ_ТаблицаДокумента.Контрагент,
		|	СУММА(ВТ_ТаблицаДокумента.Сумма) КАК Сумма,
		|	ВТ_ТаблицаДокумента.Валюта
		|ИЗ
		|	ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
		|ГДЕ
		|	ВТ_ТаблицаДокумента.Сумма <> 0
		|	И ВТ_ТаблицаДокумента.ВидИспользования = ЗНАЧЕНИЕ(Перечисление.ВидыИспользованныхЗапчастей.Покупные)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ТаблицаДокумента.Контрагент,
		|	ВТ_ТаблицаДокумента.Подразделение,
		|	ВТ_ТаблицаДокумента.Период,
		|	ВТ_ТаблицаДокумента.Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТаблицаДокумента.Период,
		|	ВТ_ТаблицаДокумента.Подразделение,
		|	ВТ_ТаблицаДокумента.СтатьяЗатрат,
		|	ВТ_ТаблицаДокумента.Объект,
		|	СУММА(ВТ_ТаблицаДокумента.Сумма) КАК СуммаРасходов
		|ИЗ
		|	ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
		|ГДЕ
		|	ВТ_ТаблицаДокумента.Сумма <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ТаблицаДокумента.Период,
		|	ВТ_ТаблицаДокумента.Подразделение,
		|	ВТ_ТаблицаДокумента.СтатьяЗатрат,
		|	ВТ_ТаблицаДокумента.Объект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ВТ_ТаблицаДокумента.Период,
		|	ВТ_ТаблицаДокумента.Подразделение,
		|	ВТ_ТаблицаДокумента.Склад,
		|	ВТ_ТаблицаДокумента.Номенклатура,
		|	ЗНАЧЕНИЕ(ПланСчетов.Внутренний.ЗапасныеЧасти) КАК СчетУчета,
		|	ВТ_ТаблицаДокумента.Валюта,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыХраненияЗапасов.НаСкладе) КАК ВидХранения,
		|	ВТ_ТаблицаДокумента.Количество,
		|	ВТ_ТаблицаДокумента.Сумма КАК Стоимость,
		|	ВТ_ТаблицаДокумента.Сумма КАК СтоимостьВал,
		|	ЗНАЧЕНИЕ(ПланСчетов.Внутренний.Затраты) КАК КорСчет
		|ИЗ
		|	ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
		|ГДЕ
		|	ВТ_ТаблицаДокумента.ВидИспользования = ЗНАЧЕНИЕ(Перечисление.ВидыИспользованныхЗапчастей.Собственные)
		|	И ВТ_ТаблицаДокумента.Сумма <> 0";
		
		#КонецОбласти 
		
		запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		результатЗапроса = запрос.ВыполнитьПакет();
		
		Движения.ВзаиморасчетыСПоставщиками.Загрузить(результатЗапроса[1].Выгрузить());
		Движения.ФинансовыйРезультат.Загрузить(результатЗапроса[2].Выгрузить());
		Движения.ПартииТоваровНаСкладах.Загрузить(результатЗапроса[3].Выгрузить());
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(отказ, режимЗаписи, режимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Подразделение.Пустая() Тогда
		
		ТекстСообщения = НСтр("ru = 'Запись невозможна без заполнения Подразделения.'");
		ПроверкаДанныхКлиентСервер.СообщитьОбОшибке(отказ, текстСообщения, ЭтотОбъект, "Подразделение");
		
	КонецЕсли;
	
	Если ПоказанияСпидометра = 0 Тогда
		
		ТекстСообщения = НСтр("ru = 'Запись невозможна без заполнения показания спидометра.'");
		ПроверкаДанныхКлиентСервер.СообщитьОбОшибке(отказ, текстСообщения, ЭтотОбъект, "ПоказанияСпидометра");
		
	КонецЕсли;
	
	СуммаДокумента = ОбщегоНазначенияСервер.ПолучитьСуммуДокумента(ЭтотОбъект, "ВыполненныеРаботы,ИспользованныеЗапчасти");

КонецПроцедуры

Процедура ПриКопировании(объектКопирования)
	
	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеСкопированногоДокумента(ЭтотОбъект, объектКопирования);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(отказ, проверяемыеРеквизиты)
	
	Если ОперацияДокумента = Справочники.ОперацииДокументов.АктНаРемонтАвтомобиля Тогда
		
		проверяемыеРеквизиты.Добавить("Контрагент");
		проверяемыеРеквизиты.Добавить("ИспользованныеЗапчасти.ВидИспользованойЗапчасти");
		проверяемыеРеквизиты.Добавить("ИспользованныеЗапчасти.Запчасть");
		
	КонецЕсли;
	
	
КонецПроцедуры
	
#КонецОбласти 