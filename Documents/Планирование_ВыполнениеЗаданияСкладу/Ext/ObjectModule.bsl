
#Область ОбработчикиСобытий

Процедура ПриКопировании(объектКопирования)
	
	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеСкопированногоДокумента(ЭтотОбъект, объектКопирования);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(данныеЗаполнения, стандартнаяОбработка)
	
	заполненНаОснованииДокумента = Ложь;

	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеНовогоДокумента(ЭтотОбъект, данныеЗаполнения);
	
	// Заполнение по умолчанию
	Если Подразделение.Пустая() Тогда
		Подразделение = ПользователиСервер.ПолучитьЗначениеНастройки("ОсновноеПодразделение");
	КонецЕсли; 
	
	Если Ответственный.Пустая() Тогда
		Ответственный = ПользователиСервер.ПолучитьЗначениеНастройки("ОсновнойОтветственный");
	КонецЕсли;
	
	// Ввод на основании.
	типДанныхЗаполнения = ТипЗнч(данныеЗаполнения);
	
	Если типДанныхЗаполнения = Тип("Структура") Тогда
		
		ЗаполнитьДокументПоОтбору(данныеЗаполнения);
		
	ИначеЕсли типДанныхЗаполнения = Тип("ДокументСсылка.Планирвоание_ЗаданиеСкладу") Тогда
		
		ЗаполнитьДокументНаОснованииЗаданиеСкладу(данныеЗаполнения);
		заполненНаОснованииДокумента = Истина;
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаПроведения(отказ, режимПроведения)
	
	Движения.ВзаиморасчетыССотрудниками.Записывать = Истина;
	Движения.ФинансовыйРезультат.Записывать = Истина;
	Движения.РасчетыПоОплатеТруда.Записывать = Истина;
	
	запрос = Новый Запрос;
	
	#Область ТекстЗапроса
	
	запрос.Текст =
	"ВЫБРАТЬ
	|	Планирование_ВыполнениеПлановПоЗаносуДенегРасчетЗарплаты.Ссылка.Подразделение,
	|	НАЧАЛОПЕРИОДА(Планирование_ВыполнениеПлановПоЗаносуДенегРасчетЗарплаты.Ссылка.Дата, МЕСЯЦ) КАК ПериодРегистрации,
	|	Планирование_ВыполнениеПлановПоЗаносуДенегРасчетЗарплаты.Ссылка.Дата КАК Период,
	|	Планирование_ВыполнениеПлановПоЗаносуДенегРасчетЗарплаты.Сотрудник,
	|	Планирование_ВыполнениеПлановПоЗаносуДенегРасчетЗарплаты.ВидРасчета,
	|	СУММА(Планирование_ВыполнениеПлановПоЗаносуДенегРасчетЗарплаты.Результат) КАК Результат,
	|	Планирование_ВыполнениеПлановПоЗаносуДенегРасчетЗарплаты.Сотрудник.КатегорияРаботника.СтатьяЗатратПоЗарплате КАК СтатьяЗатрат
	|ПОМЕСТИТЬ ВТ_ДанныеДокумента
	|ИЗ
	|	Документ.Планирование_ВыполнениеПлановПоЗаносуДенег.РасчетЗарплаты КАК Планирование_ВыполнениеПлановПоЗаносуДенегРасчетЗарплаты
	|ГДЕ
	|	Планирование_ВыполнениеПлановПоЗаносуДенегРасчетЗарплаты.Ссылка = &Ссылка
	|	И Планирование_ВыполнениеПлановПоЗаносуДенегРасчетЗарплаты.Результат <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	Планирование_ВыполнениеПлановПоЗаносуДенегРасчетЗарплаты.Ссылка.Подразделение,
	|	НАЧАЛОПЕРИОДА(Планирование_ВыполнениеПлановПоЗаносуДенегРасчетЗарплаты.Ссылка.Дата, МЕСЯЦ),
	|	Планирование_ВыполнениеПлановПоЗаносуДенегРасчетЗарплаты.Сотрудник,
	|	Планирование_ВыполнениеПлановПоЗаносуДенегРасчетЗарплаты.ВидРасчета,
	|	Планирование_ВыполнениеПлановПоЗаносуДенегРасчетЗарплаты.Сотрудник.КатегорияРаботника.СтатьяЗатратПоЗарплате,
	|	Планирование_ВыполнениеПлановПоЗаносуДенегРасчетЗарплаты.Ссылка.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.Сотрудник,
	|	ЗНАЧЕНИЕ(ПланСчетов.Внутренний.Зарплата) КАК СчетУчета,
	|	ВТ_ДанныеДокумента.Результат КАК Сумма
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.СтатьяЗатрат,
	|	СУММА(ВТ_ДанныеДокумента.Результат) КАК СуммаРасходов
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.СтатьяЗатрат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.Сотрудник,
	|	ВТ_ДанныеДокумента.ПериодРегистрации,
	|	ВТ_ДанныеДокумента.ВидРасчета,
	|	ВТ_ДанныеДокумента.Результат
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента";
	
	#КонецОбласти
	
	запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	результатЗапроса = запрос.ВыполнитьПакет();
	
	Движения.ВзаиморасчетыССотрудниками.Загрузить(результатЗапроса[1].Выгрузить());
	Движения.ФинансовыйРезультат.Загрузить(результатЗапроса[2].Выгрузить());
	Движения.РасчетыПоОплатеТруда.Загрузить(результатЗапроса[3].Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеПоОснованию

Процедура ЗаполнитьДокументПоОтбору(Знач данныеЗаполнения)
	
КонецПроцедуры	

Процедура ЗаполнитьДокументНаОснованииЗаданиеСкладу(Знач данныеЗаполнения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Заполнение реквизитов документа.
	ЗаполнениеОбъектовСервер.ЗаполнитьРеквизитыДокументаПриВводеНаОсновании(ЭтотОбъект, данныеЗаполнения);
	
	запрос = Новый Запрос;
	
	#Область ТекстЗапроса
	
	запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Планирвоание_ЗаданиеСкладуСостав.Ссылка.Подразделение,
	|	Планирвоание_ЗаданиеСкладуСостав.Сотрудник,
	|	Планирвоание_ЗаданиеСкладуСостав.ВидРаботы
	|ПОМЕСТИТЬ ВТ_ДанныеЗаполнения
	|ИЗ
	|	Документ.Планирвоание_ЗаданиеСкладу.Состав КАК Планирвоание_ЗаданиеСкладуСостав
	|ГДЕ
	|	Планирвоание_ЗаданиеСкладуСостав.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеЗаполнения.Сотрудник,
	|	ВТ_ДанныеЗаполнения.ВидРаботы
	|ИЗ
	|	ВТ_ДанныеЗаполнения КАК ВТ_ДанныеЗаполнения";
	
	#КонецОбласти
	
	запрос.УстановитьПараметр("Ссылка", данныеЗаполнения);
	запрос.УстановитьПараметр("ДатаСреза", КонецМесяца(данныеЗаполнения.Дата));
	
	результатЗапроса = запрос.ВыполнитьПакет();
	
	ЭтотОбъект.ИсходныеДанные.Загрузить(результатЗапроса[1].Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти