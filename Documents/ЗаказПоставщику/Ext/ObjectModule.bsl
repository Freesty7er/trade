
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(данныеЗаполнения, текстЗаполнения, стандартнаяОбработка)
	
	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеНовогоДокумента(ЭтотОбъект, данныеЗаполнения);
	
	Если Подразделение.Пустая() Тогда
		Подразделение = ПользователиСервер.ПолучитьЗначениеНастройки("ОсновноеПодразделение");
	КонецЕсли;
	
	// ввод на основании
	мТипЦен = Справочники.ТипыЦен.Закупка;
	
	Если ТипЗнч(данныеЗаполнения) = Тип("ДокументСсылка.КпкЗаявка")
		Или ТипЗнч(данныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		
		// Заполнение реквизитов документа.
		ЗаполнениеОбъектовСервер.ЗаполнитьРеквизитыДокументаПриВводеНаОсновании(ЭтотОбъект, данныеЗаполнения);
		
		Контрагент = НомерЗаказа.Поставщик;
		
		ценыНоменклатуры = ЦенообразованиеСервер.ПолучитьЦеныНоменклатуры(Дата, Подразделение, данныеЗаполнения.Запасы.ВыгрузитьКолонку("Номенклатура"), мТипЦен);
		
		Запасы.Очистить();	// Очистим табличную часть на случай перезаполнения документа.
		Для каждого строкаЗапасы Из данныеЗаполнения.Запасы Цикл
			
			новаяСтрока = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(новаяСтрока, строкаЗапасы);
			
			новаяСтрока.Цена = ценыНоменклатуры[новаяСтрока.Номенклатура].Цена;
			ДокументЗаказПоставщикуКлиентСервер.РассчитатьДанныеСтроки(новаяСтрока);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(отказ, режимПроведения)
	
	Движения.ЗаказыПоставщикам.Записывать = Истина;
	
	запрос = Новый Запрос;
	
	#Область ТекстЗапроса
	
	запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказПоставщикуЗапасы.Ссылка.Дата КАК Период,
	|	ЗаказПоставщикуЗапасы.Ссылка.Подразделение,
	|	ЗаказПоставщикуЗапасы.Номенклатура.Родитель.Склад КАК Склад,
	|	ЗаказПоставщикуЗапасы.Номенклатура,
	|	ЗаказПоставщикуЗапасы.Ссылка.Контрагент,
	|	ЗаказПоставщикуЗапасы.Ссылка КАК ЗаказПоставщику,
	|	ЗаказПоставщикуЗапасы.Количество КАК Заказано
	|ИЗ
	|	Документ.ЗаказПоставщику.Запасы КАК ЗаказПоставщикуЗапасы
	|ГДЕ
	|	ЗаказПоставщикуЗапасы.Ссылка = &Документ";
	
	#КонецОбласти
	
	запрос.УстановитьПараметр("Документ", Ссылка);
	
	Движения.ЗаказыПоставщикам.Загрузить(запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(отказ, проверяемыеРеквизиты)
	
	ОбработкаТабличныхЧастейСервер.ПроверитьДублиСтрокТабличнойЧасти(отказ, этотОбъект, "Запасы", Новый Структура("Номенклатура", "Номенклатура"));

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Подразделение.Пустая() Тогда
		
		ТекстСообщения = НСтр("ru = 'Запись невозможна без заполнения Подразделения.'");
		ПроверкаДанныхКлиентСервер.СообщитьОбОшибке(отказ, текстСообщения, ЭтотОбъект, "Подразделение");
		
	КонецЕсли;
	
	Если ПометкаУдаления = Ссылка.ПометкаУдаления Тогда
		
		Если РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения Тогда
			
			Если Не Отказ Тогда
				
				// Отсортируем по виду номенклатуры.
				Запрос = Новый Запрос;
				
				#Область ТекстЗапроса
				Запрос.Текст =
				"ВЫБРАТЬ
				|	ЗаказПоставщикуЗапасы.Номенклатура,
				|	ЗаказПоставщикуЗапасы.ЕдиницаИзмерения,
				|	ЗаказПоставщикуЗапасы.Количество,
				|	ЗаказПоставщикуЗапасы.Цена,
				|	ЗаказПоставщикуЗапасы.Сумма,
				|	ЗаказПоставщикуЗапасы.СтавкаНДС,
				|	ЗаказПоставщикуЗапасы.СуммаНДС,
				|	ЗаказПоставщикуЗапасы.Всего
				|ИЗ
				|	Документ.ЗаказПоставщику.Запасы КАК ЗаказПоставщикуЗапасы
				|ГДЕ
				|	ЗаказПоставщикуЗапасы.Ссылка = &Ссылка
				|
				|УПОРЯДОЧИТЬ ПО
				|	ЗаказПоставщикуЗапасы.Номенклатура.ВидНоменклатуры.Код";
				#КонецОбласти
				
				Запрос.УстановитьПараметр("Ссылка", Ссылка);
				
				СуммаДокумента = ОбщегоНазначенияСервер.ПолучитьСуммуДокумента(ЭтотОбъект, "Запасы");
				
			КонецЕсли; 
			
		КонецЕсли;
		
	КонецЕсли; 

КонецПроцедуры

Процедура ПриКопировании(объектКопирования)
	
	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеСкопированногоДокумента(ЭтотОбъект, ОбъектКопирования);
	
КонецПроцедуры

#КонецОбласти
