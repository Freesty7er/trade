Функция ПолучитьПредставлениеПлательщика(объект) Экспорт
	
	представлениеПлательщика = "";
	
	корСчет = объект.ОперацияДокумента.КорСчет;
	
	Если корСчет = ПланыСчетов.Внутренний.ВзаиморасчетыСПоставщиками 
		Или корСчет = ПланыСчетов.Внутренний.ВзаиморасчетыСПокупателями Тогда
		
		представлениеПлательщика = СокрЛП(объект.Контрагент);
		
	ИначеЕсли корСчет = ПланыСчетов.Внутренний.Доходы Тогда
		
		представлениеПлательщика = СокрЛП(объект.СтатьяДоходов);
		
	ИначеЕсли корСчет = ПланыСчетов.Внутренний.Затраты Тогда
		
		представлениеПлательщика = СокрЛП(объект.СтатьяЗатрат);
		
	ИначеЕсли корСчет = ПланыСчетов.Внутренний.ВнутрихозяйственныеРасчеты
		Или корСчет = ПланыСчетов.Внутренний.Инкассация Тогда
		
		представлениеПлательщика = СокрЛП(объект.ЦФО);
		
	ИначеЕсли корСчет = ПланыСчетов.Внутренний.РасчетыСПрочимиДебиторами 
		Или корСчет = ПланыСчетов.Внутренний.РасчетыСПодотчетнымиЛицами
		Или корСчет = ПланыСчетов.Внутренний.Зарплата Тогда
		
		представлениеПлательщика = СокрЛП(объект.Сотрудник);
		
	КонецЕсли;
	
	Возврат (представлениеПлательщика);
	
КонецФункции

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства"..
//
Процедура ИнициализироватьДанныеДокумента(документСсылка, структураДополнительныеСвойства) Экспорт
	
	запрос = Новый Запрос;
	запрос.МенеджерВременныхТаблиц = структураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	#Область ТекстЗапроса
	
	// Назначение запросов:
	//	0 - Временная таблица "РеквизитыДокумента". Значения реквизитов документа для использования в движениях документа.
	//	1 - Временная таблица "ДенежныеСредства". 
	//	2 - Временная таблица "ВзаиморасчетыСПокупателями". 
	//	3 - Временная таблица "ВзаиморасчетыССотрудниками". 
	//	4 - Временная таблица "ВзаиморасчетыПоВыданнымКредитам". 
	//	5 - Временная таблица "ВзаиморасчетыСПоставщиками". 
	//	6 - Временная таблица "ФинансовыйРезультат". 
	//	7 - Временная таблица "ПартииТоваровНаСкладах". 
	//  8 - Временная таблица "РозничнаяВыручка"
	
	запрос.Текст =
	"ВЫБРАТЬ
	|	ПоступлениеДенег.Ссылка,
	|	ПоступлениеДенег.Дата,
	|	ПоступлениеДенег.Подразделение,
	|	ПоступлениеДенег.Контрагент,
	|	ВЫБОР
	|		КОГДА ПоступлениеДенег.Контрагент.ТипВеденияВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыВеденияВзаиморасчетов.ПоДоговору)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ВестиРасчетыПоДокументам,
	|	ПараметрыУчетаПоПодразделениямСрезПоследних.ЦенаСклада,
	|	ПоступлениеДенег.ДенежныйСчет,
	|	ПоступлениеДенег.ДенежныйСчетОтправитель,
	|	ПоступлениеДенег.СуммаДокумента,
	|	ПоступлениеДенег.Курс,
	|	ПоступлениеДенег.Кратность,
	|	ПоступлениеДенег.ВалютаДокумента,
	|	ПоступлениеДенег.ОперацияДокумента,
	|	ПоступлениеДенег.КредДокумент,
	|	ПоступлениеДенег.Сотрудник,
	|	ПоступлениеДенег.ЦФО,
	|	ПоступлениеДенег.СтатьяДоходов,
	|	ПоступлениеДенег.СтатьяЗатрат,
	|	ПоступлениеДенег.СуммаДокумента * ВЫБОР
	|		КОГДА ПартииТоваровНаСкладахОстатки.ПродСтоимостьОстаток ЕСТЬ NULL 
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПартииТоваровНаСкладахОстатки.ПродСтоимостьОстаток <= ПартииТоваровНаСкладахОстатки.СтоимостьОстаток
	|					ТОГДА 0
	|				ИНАЧЕ 1 - ПартииТоваровНаСкладахОстатки.СтоимостьОстаток / ПартииТоваровНаСкладахОстатки.ПродСтоимостьОстаток
	|			КОНЕЦ
	|	КОНЕЦ КАК РеализованнаяНаценка
	|ПОМЕСТИТЬ РеквизитыДокумента
	|ИЗ
	|	Документ.ПоступлениеДенег КАК ПоступлениеДенег
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыУчетаПоПодразделениям.СрезПоследних(&МоментВремени, ) КАК ПараметрыУчетаПоПодразделениямСрезПоследних
	|		ПО ПоступлениеДенег.Подразделение = ПараметрыУчетаПоПодразделениямСрезПоследних.СтруктурнаяЕдиница
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровНаСкладах.Остатки(&МоментВремени, ) КАК ПартииТоваровНаСкладахОстатки
	|		ПО ПоступлениеДенег.Подразделение = ПартииТоваровНаСкладахОстатки.Подразделение
	|			И ПоступлениеДенег.ЦФО = ПартииТоваровНаСкладахОстатки.ЦФО
	|			И (ПоступлениеДенег.ОперацияДокумента = ЗНАЧЕНИЕ(Справочник.ОперацииДокументов.ПоступлениеДенегИнкассация))
	|ГДЕ
	|	ПоступлениеДенег.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеквизитыДокумента.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	РеквизитыДокумента.ДенежныйСчет,
	|	РеквизитыДокумента.Подразделение,
	|	РеквизитыДокумента.СуммаДокумента * РеквизитыДокумента.Курс / РеквизитыДокумента.Кратность КАК Сумма,
	|	РеквизитыДокумента.СуммаДокумента КАК СуммаВал,
	|	РеквизитыДокумента.ВалютаДокумента КАК Валюта,
	|	РеквизитыДокумента.ДенежныйСчет.СчетУчета КАК СчетУчета,
	|	РеквизитыДокумента.ОперацияДокумента.КорСчет КАК КорСчет
	|ПОМЕСТИТЬ ВременнаяТаблицаДенежныеСредства
	|ИЗ
	|	РеквизитыДокумента КАК РеквизитыДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеквизитыДокумента.Дата,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	РеквизитыДокумента.ДенежныйСчетОтправитель,
	|	РеквизитыДокумента.Подразделение,
	|	РеквизитыДокумента.СуммаДокумента * РеквизитыДокумента.Курс / РеквизитыДокумента.Кратность,
	|	РеквизитыДокумента.СуммаДокумента,
	|	РеквизитыДокумента.ВалютаДокумента,
	|	РеквизитыДокумента.ДенежныйСчет.СчетУчета,
	|	РеквизитыДокумента.ОперацияДокумента.КорСчет
	|ИЗ
	|	РеквизитыДокумента КАК РеквизитыДокумента
	|ГДЕ
	|	РеквизитыДокумента.ОперацияДокумента = ЗНАЧЕНИЕ(Справочник.ОперацииДокументов.ПоступлениеДенегПеремещениеМеждуСчетами)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеквизитыДокумента.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	РеквизитыДокумента.Подразделение,
	|	ВЫБОР
	|		КОГДА РеквизитыДокумента.Контрагент.ТипВеденияВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыВеденияВзаиморасчетов.ПоДоговору)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Менеджеры.БезМенеджера)
	|		ИНАЧЕ РеквизитыДокумента.КредДокумент.Менеджер
	|	КОНЕЦ КАК Менеджер,
	|	РеквизитыДокумента.Контрагент,
	|	ВЫБОР
	|		КОГДА РеквизитыДокумента.Контрагент.ТипВеденияВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыВеденияВзаиморасчетов.ПоДоговору)
	|			ТОГДА ЗНАЧЕНИЕ(Документ.РасходнаяНакладная.ПустаяСсылка)
	|		ИНАЧЕ РеквизитыДокумента.КредДокумент
	|	КОНЕЦ КАК КредитныйДокумент,
	|	ЗНАЧЕНИЕ(Справочник.ОтделыВМагазинах.ПустаяСсылка) КАК ЦФО,
	|	ЗНАЧЕНИЕ(ПланСчетов.Внутренний.ВзаиморасчетыСПокупателямиЗаТовар) КАК СчетУчета,
	|	РеквизитыДокумента.ВалютаДокумента КАК Валюта,
	|	ЗНАЧЕНИЕ(Справочник.РазделителиУчёта.ПустаяСсылка) КАК РазделительУчёта,
	|	РеквизитыДокумента.СуммаДокумента * РеквизитыДокумента.Курс / РеквизитыДокумента.Кратность КАК Сумма,
	|	РеквизитыДокумента.СуммаДокумента КАК СуммаВал,
	|	РеквизитыДокумента.ДенежныйСчет.СчетУчета КАК КорСчет
	|ПОМЕСТИТЬ ВременнаяТаблицаВзаиморасчетыСПокупателями
	|ИЗ
	|	РеквизитыДокумента КАК РеквизитыДокумента
	|ГДЕ
	|	РеквизитыДокумента.ОперацияДокумента = ЗНАЧЕНИЕ(Справочник.ОперацииДокументов.ПоступлениеДенегОплата)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеквизитыДокумента.Дата,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	РеквизитыДокумента.Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.Менеджеры.БезМенеджера),
	|	РеквизитыДокумента.Контрагент,
	|	ЗНАЧЕНИЕ(Документ.РасходнаяНакладная.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.ОтделыВМагазинах.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(ПланСчетов.Внутренний.ВзаиморасчетыСПокупателямиПоКредитам),
	|	РеквизитыДокумента.ВалютаДокумента,
	|	ЗНАЧЕНИЕ(Справочник.РазделителиУчёта.ПустаяСсылка),
	|	РеквизитыДокумента.СуммаДокумента * РеквизитыДокумента.Курс / РеквизитыДокумента.Кратность,
	|	РеквизитыДокумента.СуммаДокумента,
	|	РеквизитыДокумента.ДенежныйСчет.СчетУчета
	|ИЗ
	|	РеквизитыДокумента КАК РеквизитыДокумента
	|ГДЕ
	|	РеквизитыДокумента.ОперацияДокумента = ЗНАЧЕНИЕ(Справочник.ОперацииДокументов.ВозвратКредитаПоступление)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеквизитыДокумента.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	РеквизитыДокумента.Подразделение,
	|	РеквизитыДокумента.Сотрудник,
	|	РеквизитыДокумента.СуммаДокумента * РеквизитыДокумента.Курс / РеквизитыДокумента.Кратность КАК Сумма,
	|	РеквизитыДокумента.СуммаДокумента КАК СуммаВал,
	|	РеквизитыДокумента.ВалютаДокумента КАК Валюта,
	|	ЗНАЧЕНИЕ(ПланСчетов.Внутренний.Подотчет) КАК СчетУчета,
	|	РеквизитыДокумента.ДенежныйСчет.СчетУчета КАК КорСчет
	|ПОМЕСТИТЬ ВременнаяТаблицаВзаиморасчетыССотрудниками
	|ИЗ
	|	РеквизитыДокумента КАК РеквизитыДокумента
	|ГДЕ
	|	РеквизитыДокумента.ОперацияДокумента = ЗНАЧЕНИЕ(Справочник.ОперацииДокументов.ПоступлениеДенегВозвратПодотчета)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеквизитыДокумента.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	РеквизитыДокумента.Подразделение,
	|	РеквизитыДокумента.Сотрудник,
	|	РеквизитыДокумента.СуммаДокумента * РеквизитыДокумента.Курс / РеквизитыДокумента.Кратность КАК Сумма,
	|	РеквизитыДокумента.СуммаДокумента КАК СуммаВал,
	|	РеквизитыДокумента.ВалютаДокумента КАК Валюта,
	|	ЗНАЧЕНИЕ(ПланСчетов.Внутренний.КредитыИЗаймы) КАК СчетУчета,
	|	РеквизитыДокумента.ДенежныйСчет.СчетУчета КАК КорСчет
	|ПОМЕСТИТЬ ВременнаяТаблицаВзаиморасчетыПоВыданнымКредитам
	|ИЗ
	|	РеквизитыДокумента КАК РеквизитыДокумента
	|ГДЕ
	|	РеквизитыДокумента.ОперацияДокумента = ЗНАЧЕНИЕ(Справочник.ОперацииДокументов.ПоступлениеДенегВозвратКредитаСотрудником)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеквизитыДокумента.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	РеквизитыДокумента.Подразделение,
	|	РеквизитыДокумента.Контрагент,
	|	РеквизитыДокумента.СуммаДокумента * РеквизитыДокумента.Курс / РеквизитыДокумента.Кратность КАК Сумма,
	|	РеквизитыДокумента.СуммаДокумента КАК СуммаВал,
	|	РеквизитыДокумента.ВалютаДокумента КАК Валюта,
	|	ЗНАЧЕНИЕ(ПланСчетов.Внутренний.ВзаиморасчетыСПоставщикамиЗаТовар) КАК СчетУчета,
	|	РеквизитыДокумента.ЦФО КАК ЦФО,
	|	РеквизитыДокумента.ДенежныйСчет.СчетУчета КАК КорСчет
	|ПОМЕСТИТЬ ВременнаяТаблицаВзаиморасчетыСПоставщиками
	|ИЗ
	|	РеквизитыДокумента КАК РеквизитыДокумента
	|ГДЕ
	|	РеквизитыДокумента.ОперацияДокумента = ЗНАЧЕНИЕ(Справочник.ОперацииДокументов.ВозвратДенегОтПоставщика)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеквизитыДокумента.Дата,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	РеквизитыДокумента.Подразделение,
	|	РеквизитыДокумента.Контрагент,
	|	РеквизитыДокумента.СуммаДокумента * РеквизитыДокумента.Курс / РеквизитыДокумента.Кратность,
	|	РеквизитыДокумента.СуммаДокумента,
	|	РеквизитыДокумента.ВалютаДокумента,
	|	ЗНАЧЕНИЕ(ПланСчетов.Внутренний.ВзаиморасчетыСПоставщикамиПоКредитам),
	|	РеквизитыДокумента.ЦФО,
	|	РеквизитыДокумента.ДенежныйСчет.СчетУчета
	|ИЗ
	|	РеквизитыДокумента КАК РеквизитыДокумента
	|ГДЕ
	|	РеквизитыДокумента.ОперацияДокумента = ЗНАЧЕНИЕ(Справочник.ОперацииДокументов.ПолучениеКредита)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеквизитыДокумента.Дата КАК Период,
	|	РеквизитыДокумента.Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиДоходов.ПустаяСсылка) КАК СтатьяДоходов,
	|	РеквизитыДокумента.СтатьяЗатрат,
	|	0 КАК СуммаДоходов,
	|	-РеквизитыДокумента.СуммаДокумента * РеквизитыДокумента.Курс / РеквизитыДокумента.Кратность КАК СуммаРасходов,
	|	РеквизитыДокумента.ЦФО КАК Объект,
	|	РеквизитыДокумента.ДенежныйСчет.СчетУчета КАК КорСчет
	|ПОМЕСТИТЬ ВременнаяТаблицаФинансовыйРезультат
	|ИЗ
	|	РеквизитыДокумента КАК РеквизитыДокумента
	|ГДЕ
	|	РеквизитыДокумента.ОперацияДокумента = ЗНАЧЕНИЕ(Справочник.ОперацииДокументов.ПоступлениеДенегВычетИзЗатрат)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеквизитыДокумента.Дата,
	|	РеквизитыДокумента.Подразделение,
	|	РеквизитыДокумента.СтатьяДоходов,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.ПустаяСсылка),
	|	РеквизитыДокумента.СуммаДокумента * РеквизитыДокумента.Курс / РеквизитыДокумента.Кратность,
	|	0,
	|	ЗНАЧЕНИЕ(Справочник.ОтделыВМагазинах.ПустаяСсылка),
	|	РеквизитыДокумента.ДенежныйСчет.СчетУчета
	|ИЗ
	|	РеквизитыДокумента КАК РеквизитыДокумента
	|ГДЕ
	|	РеквизитыДокумента.ОперацияДокумента = ЗНАЧЕНИЕ(Справочник.ОперацииДокументов.ПоступлениеДенегДоходы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеквизитыДокумента.Дата,
	|	РеквизитыДокумента.Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиДоходов.МаржинальныйДоход),
	|	ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.ПустаяСсылка),
	|	ВЫБОР
	|		КОГДА РеквизитыДокумента.РеализованнаяНаценка < 0
	|			ТОГДА РеквизитыДокумента.СуммаДокумента * 0.164
	|		ИНАЧЕ РеквизитыДокумента.РеализованнаяНаценка
	|	КОНЕЦ * РеквизитыДокумента.Курс / РеквизитыДокумента.Кратность,
	|	0,
	|	РеквизитыДокумента.ЦФО,
	|	РеквизитыДокумента.ДенежныйСчет.СчетУчета
	|ИЗ
	|	РеквизитыДокумента КАК РеквизитыДокумента
	|ГДЕ
	|	РеквизитыДокумента.ОперацияДокумента = ЗНАЧЕНИЕ(Справочник.ОперацииДокументов.ПоступлениеДенегИнкассация)
	|	И РеквизитыДокумента.РеализованнаяНаценка <> 0
	|	И &ЕстьСписаниеРеализованнойНаценки = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеквизитыДокумента.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	РеквизитыДокумента.Подразделение,
	|	РеквизитыДокумента.ЦФО,
	|	РеквизитыДокумента.ЦФО.Склад КАК Склад,
	|	(РеквизитыДокумента.СуммаДокумента - ВЫБОР
	|		КОГДА РеквизитыДокумента.РеализованнаяНаценка < 0
	|			ТОГДА РеквизитыДокумента.СуммаДокумента * 0.164
	|		ИНАЧЕ РеквизитыДокумента.РеализованнаяНаценка
	|	КОНЕЦ) * РеквизитыДокумента.Курс / РеквизитыДокумента.Кратность КАК Стоимость,
	|	РеквизитыДокумента.СуммаДокумента - ВЫБОР
	|		КОГДА РеквизитыДокумента.РеализованнаяНаценка < 0
	|			ТОГДА РеквизитыДокумента.СуммаДокумента * 0.164
	|		ИНАЧЕ РеквизитыДокумента.РеализованнаяНаценка
	|	КОНЕЦ КАК СтоимостьВал,
	|	РеквизитыДокумента.СуммаДокумента * РеквизитыДокумента.Курс / РеквизитыДокумента.Кратность КАК ПродСтоимость,
	|	РеквизитыДокумента.ВалютаДокумента КАК Валюта,
	|	РеквизитыДокумента.ОперацияДокумента.КорСчет КАК СчетУчета,
	|	РеквизитыДокумента.ДенежныйСчет.СчетУчета КАК КорСчет
	|ПОМЕСТИТЬ ВременнаяТаблицаПартииТоваровНаСкладах
	|ИЗ
	|	РеквизитыДокумента КАК РеквизитыДокумента
	|ГДЕ
	|	РеквизитыДокумента.ОперацияДокумента = ЗНАЧЕНИЕ(Справочник.ОперацииДокументов.ПоступлениеДенегИнкассация)
	|	И ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеквизитыДокумента.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	РеквизитыДокумента.Подразделение,
	|	РеквизитыДокумента.ЦФО,
	|	РеквизитыДокумента.СуммаДокумента КАК Сумма,
	|	РеквизитыДокумента.ОперацияДокумента.КорСчет КАК СчетУчета,
	|	РеквизитыДокумента.ДенежныйСчет.СчетУчета КАК КорСчет,
	|	РеквизитыДокумента.ВалютаДокумента КАК Валюта,
	|	РеквизитыДокумента.СуммаДокумента КАК СуммаВал
	|ПОМЕСТИТЬ ВременнаяТаблицаРозничнаяВыручка
	|ИЗ
	|	РеквизитыДокумента КАК РеквизитыДокумента
	|ГДЕ
	|	РеквизитыДокумента.ОперацияДокумента = ЗНАЧЕНИЕ(Справочник.ОперацииДокументов.ПоступлениеДенегИнкассация)";
	
	#КонецОбласти
	
	запрос.УстановитьПараметр("Ссылка", документСсылка);
	запрос.УстановитьПараметр("МоментВремени", Новый Граница(структураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	
	// ВРЕМЕННО!!! Переделать на соединение с регистром "параметрыУчетаПоПодразделению"
	параметрыУчетаПоПодразделению = РегистрыСведений.ПараметрыУчетаПоПодразделениям.ПолучитьПараметрыУчетаПоПодразделению(структураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата, документСсылка.Подразделение, Новый Структура("СписаниеНаценкиПоИнкассации"));
	
	запрос.УстановитьПараметр("ЕстьСписаниеРеализованнойНаценки", параметрыУчетаПоПодразделению.СписаниеНаценкиПоИнкассации);
	
	запрос.ВыполнитьПакет();
	
	#Область ПросмотрВременныхТаблиц
	
	запрос.Текст = "ВЫБРАТЬ * ИЗ РеквизитыДокумента";
	
	запрос.Текст = "ВЫБРАТЬ * ИЗ ВременнаяТаблицаДенежныеСредства";
	запрос.Текст = "ВЫБРАТЬ * ИЗ ВременнаяТаблицаВзаиморасчетыСПокупателями";
	запрос.Текст = "ВЫБРАТЬ * ИЗ ВременнаяТаблицаВзаиморасчетыССотрудниками";
	запрос.Текст = "ВЫБРАТЬ * ИЗ ВременнаяТаблицаВзаиморасчетыПоВыданнымКредитам";
	запрос.Текст = "ВЫБРАТЬ * ИЗ ВременнаяТаблицаВзаиморасчетыСПоставщиками";
	запрос.Текст = "ВЫБРАТЬ * ИЗ ВременнаяТаблицаФинансовыйРезультат";
	запрос.Текст = "ВЫБРАТЬ * ИЗ ВременнаяТаблицаПартииТоваровНаСкладах";
	
	#КонецОбласти
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()