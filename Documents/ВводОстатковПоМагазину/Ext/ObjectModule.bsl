#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(отказ, режимПроведения)
	
	Проводки = Движения.Управленческий;
	ДокументыПродажи = Движения.ДокументыПродажи;
	

	// Товары в рознице
	НоваяПроводка = Проводки.Добавить();
	НоваяПроводка.Период		= Дата;
	НоваяПроводка.Подразделение	= Подразделение;
	НоваяПроводка.Отдел 		= Отдел;
	
	
	НоваяПроводка.СчетДт = ПланыСчетов.Внутренний.ТоварыВРознице;
	
	НоваяПроводка.СчетКт = ПланыСчетов.Внутренний.ПУСТО;
	
	НоваяПроводка.Сумма	= СуммаПродСтоимость;
	НоваяПроводка.Содержание	= "Товары в розн.: "+ СокрЛП(Магазин) + " / " + СокрЛП(Отдел);
	
	// Розничная наценка.
    НоваяПроводка = Проводки.Добавить();
	НоваяПроводка.Период		= Дата;
	НоваяПроводка.Подразделение = Подразделение;
	НоваяПроводка.Отдел 		= Отдел;
	
	НоваяПроводка.СчетДт = ПланыСчетов.Внутренний.ПУСТО;
	
	НоваяПроводка.СчетКт = ПланыСчетов.Внутренний.РозничнаяНаценкаБаза;
	
	НоваяПроводка.Сумма	= СуммаНаценка;
	НоваяПроводка.Содержание	= "Наценка: "+ СокрЛП(Магазин) + " / " + СокрЛП(Отдел);
	
	// Взаиморасчеты
	Для Каждого СтрокаВзаиморасчеты Из Взаиморасчеты Цикл
		
		НоваяПроводка = Проводки.Добавить();
		НоваяПроводка.Период		= Дата;
		НоваяПроводка.Подразделение = Подразделение;
		НоваяПроводка.Отдел 		= Отдел;
		
		НоваяПроводка.СчетДт = ПланыСчетов.Внутренний.ПУСТО;
		
		НоваяПроводка.СчетКт = ПланыСчетов.Внутренний.ПоставщикиБаза;
		НоваяПроводка.СубконтоКт.Контрагенты 		= СтрокаВзаиморасчеты.Контрагент;
		НоваяПроводка.СубконтоКт.ДокументПоставки 	= СтрокаВзаиморасчеты.ДокументПоставки;
		
		
		НоваяПроводка.Сумма	= СтрокаВзаиморасчеты.СуммаНашДолг - СтрокаВзаиморасчеты.СуммаНамДолг;
		НоваяПроводка.Содержание	= "Накладная: "+ СокрЛП(СтрокаВзаиморасчеты.ДокументПоставки) + "("+СокрЛП(Магазин) + " / " + СокрЛП(Отдел) + ")";
		
		// Документ продажи.
		Движение = ДокументыПродажи.Добавить();
		Движение.Подразделение = Подразделение;
		Движение.Контрагент = СтрокаВзаиморасчеты.Контрагент;
		Движение.НомерДокумента = СтрокаВзаиморасчеты.ДокументПоставки;
		Движение.ДатаДокумента = СтрокаВзаиморасчеты.ДатаДокумента;
		
		Движение.Магазин	= Магазин;
		Движение.Отдел		= Отдел;
		Движение.Сумма 		= СтрокаВзаиморасчеты.СуммаНашДолг - СтрокаВзаиморасчеты.СуммаНамДолг;
		
	КонецЦикла;
		
		
	Проводки.Записать();
	ДокументыПродажи.Записать();
	
	
КонецПроцедуры

Процедура ПередЗаписью(отказ, режимЗаписи, режимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Подразделение.Пустая() Тогда
		
		ТекстСообщения = НСтр("ru = 'Запись невозможна без заполнения Подразделения.'");
		ПроверкаДанныхКлиентСервер.СообщитьОбОшибке(отказ, текстСообщения, ЭтотОбъект, "Подразделение");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(объектКопирования)
	
	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеСкопированногоДокумента(ЭтотОбъект, объектКопирования);
	
КонецПроцедуры

#КонецОбласти 