#Область ОбработчикиСобытий

Процедура ПередЗаписью(отказ, режимЗаписи, режимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Подразделение.Пустая() Тогда
		
		ТекстСообщения = НСтр("ru = 'Запись невозможна без заполнения Подразделения.'");
		ПроверкаДанныхКлиентСервер.СообщитьОбОшибке(отказ, текстСообщения, ЭтотОбъект, "Подразделение");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(отказ)
	
	Если Не Статус = Перечисления.СтатусыЗаданийТорговымПредставителям.КОтработке Или ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	
	этоНовыйОбъект = Ложь;
	ДополнительныеСвойства.Свойство("ЭтоНовый", этоНовыйОбъект);
	
	объектИзменён = Ложь;
	ДополнительныеСвойства.Свойство("ОбъектИзменён", объектИзменён);
	
	Если этоНовыйОбъект Или объектИзменён Тогда 
		
		УстановитьПривилегированныйРежим(Истина);
		
		НоваяЗаписьДокументыАгентов = РегистрыСведений.БПАГДокументыАгентов.СоздатьМенеджерЗаписи();
		НоваяЗаписьДокументыАгентов.ВидДокумента = Перечисления.БПАГВидыДокументов.Анкета;
		
		НоваяЗаписьДокументыАгентов.Период 			= ТекущаяДатаСеанса();
		НоваяЗаписьДокументыАгентов.Агент 			= КонтроллерОКК;
		НоваяЗаписьДокументыАгентов.Документ 		= Ссылка;
		НоваяЗаписьДокументыАгентов.ТорговаяТочка 	= ТорговаяТочка;
		
		НоваяЗаписьДокументыАгентов.ДатаСозданияНаУстройстве = Дата;
		НоваяЗаписьДокументыАгентов.ДатаИзмененияНаУстройстве = ТекущаяДатаСеанса();
		
		НоваяЗаписьДокументыАгентов.ИД = Ссылка.УникальныйИдентификатор();
		НоваяЗаписьДокументыАгентов.НомерДокументаНаУстройстве = Номер;
		
		НоваяЗаписьДокументыАгентов.Записать();
		
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(объектКопирования)
	
	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеСкопированногоДокумента(ЭтотОбъект, ОбъектКопирования);
	
	Статус = Перечисления.СтатусыЗаданийТорговымПредставителям.НеПодготовлено;
	ТорговыйАгент = Неопределено;
	Супервайзер = Неопределено;
	
КонецПроцедуры

#КонецОбласти