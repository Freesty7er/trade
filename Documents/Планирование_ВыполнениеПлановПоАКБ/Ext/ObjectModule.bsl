
#Область ОбработчикиСобытий

Процедура ПриКопировании(объектКопирования)
	
	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеСкопированногоДокумента(ЭтотОбъект, объектКопирования);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(данныеЗаполнения, стандартнаяОбработка)
	
	заполненНаОснованииДокумента = Ложь;

	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеНовогоДокумента(ЭтотОбъект, данныеЗаполнения);
	
	// Заполнение по умолчанию
	Если Подразделение.Пустая() Тогда
		Подразделение = ПользователиСервер.ПолучитьЗначениеНастройки("ОсновноеПодразделение");
	КонецЕсли; 
	
	Если Ответственный.Пустая() Тогда
		Ответственный = ПользователиСервер.ПолучитьЗначениеНастройки("ОсновнойОтветственный");
	КонецЕсли;
	
	// Ввод на основании.
	типДанныхЗаполнения = ТипЗнч(данныеЗаполнения);
	
	Если типДанныхЗаполнения = Тип("Структура") Тогда
		
		ЗаполнитьДокументПоОтбору(данныеЗаполнения);
		
	ИначеЕсли типДанныхЗаполнения = Тип("ДокументСсылка.Планирование_УстановкаПлановПоАКБ") Тогда
		
		ЗаполнитьДокументНаОснованииУстановкиПлановПоАКБ(данныеЗаполнения);
		заполненНаОснованииДокумента = Истина;
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаПроведения(отказ, режимПроведения)
	
	Движения.ВзаиморасчетыССотрудниками.Записывать = Истина;
	Движения.ФинансовыйРезультат.Записывать = Истина;
	Движения.РасчетыПоОплатеТруда.Записывать = Истина;
	
	запрос = Новый Запрос;
	
	#Область ТекстЗапроса
	
	запрос.Текст =
	"ВЫБРАТЬ
	|	Планирование_ВыполнениеПлановПоАКБРасчетЗарплаты.Ссылка.Подразделение,
	|	НАЧАЛОПЕРИОДА(Планирование_ВыполнениеПлановПоАКБРасчетЗарплаты.Ссылка.Дата, МЕСЯЦ) КАК ПериодРегистрации,
	|	Планирование_ВыполнениеПлановПоАКБРасчетЗарплаты.Ссылка.Дата КАК Период,
	|	Планирование_ВыполнениеПлановПоАКБРасчетЗарплаты.Сотрудник,
	|	Планирование_ВыполнениеПлановПоАКБРасчетЗарплаты.ВидРасчета,
	|	СУММА(Планирование_ВыполнениеПлановПоАКБРасчетЗарплаты.Результат) КАК Результат,
	|	Планирование_ВыполнениеПлановПоАКБРасчетЗарплаты.Сотрудник.КатегорияРаботника.СтатьяЗатратПоЗарплате КАК СтатьяЗатрат
	|ПОМЕСТИТЬ ВТ_ДанныеДокумента
	|ИЗ
	|	Документ.Планирование_ВыполнениеПлановПоАКБ.РасчетЗарплаты КАК Планирование_ВыполнениеПлановПоАКБРасчетЗарплаты
	|ГДЕ
	|	Планирование_ВыполнениеПлановПоАКБРасчетЗарплаты.Ссылка = &Ссылка
	|	И Планирование_ВыполнениеПлановПоАКБРасчетЗарплаты.Результат <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	Планирование_ВыполнениеПлановПоАКБРасчетЗарплаты.Ссылка.Подразделение,
	|	НАЧАЛОПЕРИОДА(Планирование_ВыполнениеПлановПоАКБРасчетЗарплаты.Ссылка.Дата, МЕСЯЦ),
	|	Планирование_ВыполнениеПлановПоАКБРасчетЗарплаты.Сотрудник,
	|	Планирование_ВыполнениеПлановПоАКБРасчетЗарплаты.ВидРасчета,
	|	Планирование_ВыполнениеПлановПоАКБРасчетЗарплаты.Сотрудник.КатегорияРаботника.СтатьяЗатратПоЗарплате,
	|	Планирование_ВыполнениеПлановПоАКБРасчетЗарплаты.Ссылка.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.Сотрудник,
	|	ЗНАЧЕНИЕ(ПланСчетов.Внутренний.Зарплата) КАК СчетУчета,
	|	ВТ_ДанныеДокумента.Результат КАК Сумма
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.СтатьяЗатрат,
	|	СУММА(ВТ_ДанныеДокумента.Результат) КАК СуммаРасходов
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.СтатьяЗатрат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.Подразделение,
	|	ВТ_ДанныеДокумента.Сотрудник,
	|	ВТ_ДанныеДокумента.ПериодРегистрации,
	|	ВТ_ДанныеДокумента.ВидРасчета,
	|	ВТ_ДанныеДокумента.Результат
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента";
	
	#КонецОбласти
	
	запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	результатЗапроса = запрос.ВыполнитьПакет();
	
	Движения.ВзаиморасчетыССотрудниками.Загрузить(результатЗапроса[1].Выгрузить());
	Движения.ФинансовыйРезультат.Загрузить(результатЗапроса[2].Выгрузить());
	Движения.РасчетыПоОплатеТруда.Загрузить(результатЗапроса[3].Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеПоОснованию

Процедура ЗаполнитьДокументПоОтбору(Знач данныеЗаполнения)
	
КонецПроцедуры	

Процедура ЗаполнитьДокументНаОснованииУстановкиПлановПоАКБ(Знач данныеЗаполнения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Заполнение реквизитов документа.
	ЗаполнениеОбъектовСервер.ЗаполнитьРеквизитыДокументаПриВводеНаОсновании(ЭтотОбъект, данныеЗаполнения);
	
	запрос = Новый Запрос;
	
	#Область ТекстЗапроса
	
	запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Планирование_УстановкаПлановПоАКБСостав.Менеджер,
	|	Планирование_УстановкаПлановПоАКБСостав.Ссылка.ПоказательКПИ,
	|	Планирование_УстановкаПлановПоАКБСостав.Ссылка.Подразделение
	|ПОМЕСТИТЬ ВТ_ДанныеЗаполнения
	|ИЗ
	|	Документ.Планирование_УстановкаПлановПоАКБ.Состав КАК Планирование_УстановкаПлановПоАКБСостав
	|ГДЕ
	|	Планирование_УстановкаПлановПоАКБСостав.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеЗаполнения.Менеджер
	|ИЗ
	|	ВТ_ДанныеЗаполнения КАК ВТ_ДанныеЗаполнения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Менеджеры.Менеджер,
	|	КД_НазначенияТорговыхПредставителейСрезПоследних.Сотрудник КАК Сотрудник
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ВТ_ДанныеЗаполнения.Менеджер КАК Менеджер,
	|		ВТ_ДанныеЗаполнения.Подразделение КАК Подразделение
	|	ИЗ
	|		ВТ_ДанныеЗаполнения КАК ВТ_ДанныеЗаполнения
	|	ГДЕ
	|		ВТ_ДанныеЗаполнения.Менеджер.ЭтоГруппа = ЛОЖЬ) КАК Менеджеры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КД_НазначенияТорговыхПредставителей.СрезПоследних(&ДатаСреза, ) КАК КД_НазначенияТорговыхПредставителейСрезПоследних
	|		ПО Менеджеры.Подразделение = КД_НазначенияТорговыхПредставителейСрезПоследних.Подразделение
	|			И Менеджеры.Менеджер = КД_НазначенияТорговыхПредставителейСрезПоследних.Менеджер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Супервайзеры.Менеджер,
	|	КД_НазначенияСупервайзеровСрезПоследних.Сотрудник
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ВТ_ДанныеЗаполнения.Менеджер КАК Менеджер,
	|		ВТ_ДанныеЗаполнения.Подразделение КАК Подразделение
	|	ИЗ
	|		ВТ_ДанныеЗаполнения КАК ВТ_ДанныеЗаполнения
	|	ГДЕ
	|		ВТ_ДанныеЗаполнения.Менеджер.ЭтоГруппа = ИСТИНА) КАК Супервайзеры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КД_НазначенияСупервайзеров.СрезПоследних(&ДатаСреза, ) КАК КД_НазначенияСупервайзеровСрезПоследних
	|		ПО Супервайзеры.Подразделение = КД_НазначенияСупервайзеровСрезПоследних.Подразделение
	|			И Супервайзеры.Менеджер = КД_НазначенияСупервайзеровСрезПоследних.Менеджер";
	
	#КонецОбласти
	
	запрос.УстановитьПараметр("Ссылка", данныеЗаполнения);
	запрос.УстановитьПараметр("ДатаСреза", КонецМесяца(данныеЗаполнения.Дата));
	
	результатЗапроса = запрос.ВыполнитьПакет();
	
	ЭтотОбъект.ИсходныеДанные.Загрузить(результатЗапроса[1].Выгрузить());
	ЭтотОбъект.БазыРасчетаЗарплаты.Загрузить(результатЗапроса[2].Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти