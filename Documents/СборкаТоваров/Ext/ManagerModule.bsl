Процедура ПечатьСборкаТоваров(ТабличныйДокумент, Шапка, ВыборкаСтрокЗапасы)
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СборкаТоваров_СборкаТОваров";
	
	Макет = УправлениеПечатью.ПолучитьМакет("Документ.СборкаТоваров.СборкаТоваров");
	
	НомерДокумента = Шапка.Номер;
	ДатаДокумента = Шапка.Дата;

	//КодЯзыкаПечать = Локализация.ПолучитьЯзыкФормированияПечатныхФорм();	
	//Макет.КодЯзыкаМакета = КодЯзыкаПечать;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	
	// вынести получить данные организации и т.п.
	ТекстЗаголовка = "Сборка товаров № "+НомерДокумента+ " от "+ДатаДокумента;
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	
	ОбластьМакета = Макет.ПолучитьОбласть("Комплект");
	Номенклатура = Шапка.Номенклатура;
	Количество = Шапка.Количество;
	ОбластьМакета.Параметры.Номенклатура = Номенклатура;
	ОбластьМакета.Параметры.Количество = Количество;
	ОбластьМакета.Параметры.Цена = Шапка.Цена;
	ОбластьМакета.Параметры.Сумма = Шапка.Цена*Количество;
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	НомСтр = 0;
	итСумма = 0;
	
	Пока ВыборкаСтрокЗапасы.Следующий() Цикл
		
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		НомСтр = НомСтр + 1;
		ОбластьМакета.Параметры.НоменклатураПредставление = ВыборкаСтрокЗапасы.Номенклатура;
		ОбластьМакета.Параметры.Количество = ВыборкаСтрокЗапасы.Количество;
		ОбластьМакета.Параметры.Упаковка = ВыборкаСтрокЗапасы.Номенклатура.ЕдиницаИзмерения;
		ОбластьМакета.Параметры.Цена = ВыборкаСтрокЗапасы.Цена;
		ОбластьМакета.Параметры.Сумма = ВыборкаСтрокЗапасы.Сумма;
		ОбластьМакета.Параметры.НомерСтроки = НомСтр;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		итСумма = итСумма + ВыборкаСтрокЗапасы.Сумма;		
		
	КонецЦикла;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
	ОбластьМакета.Параметры.итСумма = итСумма;	
	ОбластьМакета.Параметры.ОтветственныйПредставление = Шапка.Автор;
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
КонецПроцедуры

Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ИмяМакета, КоллекцияПечатныхФорм)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_СборкаТоваров";
	
	ПервыйДокумент = Истина;
	
	Для Каждого ТекущийДокумент Из МассивОбъектов Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если ИмяМакета = "СборкаТоваров" Тогда
			
			запрос = Новый Запрос();
			
			#Область ТекстЗапроса
			
			запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СборкаТоваров.Ссылка,
			|	СборкаТоваров.ВерсияДанных,
			|	СборкаТоваров.ПометкаУдаления,
			|	СборкаТоваров.Номер,
			|	СборкаТоваров.Дата,
			|	СборкаТоваров.Проведен,
			|	СборкаТоваров.Подразделение,
			|	СборкаТоваров.ТипЦен,
			|	СборкаТоваров.Номенклатура КАК Номенклатура,
			|	СборкаТоваров.Склад,
			|	СборкаТоваров.Количество КАК Количество,
			|	СборкаТоваров.Цена,
			|	СборкаТоваров.СуммаДокумента,
			|	СборкаТоваров.Комментарий,
			|	СборкаТоваров.Автор,
			|	СборкаТоваров.Ответственный,
			|	СборкаТоваров.Редактор,
			|	СборкаТоваров.ИсходнаяНоменклатура.(
			|		Ссылка,
			|		НомерСтроки,
			|		Номенклатура,
			|		Склад,
			|		Количество,
			|		Цена,
			|		Сумма
			|	)
			|ИЗ
			|	Документ.СборкаТоваров КАК СборкаТоваров
			|ГДЕ
			|	СборкаТоваров.Ссылка = &ТекущийДокумент";
			
			#КонецОбласти
			
			запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
			запрос.УстановитьПараметр("ДатаСреза", КонецДня(ТекущийДокумент.Дата));
			
			шапка = запрос.Выполнить().Выбрать();
			шапка.Следующий();
			
			номерДокумента = Шапка.Номер;
			
			выборкаСтрокЗапасы = шапка.ИсходнаяНоменклатура.Выбрать();
			ПечатьСборкаТоваров(ТабличныйДокумент, Шапка, ВыборкаСтрокЗапасы);
			ТабличныйДокумент.ОриентацияСтраницы=ОриентацияСтраницы.Ландшафт;
			
		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ТекущийДокумент);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатнаяФорма()

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов,
				 ПараметрыПечати,
				 КоллекцияПечатныхФорм,
				 ОбъектыПечати,
				 ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СборкаТоваров", "СборкаТоваров", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "СборкаТоваров", КоллекцияПечатныхФорм));
	
КонецПроцедуры


