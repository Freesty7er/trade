#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ЗапретСкидокНоменклатуры.Записывать = Истина;
	
	запрос = Новый Запрос;
	
	#Область ТекстЗапроса
	
	запрос.Текст = 
	"ВЫБРАТЬ
	|	УстановкаЗапретаСкидокПоНоменклатуреСписокНоменклатуры.Ссылка.Дата КАК Период,
	|	УстановкаЗапретаСкидокПоНоменклатуреСписокНоменклатуры.Ссылка.Подразделение,
	|	УстановкаЗапретаСкидокПоНоменклатуреСписокНоменклатуры.Номенклатура,
	|	УстановкаЗапретаСкидокПоНоменклатуреСписокКаналовСбыта.КаналСбыта,
	|	КОНЕЦПЕРИОДА(УстановкаЗапретаСкидокПоНоменклатуреСписокКаналовСбыта.Ссылка.ДатаОкончания, ДЕНЬ) КАК ДатаОкончания
	|ИЗ
	|	Документ.УстановкаЗапретаСкидокПоНоменклатуре.СписокНоменклатуры КАК УстановкаЗапретаСкидокПоНоменклатуреСписокНоменклатуры,
	|	Документ.УстановкаЗапретаСкидокПоНоменклатуре.СписокКаналовСбыта КАК УстановкаЗапретаСкидокПоНоменклатуреСписокКаналовСбыта
	|ГДЕ
	|	УстановкаЗапретаСкидокПоНоменклатуреСписокНоменклатуры.Ссылка = &Ссылка
	|	И УстановкаЗапретаСкидокПоНоменклатуреСписокКаналовСбыта.Ссылка = &Ссылка";
		
	#КонецОбласти
	
	запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	результатЗапроса = запрос.Выполнить();
	
	Движения.ЗапретСкидокНоменклатуры.Загрузить(результатЗапроса.Выгрузить());
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(отказ, проверяемыеРеквизиты)
	
	ОбработкаТабличныхЧастейСервер.ПроверитьДублиСтрокТабличнойЧасти(отказ, этотОбъект, "СписокНоменклатуры", Новый Структура("Номенклатура", "Номенклатура"));
	ОбработкаТабличныхЧастейСервер.ПроверитьДублиСтрокТабличнойЧасти(отказ, этотОбъект, "СписокКаналовСбыта", Новый Структура("КаналСбыта", "КаналСбыта"));
	
КонецПроцедуры

Процедура ПередЗаписью(отказ, режимЗаписи, режимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Подразделение.Пустая() Тогда
		
		ТекстСообщения = НСтр("ru = 'Запись невозможна без заполнения Подразделения.'");
		ПроверкаДанныхКлиентСервер.СообщитьОбОшибке(отказ, текстСообщения, ЭтотОбъект, "Подразделение");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(объектКопирования)
	
	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеСкопированногоДокумента(ЭтотОбъект, объектКопирования);
	
КонецПроцедуры

#КонецОбласти 