&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВзаиморасчетыСПокупателямиОстатки.Контрагент КАК Контрагент,
	|	ВзаиморасчетыСПокупателямиОстатки.Менеджер,
	|	ВзаиморасчетыСПокупателямиОстатки.КредитныйДокумент КАК КредДокумент,
	|	ВзаиморасчетыСПокупателямиОстатки.СуммаОстаток КАК Долг
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыСПокупателями.Остатки(
	|			&МоментВремени,
	|			ВЫБОР
	|					КОГДА &ЕстьОтборПоКонтрагенту
	|						ТОГДА Контрагент В ИЕРАРХИИ (&Контрагент)
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ
	|				И ВЫБОР
	|					КОГДА &ЕстьОтборПоМенеджеру
	|						ТОГДА Менеджер = &Менеджер
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ
	|				И Подразделение = &Подразделение) КАК ВзаиморасчетыСПокупателямиОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Контрагент,
	|	КредДокумент
	|ИТОГИ
	|	СУММА(Долг)
	|ПО
	|	Контрагент,
	|	КредДокумент");
	
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.УстановитьПараметр("Менеджер", Объект.ОтборМенеджер);
	Запрос.УстановитьПараметр("Контрагент", Объект.ОтборКонтрагент);
	
	Запрос.УстановитьПараметр("ЕстьОтборПоМенеджеру", ЗначениеЗаполнено(Объект.ОтборМенеджер));
	Запрос.УстановитьПараметр("ЕстьОтборПоКонтрагенту", ЗначениеЗаполнено(Объект.ОтборКонтрагент));
	
	Если Объект.Дата - НачалоДня(Объект.Дата) = 0 Тогда
		// Документ еще не записан
		МоментВремени = КонецДня(Объект.Дата);
	Иначе
		МоментВремени = Новый Граница(Объект.Дата, ВидГраницы.Исключая)
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.Прямой);
	
	Объект.СтрокиДокумента.Очистить();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Группировка() = "КредДокумент" Тогда
			
			НоваяСтрока = Объект.СтрокиДокумента.Добавить();
		
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнить(Команда)
	
	ПараметрыЗаполнения = Новый Структура("ОтборМенеджер, ОтборКонтрагент", Объект.ОтборМенеджер, Объект.ОтборКонтрагент);
	ПараметрыЗаполнения = ОткрытьФормуМодально("Документ.ИнвентаризацияВзаиморасчетовСПокупателями.Форма.ФормаПараметрыЗаполнения", Новый Структура("ЗначениеЗаполнения", ПараметрыЗаполнения));
	
	Если ТипЗнч(ПараметрыЗаполнения) = Тип("Структура") Тогда
		
		Если ПараметрыЗаполнения.Свойство("ОтборМенеджер") Тогда
			Объект.ОтборМенеджер = ПараметрыЗаполнения.ОтборМенеджер;
		КонецЕсли;
		
		Если ПараметрыЗаполнения.Свойство("ОтборКонтрагент") Тогда
			Объект.ОтборКонтрагент = ПараметрыЗаполнения.ОтборКонтрагент;
		КонецЕсли;
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
		
	ЗаполнитьНаСервере();
	
КонецПроцедуры
