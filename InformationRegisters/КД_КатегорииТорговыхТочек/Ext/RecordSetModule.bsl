
Процедура ПередЗаписью(отказ, замещение)
	
	Если ЭтотОбъект.Количество() > 0 И Не ОбменДанными.Загрузка Тогда
		
		запрос = Новый Запрос;
		
		#Область ТекстЗапроса
		
		запрос.Текст = 
		"ВЫБРАТЬ
		|	КД_КатегоризацияТорговыхТочек.Период КАК Период,
		|	КД_КатегоризацияТорговыхТочек.Подразделение КАК Подразделение,
		|	КД_КатегоризацияТорговыхТочек.ГруппаПродукции КАК ГруппаПродукции,
		|	КД_КатегоризацияТорговыхТочек.КаналСбыта КАК КаналСбыта,
		|	КД_КатегоризацияТорговыхТочек.ТипСтандарта КАК ТипСтандарта,
		|	КД_КатегоризацияТорговыхТочек.КатегорияТТ,
		|	МАКСИМУМ(ЕСТЬNULL(КД_КатегоризацияТорговыхТочек1.КоличествоSKUПорог, 0)) КАК Порог1,
		|	КД_КатегоризацияТорговыхТочек.КоличествоSKUПорог КАК Порог2
		|ПОМЕСТИТЬ ВТ_ПериодыКатегоризации
		|ИЗ
		|	РегистрСведений.КД_КатегоризацияТорговыхТочек КАК КД_КатегоризацияТорговыхТочек
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КД_КатегоризацияТорговыхТочек КАК КД_КатегоризацияТорговыхТочек1
		|		ПО КД_КатегоризацияТорговыхТочек.Период = КД_КатегоризацияТорговыхТочек1.Период
		|			И КД_КатегоризацияТорговыхТочек.Подразделение = КД_КатегоризацияТорговыхТочек1.Подразделение
		|			И КД_КатегоризацияТорговыхТочек.ГруппаПродукции = КД_КатегоризацияТорговыхТочек1.ГруппаПродукции
		|			И КД_КатегоризацияТорговыхТочек.КаналСбыта = КД_КатегоризацияТорговыхТочек1.КаналСбыта
		|			И КД_КатегоризацияТорговыхТочек.ТипСтандарта = КД_КатегоризацияТорговыхТочек1.ТипСтандарта
		|			И КД_КатегоризацияТорговыхТочек.КоличествоSKUПорог > КД_КатегоризацияТорговыхТочек1.КоличествоSKUПорог
		|ГДЕ
		|	КД_КатегоризацияТорговыхТочек.КоличествоSKUПорог <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	КД_КатегоризацияТорговыхТочек.Период,
		|	КД_КатегоризацияТорговыхТочек.Подразделение,
		|	КД_КатегоризацияТорговыхТочек.ГруппаПродукции,
		|	КД_КатегоризацияТорговыхТочек.КаналСбыта,
		|	КД_КатегоризацияТорговыхТочек.ТипСтандарта,
		|	КД_КатегоризацияТорговыхТочек.КатегорияТТ,
		|	КД_КатегоризацияТорговыхТочек.КоличествоSKUПорог
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НаборЗаписей.Период КАК Период,
		|	НаборЗаписей.Подразделение КАК Подразделение,
		|	НаборЗаписей.ТорговаяТочка КАК ТорговаяТочка,
		|	НаборЗаписей.ГруппаПродукции КАК ГруппаПродукции,
		|	НаборЗаписей.КоличествоSKU КАК КоличествоSKU
		|ПОМЕСТИТЬ ВТ_НаборЗаписей
		|ИЗ
		|	&НаборЗаписей КАК НаборЗаписей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НаборЗаписейСПериодами.Период,
		|	НаборЗаписейСПериодами.Подразделение,
		|	НаборЗаписейСПериодами.ТорговаяТочка,
		|	НаборЗаписейСПериодами.ГруппаПродукции,
		|	НаборЗаписейСПериодами.КоличествоSKU,
		|	ВТ_ПериодыКатегоризации.КатегорияТТ
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВложенныйЗапрос.Период КАК Период,
		|		ВложенныйЗапрос.Подразделение КАК Подразделение,
		|		ВложенныйЗапрос.ТорговаяТочка КАК ТорговаяТочка,
		|		ВложенныйЗапрос.ГруппаПродукции КАК ГруппаПродукции,
		|		ВложенныйЗапрос.КоличествоSKU КАК КоличествоSKU,
		|		ВложенныйЗапрос.КаналСбыта КАК КаналСбыта,
		|		ВложенныйЗапрос.ТипСтандартаПрисутствия КАК ТипСтандартаПрисутствия,
		|		МАКСИМУМ(ВТ_ПериодыКатегоризации.Период) КАК ПериодКатегоризации
		|	ИЗ
		|		(ВЫБРАТЬ
		|			ВТ_НаборЗаписей.Период КАК Период,
		|			ВТ_НаборЗаписей.Подразделение КАК Подразделение,
		|			ВТ_НаборЗаписей.ТорговаяТочка КАК ТорговаяТочка,
		|			ВТ_НаборЗаписей.ГруппаПродукции КАК ГруппаПродукции,
		|			ВТ_НаборЗаписей.КоличествоSKU КАК КоличествоSKU,
		|			Контрагенты.КД_КаналСбыта КАК КаналСбыта,
		|			Контрагенты.КД_ТипСтандартаПрисутствия КАК ТипСтандартаПрисутствия
		|		ИЗ
		|			ВТ_НаборЗаписей КАК ВТ_НаборЗаписей
		|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|				ПО ВТ_НаборЗаписей.ТорговаяТочка = Контрагенты.Ссылка
		|					И ВТ_НаборЗаписей.ТорговаяТочка = Контрагенты.Ссылка
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ВТ_НаборЗаписей.Период,
		|			ВТ_НаборЗаписей.Подразделение,
		|			ВТ_НаборЗаписей.ТорговаяТочка,
		|			ВТ_НаборЗаписей.ГруппаПродукции,
		|			ВТ_НаборЗаписей.КоличествоSKU,
		|			Контрагенты.КД_КаналСбыта,
		|			Контрагенты.КД_ТипСтандартаПрисутствия) КАК ВложенныйЗапрос
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодыКатегоризации КАК ВТ_ПериодыКатегоризации
		|			ПО ВложенныйЗапрос.КаналСбыта = ВТ_ПериодыКатегоризации.КаналСбыта
		|				И ВложенныйЗапрос.ТипСтандартаПрисутствия = ВТ_ПериодыКатегоризации.ТипСтандарта
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ВложенныйЗапрос.Период,
		|		ВложенныйЗапрос.Подразделение,
		|		ВложенныйЗапрос.ТорговаяТочка,
		|		ВложенныйЗапрос.ГруппаПродукции,
		|		ВложенныйЗапрос.КоличествоSKU,
		|		ВложенныйЗапрос.КаналСбыта,
		|		ВложенныйЗапрос.ТипСтандартаПрисутствия) КАК НаборЗаписейСПериодами
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодыКатегоризации КАК ВТ_ПериодыКатегоризации
		|		ПО НаборЗаписейСПериодами.ПериодКатегоризации = ВТ_ПериодыКатегоризации.Период
		|			И НаборЗаписейСПериодами.Подразделение = ВТ_ПериодыКатегоризации.Подразделение
		|			И НаборЗаписейСПериодами.ГруппаПродукции = ВТ_ПериодыКатегоризации.ГруппаПродукции
		|			И НаборЗаписейСПериодами.КаналСбыта = ВТ_ПериодыКатегоризации.КаналСбыта
		|			И НаборЗаписейСПериодами.ТипСтандартаПрисутствия = ВТ_ПериодыКатегоризации.ТипСтандарта
		|			И НаборЗаписейСПериодами.КоличествоSKU > ВТ_ПериодыКатегоризации.Порог1
		|			И НаборЗаписейСПериодами.КоличествоSKU <= ВТ_ПериодыКатегоризации.Порог2";
		
		#КонецОбласти
		
		запрос.УстановитьПараметр("НаборЗаписей", ЭтотОбъект.Выгрузить(,"НомерСтроки, Период, Подразделение, ТорговаяТочка, ГруппаПродукции, КоличествоSKU"));
		
		результатЗапроса = запрос.Выполнить();
		
		массивСКатегориями = результатЗапроса.Выгрузить().ВыгрузитьКолонку("КатегорияТТ");
		
		Если ЭтотОбъект.Количество() = массивСКатегориями.Количество() И массивСКатегориями.Количество() > 0 Тогда
			Если Не массивСКатегориями[0] = null Тогда
				ЭтотОбъект.ЗагрузитьКолонку(массивСКатегориями, "КатегорияТТ");
			КонецЕсли;
		Иначе
			текстСообщения = НСтр("ru = 'Возникла ошибка при определении категорий торговых точек.'");
			ПроверкаДанныхКлиентСервер.СообщитьОбОшибке(отказ, текстСообщения, ЭтотОбъект);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
