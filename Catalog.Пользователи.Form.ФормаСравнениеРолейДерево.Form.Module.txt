
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СписокПользователей.ЗагрузитьЗначения(Параметры.СписокПользователей.ВыгрузитьЗначения());
КонецПроцедуры

&НаСервере
Процедура ПриОткрытииНаСервере()
	
	// получение дерево ролей по подсистемам
	деревоРолейВрем = ПользователиСерверПовтИсп.ДеревоРолей(Истина);	
	
	деревоРолейВрем.Строки.Сортировать("Синоним Возр", Истина);
	
	#Область РешениеПроблемыКеша
	
	массивКолонокУдалить = Новый Массив;
	
	Для Каждого колонка Из деревоРолейВрем.Колонки Цикл
		Если колонка.Имя = "ЭтоРоль" ИЛИ колонка.Имя = "Имя" ИЛИ колонка.Имя = "Синоним" Тогда
			Если колонка.Имя = "Синоним" Тогда
				колонка.Заголовок = "Роль";
			КонецЕсли;
		Иначе
			массивКолонокУдалить.Добавить(колонка);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого колонкаУдалить Из массивКолонокУдалить Цикл
		деревоРолейВрем.Колонки.Удалить(колонкаУдалить);
	КонецЦикла;
	
	#КонецОбласти
	
	// добавление колонок пользователей с признаком наличия роли
	счетчикПользователей = 0;
	
	Для Каждого пользователь Из СписокПользователей Цикл
		
		пользовательИнформацоннойБазы = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(пользователь.Значение.ИдентификаторПользователяИБ);
		
		Если пользовательИнформацоннойБазы = Неопределено Тогда
			
			Сообщить("Не найден пользователь информационной базы " + пользователь.Значение);
			
			Продолжить;
			
		КонецЕсли;
		
		счетчикПользователей = счетчикПользователей + 1;
		
		имяКолонкиПользователя = "П" + Строка(счетчикПользователей);
		
		деревоРолейВрем.Колонки.Добавить(имяКолонкиПользователя, Новый ОписаниеТипов("Булево"), Строка(пользователь.Значение));
		
		ролиПользователя = пользовательИнформацоннойБазы.Роли;
		
		Для Каждого роль Из ролиПользователя Цикл
			
			строкаДереваЗначений = деревоРолейВрем.Строки.Найти(Строка(роль), "Синоним", Истина);
			
			Если строкаДереваЗначений <> Неопределено Тогда
				строкаДереваЗначений[имяКолонкиПользователя] = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	#Область РешениеПроблемыКеша
	
	реквизитСравнениеРолейДерево = Элементы.Найти("СравнениеРолейДерево");
	
	Если реквизитСравнениеРолейДерево <> Неопределено Тогда
		
		массивУдаляемыхРеквизитов = Новый Массив;
		
		массивУдаляемыхРеквизитов.Добавить(реквизитСравнениеРолейДерево.ПутьКДанным);
		
		ИзменитьРеквизиты(, массивУдаляемыхРеквизитов);
		
	КонецЕсли;
	
	#КонецОбласти
	
	// Создание Реквизита формы типа ДанныеФормыДерево
    массивДобавляемыхРеквизитов = Новый Массив;
	
	массивДобавляемыхРеквизитов.Добавить(Новый РеквизитФормы("СравнениеРолейДерево", Новый ОписаниеТипов("ДеревоЗначений")));
	
	Для Каждого колонка Из деревоРолейВрем.Колонки Цикл
        массивДобавляемыхРеквизитов.Добавить(Новый РеквизитФормы(колонка.Имя, колонка.ТипЗначения, "СравнениеРолейДерево"));
	КонецЦикла;
	
    ИзменитьРеквизиты(массивДобавляемыхРеквизитов);

    // Преобразование объекта прикладного типа ДеревоЗначений в реквизит управляемой формы (данные формы)
    ЗначениеВРеквизитФормы(деревоРолейВрем, "СравнениеРолейДерево");
	
	#Область РешениеПроблемыКеша
	
	элементСравнениеРолейДерево = Элементы.Найти("СравнениеРолейДерево");
	
	Если элементСравнениеРолейДерево <> Неопределено Тогда
		Элементы.Удалить(элементСравнениеРолейДерево);
	КонецЕсли;
	
	#КонецОбласти
	
    // Создание элемента формы типа ТаблицаФормы для отображения дерева
    элементСравнениеРолейДерево = Элементы.Добавить("СравнениеРолейДерево", Тип("ТаблицаФормы"));
    элементСравнениеРолейДерево.ПутьКДанным = "СравнениеРолейДерево";
    элементСравнениеРолейДерево.Отображение = ОтображениеТаблицы.Дерево;
	элементСравнениеРолейДерево.НачальноеОтображениеДерева = НачальноеОтображениеДерева.РаскрыватьВсеУровни;
	элементСравнениеРолейДерево.АвтоВводНовойСтроки = Ложь;
	элементСравнениеРолейДерево.ИзменятьПорядокСтрок = Ложь;
	элементСравнениеРолейДерево.ИзменятьСоставСтрок = Ложь;
	
	Для Каждого колонка Из деревоРолейВрем.Колонки Цикл
		
		новыйЭлемент = Элементы.Добавить(колонка.Имя, Тип("ПолеФормы"), элементСравнениеРолейДерево);
		
		Если колонка.ТипЗначения = Новый ОписаниеТипов("Булево") Тогда
			новыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
		Иначе
			новыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		КонецЕсли;
		
		новыйЭлемент.ПутьКДанным = "СравнениеРолейДерево." + колонка.Имя;
		
		новыйЭлемент.Заголовок = колонка.Заголовок;
		
		Если колонка.Имя = "ЭтоРоль" ИЛИ колонка.Имя = "Имя" Тогда
			новыйЭлемент.Видимость = Ложь;
		КонецЕсли;
		
		новыйЭлемент.ТолькоПросмотр = Истина;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПриОткрытииНаСервере();
КонецПроцедуры
