
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(отказ, режимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	ОбщегоНазначенияСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	ОбщегоНазначенияСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Инициализация данных документа.
	//Документы.ИнвентаризацияИмущества.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	
	// Отражение в разделах учета
	//ОбщегоНазначенияСервер.ОтразитьДвиженияПоРегистрамНакопления(ДополнительныеСвойства, Движения, отказ);
		
	
	запрос = Новый Запрос;
	
	#Область ТекстЗапроса
	
	запрос.Текст =
	"ВЫБРАТЬ
	|	ИнвентаризацияИмуществаСостав.Ссылка.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ИнвентаризацияИмуществаСостав.Ссылка.Подразделение,
	|	ИнвентаризацияИмуществаСостав.Ссылка.ЦФО,
	|	ИнвентаризацияИмуществаСостав.Номенклатура,
	|	ИнвентаризацияИмуществаСостав.МОЛ,
	|	ВалютаУчета.Значение КАК Валюта,
	|	ИнвентаризацияИмуществаСостав.Количество,
	|	ИнвентаризацияИмуществаСостав.Сумма КАК Стоимость,
	|	ИнвентаризацияИмуществаСостав.Номенклатура.СчетУчета КАК СчетУчета,
	|	ЗНАЧЕНИЕ(ПланСчетов.Внутренний.Доходы) КАК КорСчет
	|ИЗ
	|	Документ.ИнвентаризацияИмущества.Состав КАК ИнвентаризацияИмуществаСостав,
	|	Константа.ВалютаУчета КАК ВалютаУчета
	|ГДЕ
	|	ИнвентаризацияИмуществаСостав.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИнвентаризацияИмуществаПереоценка.Ссылка.Дата,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ИнвентаризацияИмуществаПереоценка.Ссылка.Подразделение,
	|	ИнвентаризацияИмуществаПереоценка.Ссылка.ЦФО,
	|	ИнвентаризацияИмуществаПереоценка.Номенклатура,
	|	ИнвентаризацияИмуществаПереоценка.МОЛ,
	|	ВалютаУчета.Значение,
	|	0,
	|	ИнвентаризацияИмуществаПереоценка.Переоценка,
	|	ИнвентаризацияИмуществаПереоценка.Номенклатура.СчетУчета,
	|	ЗНАЧЕНИЕ(ПланСчетов.Внутренний.Доходы)
	|ИЗ
	|	Документ.ИнвентаризацияИмущества.Переоценка КАК ИнвентаризацияИмуществаПереоценка,
	|	Константа.ВалютаУчета КАК ВалютаУчета
	|ГДЕ
	|	ИнвентаризацияИмуществаПереоценка.Ссылка = &Ссылка
	|	И ИнвентаризацияИмуществаПереоценка.Переоценка <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнвентаризацияИмуществаСостав.Ссылка.Дата КАК Период,
	|	ИнвентаризацияИмуществаСостав.Ссылка.Подразделение,
	|	ИнвентаризацияИмуществаСостав.Ссылка.ЦФО КАК Объект,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиДоходов.ИнвентаризацияИмущества) КАК СтатьяДоходов,
	|	ИнвентаризацияИмуществаСостав.Сумма КАК СуммаДоходов,
	|	ЗНАЧЕНИЕ(ПланСчетов.Внутренний.Доходы) КАК СчетУчета,
	|	ИнвентаризацияИмуществаСостав.Номенклатура.СчетУчета КАК КорСчет
	|ИЗ
	|	Документ.ИнвентаризацияИмущества.Состав КАК ИнвентаризацияИмуществаСостав
	|ГДЕ
	|	ИнвентаризацияИмуществаСостав.Ссылка = &Ссылка
	|	И ИнвентаризацияИмуществаСостав.Сумма <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИнвентаризацияИмуществаПереоценка.Ссылка.Дата,
	|	ИнвентаризацияИмуществаПереоценка.Ссылка.Подразделение,
	|	ИнвентаризацияИмуществаПереоценка.Ссылка.ЦФО,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиДоходов.ПереоценкаИмущества),
	|	ИнвентаризацияИмуществаПереоценка.Переоценка,
	|	ЗНАЧЕНИЕ(ПланСчетов.Внутренний.Доходы),
	|	ИнвентаризацияИмуществаПереоценка.Номенклатура.СчетУчета
	|ИЗ
	|	Документ.ИнвентаризацияИмущества.Переоценка КАК ИнвентаризацияИмуществаПереоценка
	|ГДЕ
	|	ИнвентаризацияИмуществаПереоценка.Ссылка = &Ссылка
	|	И ИнвентаризацияИмуществаПереоценка.Переоценка <> 0";
		
	#КонецОбласти 
	
	запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	результатЗапроса = запрос.ВыполнитьПакет();
	
	Движения.Имущество.Загрузить(результатЗапроса[0].Выгрузить());
	Движения.ФинансовыйРезультат.Загрузить(результатЗапроса[1].Выгрузить());
	
	
КонецПроцедуры

Процедура ПередЗаписью(отказ, режимЗаписи, режимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Подразделение.Пустая() Тогда
		
		ТекстСообщения = НСтр("ru = 'Запись невозможна без заполнения Подразделения.'");
		ПроверкаДанныхКлиентСервер.СообщитьОбОшибке(отказ, текстСообщения, ЭтотОбъект, "Подразделение");
		
	КонецЕсли;
	
	Если Не Дата = КонецДня(Дата) Тогда
		Дата = КонецДня(Дата);
	КонецЕсли;
	
	Если режимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		запрос = Новый Запрос;
		
		#Область ТекстЗапроса
		
		запрос.Текст =
		"ВЫБРАТЬ
		|	ПереоценкаСостав.Номенклатура,
		|	ПереоценкаСостав.Количество,
		|	ПереоценкаСостав.Сумма
		|ПОМЕСТИТЬ ВТ_ПереоценкаСостав
		|ИЗ
		|	&ПереоценкаСостав КАК ПереоценкаСостав
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.МОЛ,
		|	ВложенныйЗапрос.Количество,
		|	ВложенныйЗапрос.Сумма,
		|	ВложенныйЗапрос.Стоимость,
		|	ЕСТЬNULL(ВложенныйЗапрос.Сумма, 0) - ЕСТЬNULL(ВложенныйЗапрос.Стоимость, 0) КАК Переоценка
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВТ_ПереоценкаСостав.Номенклатура КАК Номенклатура,
		|		ИмуществоОстатки.МОЛ КАК МОЛ,
		|		СУММА(ВТ_ПереоценкаСостав.Количество) КАК Количество,
		|		СУММА(ВТ_ПереоценкаСостав.Сумма) КАК Сумма,
		|		СУММА(ЕСТЬNULL(ИмуществоОстатки.СтоимостьОстаток, 0)) КАК Стоимость
		|	ИЗ
		|		ВТ_ПереоценкаСостав КАК ВТ_ПереоценкаСостав
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Имущество.Остатки(
		|					&МоментВремени,
		|					Подразделение = &Подразделение
		|						И ЦФО = &ЦФО) КАК ИмуществоОстатки
		|			ПО ВТ_ПереоценкаСостав.Номенклатура = ИмуществоОстатки.Номенклатура
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ВТ_ПереоценкаСостав.Номенклатура,
		|		ИмуществоОстатки.МОЛ) КАК ВложенныйЗапрос";
		
		#КонецОбласти
		
		Если Ссылка.Пустая() Тогда
			моментВремени = Дата;
		Иначе
			моментВремени = Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая)
		КонецЕсли;
		
		запрос.УстановитьПараметр("ПереоценкаСостав", Переоценка.Выгрузить(,"Номенклатура, Количество, Сумма"));
		запрос.УстановитьПараметр("Подразделение", Подразделение);
		запрос.УстановитьПараметр("ЦФО", ЦФО);
		запрос.УстановитьПараметр("МоментВремени", моментВремени);
		
		результатЗапроса = запрос.Выполнить();
		
		Если Не результатЗапроса.Пустой() Тогда
			Переоценка.Загрузить(результатЗапроса.Выгрузить());
		КонецЕсли;
		
	КонецЕсли;
	
	СуммаДокумента = ОбщегоНазначенияСервер.ПолучитьСуммуДокумента(ЭтотОбъект, "Состав");
	
КонецПроцедуры

Процедура ПриКопировании(объектКопирования)
	
	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеСкопированногоДокумента(ЭтотОбъект, ОбъектКопирования);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(отказ, проверяемыеРеквизиты)
	
	Если Переоценка.Количество() > 0 И Не РольДоступна("ПроведениеПереоценкиИмущества") Тогда
		
		ТекстСообщения = НСтр("ru = 'Проведение невозможно. Недостаточно прав для проведения переоценки имущества.'");
		ПроверкаДанныхКлиентСервер.СообщитьОбОшибке(отказ, текстСообщения, ЭтотОбъект, "Переоценка");

	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти