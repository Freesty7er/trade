
#Область ФормированиеПечатныхФорм

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МаршрутныйЛист") Тогда
		
		НомерСтрокиМаршрута = Неопределено;
		Если ПараметрыПечати <> Неопределено
			И ПараметрыПечати.Свойство("НомерСтрокиМаршрута", НомерСтрокиМаршрута) Тогда
		
			Если МассивОбъектов.Количество() > 1 Тогда
				ВызватьИсключение НСтр("ru = 'При выбранной строке маршрута печать возможна только для одного документа.'");
			КонецЕсли; 
		
		КонецЕсли; 
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МаршрутныйЛист", "Маршрутный лист", ПечатнаяФормаМаршрутныйЛист(МассивОбъектов, ОбъектыПечати, "МаршрутныйЛист", КоллекцияПечатныхФорм, НомерСтрокиМаршрута));
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатнаяФормаМаршрутныйЛист(МассивОбъектов, ОбъектыПечати, ИмяМакета, КоллекцияПечатныхФорм, НомерСтрокиМаршрута = Неопределено)
	
	Макет = ПолучитьМакет(ИмяМакета);
	
	Запрос = Новый Запрос;
	
	#Область ТекстЗапроса
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РИНКАЙПланМаршрутаМаршруты.Ссылка КАК Ссылка,
	|	РИНКАЙПланМаршрутаМаршруты.НомерСтроки,
	|	РИНКАЙПланМаршрутаМаршруты.МаршрутРазвоза КАК МаршрутРазвоза,
	|	РИНКАЙПланМаршрутаМаршруты.Водитель,
	|	РИНКАЙПланМаршрутаМаршруты.Пробег,
	|	РИНКАЙПланМаршрутаМаршруты.ВремяВыезда,
	|	РИНКАЙПланМаршрутаМаршруты.ВремяЗаезда,
	|	РИНКАЙПланМаршрутаМаршруты.НомерАвтомобиля
	|ПОМЕСТИТЬ Маршруты
	|ИЗ
	|	Документ.РИНКАЙПланМаршрута.Маршруты КАК РИНКАЙПланМаршрутаМаршруты
	|ГДЕ
	|	РИНКАЙПланМаршрутаМаршруты.Ссылка В(&Документы)
	|	И ВЫБОР
	|			КОГДА &НомерСтрокиМаршрута = НЕОПРЕДЕЛЕНО
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ РИНКАЙПланМаршрутаМаршруты.НомерСтроки = &НомерСтрокиМаршрута
	|		КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	МаршрутРазвоза
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Маршруты.Ссылка КАК Ссылка,
	|	Маршруты.МаршрутРазвоза КАК МаршрутРазвоза,
	|	Маршруты.Ссылка.Дата КАК Дата,
	|	Маршруты.МаршрутРазвоза.Наименование КАК Маршрут,
	|	Маршруты.Водитель.Наименование КАК Водитель,
	|	Маршруты.НомерАвтомобиля КАК НомернойЗнак,
	|	Маршруты.Пробег КАК Км,
	|	Маршруты.ВремяВыезда КАК ВремяВыезда,
	|	Маршруты.ВремяЗаезда КАК ВремяЗаезда
	|ИЗ
	|	Маршруты КАК Маршруты
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	Маршруты.НомерСтроки
	|АВТОУПОРЯДОЧИВАНИЕ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РИНКАЙПланМаршрутаОстановки.Ссылка КАК Ссылка,
	|	РИНКАЙПланМаршрутаОстановки.МаршрутРазвоза КАК МаршрутРазвоза,
	|	РИНКАЙПланМаршрутаОстановки.Заявка.Номер КАК ЗаявкаНомер,
	|	РИНКАЙПланМаршрутаОстановки.Заявка.Контрагент.Код КАК КонтрагентКод,
	|	РИНКАЙПланМаршрутаОстановки.Заявка.Контрагент.Наименование КАК КонтрагентНаименование,
	|	РИНКАЙПланМаршрутаОстановки.Заявка.Контрагент.АдресДоставкиГород КАК АдресДоставкиГород,
	|	РИНКАЙПланМаршрутаОстановки.Заявка.Контрагент.АдресДоставкиУлица КАК АдресДоставкиУлица,
	|	РИНКАЙПланМаршрутаОстановки.Заявка.Контрагент.АдресДоставкиДом КАК АдресДоставкиДом,
	|	РИНКАЙПланМаршрутаОстановки.Заявка.Контрагент.АдресДоставкиСтроение КАК АдресДоставкиСтроение,
	|	РИНКАЙПланМаршрутаОстановки.ВременноеОкноНачало КАК ОкноНачало,
	|	РИНКАЙПланМаршрутаОстановки.ВременноеОкноКонец КАК ОкноКонец,
	|	РИНКАЙПланМаршрутаОстановки.РасстояниеОтПредТочки КАК Км,
	|	РИНКАЙПланМаршрутаОстановки.ВремяПрибытия КАК Прибытие,
	|	РИНКАЙПланМаршрутаОстановки.ВремяОтъезда КАК Отправление,
	|	РИНКАЙПланМаршрутаОстановки.Вес КАК Кг,
	|	РИНКАЙПланМаршрутаОстановки.Объем КАК Объём,
	|	РИНКАЙПланМаршрутаОстановки.КомментарийЗаказа КАК Примечание
	|ИЗ
	|	Маршруты КАК Маршруты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РИНКАЙПланМаршрута.Остановки КАК РИНКАЙПланМаршрутаОстановки
	|		ПО Маршруты.Ссылка = РИНКАЙПланМаршрутаОстановки.Ссылка
	|			И Маршруты.МаршрутРазвоза = РИНКАЙПланМаршрутаОстановки.МаршрутРазвоза
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	РИНКАЙПланМаршрутаОстановки.НомерСтроки";
	
	#КонецОбласти
	
	Запрос.УстановитьПараметр("Документы", МассивОбъектов);
	Запрос.УстановитьПараметр("НомерСтрокиМаршрута", НомерСтрокиМаршрута);
	
	мРезультаты = Запрос.ВыполнитьПакет();
	ВыборкаМаршруты = мРезультаты[1].Выбрать();
	
	тзОстановки = мРезультаты[2].Выгрузить();
	тзОстановки.Индексы.Добавить("Ссылка");
	тзОстановки.Индексы.Добавить("МаршрутРазвоза");
	
	стОтборОстановок = Новый Структура("Ссылка,МаршрутРазвоза") ;
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.КлючПараметровПечати = "ПараметрыПечати_РИНКАЙПланМаршрута_МаршрутныйЛист";
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ПервыйДокумент = Истина;
	
	Пока ВыборкаМаршруты.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		Пока ВыборкаМаршруты.СледующийПоЗначениюПоля("МаршрутРазвоза") Цикл
			
			Если Не ПервыйДокумент Тогда
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			
			НомерСтрокиНачало = ДокументРезультат.ВысотаТаблицы + 1;
			
			// Область "Шапка".
			Область = Макет.ПолучитьОбласть("Шапка");
			Область.Параметры.Заполнить(ВыборкаМаршруты);
			ДокументРезультат.Вывести(Область);
			
			// Область "Строка".
			Область = Макет.ПолучитьОбласть("Строка");
			
			Область.Параметры.НомерСтроки = "-";
			Область.Параметры.ЗаявкаНомер = "-";
			Область.Параметры.Имя = "Sklad";
			Область.Параметры.Адрес = "-";
			Область.Параметры.Км = 0;
			Область.Параметры.Прибытие = "-";
			Область.Параметры.Отправление = ВыборкаМаршруты.ВремяВыезда;
			Область.Параметры.Окно = "-";
			Область.Параметры.Кг = 0;
			Область.Параметры.Объём = "-";
			Область.Параметры.Примечание = "-";
			ДокументРезультат.Вывести(Область);
			
			ЗаполнитьЗначенияСвойств(стОтборОстановок, ВыборкаМаршруты);
			тзОстановкиМаршрута = тзОстановки.Скопировать(стОтборОстановок);
			
			НомерСтроки = 0;
			
			Для каждого СтрокаОстановкиМаршрута Из тзОстановкиМаршрута Цикл
				
				НомерСтроки = НомерСтроки + 1;
				
				Область.Параметры.Заполнить(СтрокаОстановкиМаршрута);
				Область.Параметры.НомерСтроки = НомерСтроки;
				Область.Параметры.Имя = СтрШаблон("%1 / %2", СтрокаОстановкиМаршрута.КонтрагентКод, СтрокаОстановкиМаршрута.КонтрагентНаименование);
				Область.Параметры.Адрес = СтрШаблон("%1, %2, %3, %4", СтрокаОстановкиМаршрута.АдресДоставкиГород, СтрокаОстановкиМаршрута.АдресДоставкиУлица, СтрокаОстановкиМаршрута.АдресДоставкиДом, СтрокаОстановкиМаршрута.АдресДоставкиСтроение);
				Область.Параметры.Окно = СтрШаблон("%1 - %2", Формат(СтрокаОстановкиМаршрута.ОкноНачало, "ДФ=ЧЧ:мм"), Формат(СтрокаОстановкиМаршрута.ОкноКонец, "ДФ=ЧЧ:мм"));
				ДокументРезультат.Вывести(Область);
				
			КонецЦикла;
			
			Область.Параметры.НомерСтроки = "-";
			Область.Параметры.ЗаявкаНомер = "-";
			Область.Параметры.Имя = "Sklad";
			Область.Параметры.Адрес = "-";
			Область.Параметры.Км = 0;
			Область.Параметры.Прибытие = ВыборкаМаршруты.ВремяЗаезда;
			Область.Параметры.Отправление = "-";
			Область.Параметры.Окно = "-";
			Область.Параметры.Кг = 0;
			Область.Параметры.Объём = "-";
			Область.Параметры.Примечание = "-";
			ДокументРезультат.Вывести(Область);

			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ДокументРезультат, НомерСтрокиНачало, ОбъектыПечати, ВыборкаМаршруты.Ссылка);
			
		КонецЦикла; 
		
	КонецЦикла;
	
	Возврат ДокументРезультат;
	
КонецФункции // ПечатнаяФормаМаршрутныйЛист()

#КонецОбласти