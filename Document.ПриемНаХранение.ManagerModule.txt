
#Область ПроцедурыИФункцииПечатиФормы

Функция ПечатнаяФорма(массивОбъектов, объектыПечати, имяМакета, коллекцияПечатныхФорм)
	
	табличныйДокумент = Новый ТабличныйДокумент;
	табличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АктПринятияКУчету";
	
	первыйДокумент = Истина;
	
	Для Каждого текущийДокумент Из массивОбъектов Цикл
		
		Если Не первыйДокумент Тогда
			табличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		первыйДокумент = Ложь;
		
		номерСтрокиНачало = табличныйДокумент.ВысотаТаблицы + 1;
		
		Если имяМакета = "АктПринятияКУчету" Тогда
			
			запрос = Новый Запрос;
			
			#Область ТекстЗапроса
			
			запрос.Текст =
			"ВЫБРАТЬ
			|	ПриемНаХранениеСостав.Ссылка.Номер,
			|	ПриемНаХранениеСостав.Ссылка.Дата,
			|	ПриемНаХранениеСостав.Ссылка.Получатель,
			|	ПриемНаХранениеСостав.Ссылка.Отправитель,
			|	ПОДСТРОКА(ПриемНаХранениеСостав.Ссылка.Комментарий, 1, 150) КАК Комментарий,
			|	ПриемНаХранениеСостав.Ссылка.Передал,
			|	ПриемНаХранениеСостав.Ссылка.Принял,
			|	ПОДСТРОКА(ПриемНаХранениеСостав.Ярлык.Наименование, 1, 6) + "" "" + ПОДСТРОКА(ПриемНаХранениеСостав.Ярлык.Описание, 1, 150) КАК Представление,
			|	ПриемНаХранениеСостав.Ярлык.ЕдиницаХранения КАК ЕдиницаИзмерения,
			|	ПриемНаХранениеСостав.Количество,
			|	ПриемНаХранениеСостав.Ярлык.СтоимостьЕдиницыХранения КАК Цена,
			|	ВЫРАЗИТЬ(ПриемНаХранениеСостав.Ярлык.СтоимостьЕдиницыХранения * ПриемНаХранениеСостав.Количество КАК ЧИСЛО(10, 2)) КАК Сумма
			|ПОМЕСТИТЬ ВТ_ДанныеДокумента
			|ИЗ
			|	Документ.ПриемНаХранение.Состав КАК ПриемНаХранениеСостав
			|ГДЕ
			|	ПриемНаХранениеСостав.Ссылка = &ТекущийДокумент
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТ_ДанныеДокумента.Номер,
			|	ВТ_ДанныеДокумента.Дата,
			|	ВТ_ДанныеДокумента.Получатель,
			|	ВТ_ДанныеДокумента.Отправитель,
			|	ВТ_ДанныеДокумента.Комментарий,
			|	ВТ_ДанныеДокумента.Передал,
			|	ВТ_ДанныеДокумента.Принял,
			|	СУММА(ВТ_ДанныеДокумента.Количество) КАК Количество,
			|	СУММА(ВТ_ДанныеДокумента.Сумма) КАК Сумма
			|ИЗ
			|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
			|
			|СГРУППИРОВАТЬ ПО
			|	ВТ_ДанныеДокумента.Дата,
			|	ВТ_ДанныеДокумента.Принял,
			|	ВТ_ДанныеДокумента.Отправитель,
			|	ВТ_ДанныеДокумента.Передал,
			|	ВТ_ДанныеДокумента.Номер,
			|	ВТ_ДанныеДокумента.Получатель,
			|	ВТ_ДанныеДокумента.Комментарий
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТ_ДанныеДокумента.Представление,
			|	ВТ_ДанныеДокумента.ЕдиницаИзмерения,
			|	ВТ_ДанныеДокумента.Количество,
			|	ВТ_ДанныеДокумента.Цена,
			|	ВТ_ДанныеДокумента.Сумма
			|ИЗ
			|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента";
			
			#КонецОбласти
			
			запрос.УстановитьПараметр("ТекущийДокумент", текущийДокумент);
			
			результатЗапроса = запрос.ВыполнитьПакет();
			
			шапка = результатЗапроса[1].Выгрузить()[0];
			
			номерДокумента = шапка.Номер;
			
			выборкаСтрокДокумента = результатЗапроса[2].Выбрать();
			
			табличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПриемНаХранение_АктПринятияКУчету";
			
			
			макет = УправлениеПечатью.ПолучитьМакет("Документ.ПриемНаХранение.АктПринятияКУчету");
			
			
			кодЯзыкаПечать = Локализация.ПолучитьЯзыкФормированияПечатныхФорм();	
			макет.КодЯзыкаМакета = кодЯзыкаПечать;
			
			областьМакета = макет.ПолучитьОбласть("Шапка");
			
			ЗаполнитьЗначенияСвойств(областьМакета.Параметры, шапка);
			областьМакета.Параметры.Дата = Формат(шапка.Дата, "ДЛФ=DD");
			
			табличныйДокумент.Вывести(областьМакета);
			
			областьМакета = Макет.ПолучитьОбласть("Строка");

			номерСтроки = 0;
			Пока выборкаСтрокДокумента.Следующий() Цикл
				
				номерСтроки = номерСтроки + 1;
				
				областьМакета.Параметры.Заполнить(выборкаСтрокДокумента);
				областьМакета.Параметры.НомерСтроки = номерСтроки;
				
				табличныйДокумент.Вывести(областьМакета);
				
			КонецЦикла;
			
			
			областьМакета = макет.ПолучитьОбласть("Итого");

			ЗаполнитьЗначенияСвойств(областьМакета.Параметры, шапка);
				
			табличныйДокумент.Вывести(областьМакета);
			
			областьМакета = макет.ПолучитьОбласть("Подвал");
			
			ЗаполнитьЗначенияСвойств(областьМакета.Параметры, шапка);
			
			табличныйДокумент.Вывести(областьМакета);
				
			табличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
			
			
		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(табличныйДокумент, номерСтрокиНачало, объектыПечати, текущийДокумент);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатнаяФорма()

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(массивОбъектов,
				 параметрыПечати,
				 коллекцияПечатныхФорм,
				 объектыПечати,
				 параметрыВывода) Экспорт
	
	параметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктПринятияКУчету") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктПринятияКУчету", "Акт принятия к учету", ПечатнаяФорма(массивОбъектов, объектыПечати, "АктПринятияКУчету", коллекцияПечатныхФорм));
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти