
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресНастроекСКД = Параметры.АдресНастроекСКД;
	Регистратор = Параметры.Регистратор;
	ДатаДокумента = Параметры.ДатаДокумента;
	Подразделение = Параметры.Подразделение;
	
КонецПроцедуры

&НаСервере
Процедура КомпоновщикНастроекИнициализировать()
	
	компоновщикНастроек = Отчет.КомпоновщикНастроек;
	
	настройкиСКД = ПолучитьИзВременногоХранилища(АдресНастроекСКД);
	
	Если настройкиСКД = Неопределено Тогда
		
		настройкиСКД = компоновщикНастроек.ПолучитьНастройки();
		
		Для Каждого элементОтбор ИЗ настройкиСКД.Отбор.Элементы Цикл
			Если Строка(элементОтбор.ЛевоеЗначение) = "Подразделение" Тогда
				элементОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
				элементОтбор.ПравоеЗначение = Подразделение;
				элементОтбор.Использование = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	параметрыРегистратор = настройкиСКД.ПараметрыДанных.Элементы.Найти("Регистратор");
	Если параметрыРегистратор = Неопределено Тогда
		параметрыРегистратор = настройкиСКД.ПараметрыДанных.Элементы.Добавить();
		параметрыРегистратор.Параметр = Новый ПараметрКомпоновкиДанных("Регистратор");
		параметрыРегистратор.Значение = Регистратор;
		параметрыРегистратор.Использование = Истина;
	Иначе
		параметрыРегистратор.Значение = Регистратор;
		параметрыРегистратор.Использование = Истина;
	КонецЕсли;
	
	параметрыДатаДокумента = настройкиСКД.ПараметрыДанных.Элементы.Найти("ДатаДокумента");
	Если параметрыДатаДокумента = Неопределено Тогда
		параметрыДатаДокумента = настройкиСКД.ПараметрыДанных.Элементы.Добавить();
		параметрыДатаДокумента.Параметр = Новый ПараметрКомпоновкиДанных("ДатаДокумента");
		параметрыДатаДокумента.Значение = ДатаДокумента;
		параметрыДатаДокумента.Использование = Истина;
	Иначе
		Если параметрыДатаДокумента.Значение = Дата("00010101000000") Тогда
			параметрыДатаДокумента.Значение = ДатаДокумента;
			параметрыДатаДокумента.Использование = Истина;
		КонецЕсли;
	КонецЕсли;
	
	компоновщикНастроек.ЗагрузитьНастройки(настройкиСКД);
	компоновщикНастроек.Восстановить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	КомпоновщикНастроекИнициализировать();
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыполнить(Команда)
	Оповестить("ИмуществоЗаполнитьПоОстаткам", Новый Структура("АдресНастроекСКД", ПоместитьВоВременноеХранилище(Отчет.КомпоновщикНастроек.Настройки, ЭтаФорма.УникальныйИдентификатор)));
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры
