
#Область НастройкиОтчётов

// Удаляет настроки отчёта из информационной базы.
//
// Параметры
//  мНастройкиОтчёта  - Массив - Набор значений типа СправочникСсылка.НастройкиОтчётов, которые должны
//                 быть удалены.
//
Процедура УдалитьНастройкиОтчёта(мНастройкиОтчёта) Экспорт
	ХранилищаНастроек.НастройкиОтчётов.УдалитьНастройкиОтчёта(мНастройкиОтчёта);
КонецПроцедуры // УдалитьНастройкиОтчёта()

#КонецОбласти

//#Область ВариантыОтчётов

//// Удаляет варианты отчёта из информационной базы.
////
//// Параметры
////  мВариантыОтчёта  - Массив - Набор значений типа СправочникСсылка.ВариантыОтчётов, которые должны
////                 быть удалены.
////
//Процедура УдалитьВариантыОтчёта(мВариантыОтчёта) Экспорт
//	ХранилищаНастроек.ВариантыОтчётов.УдалитьВариантыОтчёта(мВариантыОтчёта);
//КонецПроцедуры // УдалитьВариантыОтчёта()

//#КонецОбласти

#Область ДополнительныеПроцедурыИФункции

// Возвращает отформатированное представление даты по определённому типу периода.
//
// Параметры
//  ЗначениеПериода  - Дата - Форматируемая дата.
//  ТипПериода  - Строка - Строка, содержащая одно из значений: Секунда, Минута, Час,
//                 День, Неделя, Декада, Месяц, Квартал, Полугодие, Год.
//
// Возвращаемое значение:
//   Строка   - Форматированное представление даты.
//
Функция ПредставлениеПолейПериодов(ЗначениеПериода, ТипПериода) Экспорт

	ПредставлениеПоляПериода = "";
	
	Если ТипПериода = "Секунда" Тогда
	
		ПредставлениеПоляПериода = Формат(ЗначениеПериода, "ДФ='д ММММ гггг ""г."" ЧЧ:мм:сс'");
	
	ИначеЕсли ТипПериода = "Минута" Тогда
	
		ПредставлениеПоляПериода = Формат(ЗначениеПериода, "ДФ='д ММММ гггг ""г."" ЧЧ:мм'");
	
	ИначеЕсли ТипПериода = "Час" Тогда
	
		ПредставлениеПоляПериода = Формат(ЗначениеПериода, "ДФ='д ММММ гггг ""г."" ЧЧ ""ч""'");
	
	ИначеЕсли ТипПериода = "День" Тогда
	
		ПредставлениеПоляПериода = Формат(ЗначениеПериода, "ДЛФ=DD");
	
	ИначеЕсли ТипПериода = "Неделя" Тогда
		
		ПредставлениеПоляПериода = Формат(ЗначениеПериода, "ДЛФ=DD") + " - " + Формат(КонецНедели(ЗначениеПериода), "ДЛФ=DD");
	
	ИначеЕсли ТипПериода = "Декада" Тогда
		
		Если День(ЗначениеПериода) < 11 Тогда
			НомерДекады = "1";
		ИначеЕсли День(ЗначениеПериода) < 21 Тогда
			НомерДекады = "2";
		Иначе
			НомерДекады = "3";
		КонецЕсли; 
		ПредставлениеПоляПериода = НомерДекады + " декада " + Формат(ЗначениеПериода, "ДФ='ММММ гггг ""г.""'");
	
	ИначеЕсли ТипПериода = "Месяц" Тогда
	
		ПредставлениеПоляПериода = Формат(ЗначениеПериода, "ДФ='ММММ гггг ""г.""'");
	
	ИначеЕсли ТипПериода = "Квартал" Тогда
	
		ПредставлениеПоляПериода = Формат(ЗначениеПериода, "ДФ='к ""квартал"" гггг ""г.""'");
	
	ИначеЕсли ТипПериода = "Полугодие" Тогда
		
		Если Месяц(ЗначениеПериода) < 7 Тогда
			НомерПолугодия = "1";
		Иначе
			НомерПолугодия = "2";
		КонецЕсли; 
		ПредставлениеПоляПериода = НомерПолугодия + " полугодие " + Формат(ЗначениеПериода, "ДФ='гггг ""г.""'");
	
	ИначеЕсли ТипПериода = "Год" Тогда
	
		ПредставлениеПоляПериода = Формат(ЗначениеПериода, "ДФ='гггг ""г.""'");
		
	ИначеЕсли ТипПериода = "ДеньНедели" Тогда
		
		ПредставлениеПоляПериода = Формат(Дата(2006, 5, ЗначениеПериода), "ДФ="" ддд """);
		
	Иначе
	
		ПредставлениеПоляПериода = Строка(ЗначениеПериода);
	
	КонецЕсли; 
	
	Возврат ПредставлениеПоляПериода;

КонецФункции // ПредставлениеПолейПериодов()

#КонецОбласти
