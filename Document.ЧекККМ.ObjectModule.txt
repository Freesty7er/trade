
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(отказ, режимПроведения)
	
	Движения.Продажи.Записывать 			= Истина;
	Движения.ТоварыВРознице.Записывать 		= Истина;
	Движения.ДенежныеСредстваККМ.Записывать = Истина;
	Движения.ФинансовыйРезультат.Записывать = Истина;
	
	запрос = Новый Запрос;
	запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	#Область ТекстЗапроса
	// 0 - таблица товаров
	// 1 - движения.Продажи
	// 2 - движения.ТоварыВРознице
	// 3 - движения.ДенежныеСредстваККМ
	// 4 - движения.ФинансовыйРезультат
	
	запрос.Текст =
	"ВЫБРАТЬ
	|	ЧекККМТовары.Ссылка,
	|	ЧекККМТовары.Ссылка.Дата,
	|	ЧекККМТовары.Ссылка.Подразделение,
	|	ЧекККМТовары.Номенклатура,
	|	ЧекККМТовары.Ссылка.Р_ТорговаяТочка.КонтрагентЧастноеЛицо КАК Контрагент,
	|	ЧекККМТовары.Ссылка.Р_ПОС.ОтделВМагазине КАК ОтделВМагазине,
	|	ЧекККМТовары.Количество,
	|	ЧекККМТовары.Сумма,
	|	ЧекККМТовары.Ссылка.Р_ТорговаяТочка.ТипЦеныПродажи КАК ТипЦены,
	|	ЧекККМТовары.Ссылка.ВидОперации,
	|	ЧекККМТовары.ПроцентСкидкиНаценки,
	|	ЦеныНоменклатурыЗакупка.Цена * ЧекККМТовары.Количество КАК Себестоимость,
	|	ЧекККМТовары.ПроцентАвтоматическихСкидок,
	|	ВЫБОР
	|		КОГДА ЧекККМТовары.ПроцентАвтоматическихСкидок = 0
	|			ТОГДА 0
	|		ИНАЧЕ ЧекККМТовары.Цена * ЧекККМТовары.Количество - ЧекККМТовары.Сумма
	|	КОНЕЦ КАК СуммаСкидки,
	|	РазделительУчётаПоУмолчанию.Значение КАК РазделительУчёта
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	Документ.ЧекККМ.Товары КАК ЧекККМТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ДатаСреза, ) КАК ЦеныНоменклатурыЗакупка
	|		ПО ЧекККМТовары.Ссылка.Подразделение = ЦеныНоменклатурыЗакупка.Подразделение
	|			И ЧекККМТовары.Номенклатура = ЦеныНоменклатурыЗакупка.Номенклатура
	|			И (ЦеныНоменклатурыЗакупка.ТипЦен = &ТипЦеныЗакупки),
	|	Константа.РазделительУчётаПоУмолчанию КАК РазделительУчётаПоУмолчанию
	|ГДЕ
	|	ЧекККМТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Дата КАК Период,
	|	ТаблицаТоваров.Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.Менеджеры.БезМенеджера) КАК Менеджер,
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Контрагент,
	|	ТаблицаТоваров.ОтделВМагазине КАК ЦФО,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)
	|			ТОГДА 1
	|		ИНАЧЕ -1
	|	КОНЕЦ * ТаблицаТоваров.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)
	|			ТОГДА 1
	|		ИНАЧЕ -1
	|	КОНЕЦ * ТаблицаТоваров.Сумма КАК ПродСтоимость,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)
	|			ТОГДА 1
	|		ИНАЧЕ -1
	|	КОНЕЦ * (ТаблицаТоваров.Сумма - ЦеныНоменклатурыСрезПоследних.Цена * ТаблицаТоваров.Количество) КАК Доход,
	|	ТаблицаТоваров.ТипЦены,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЭтоВозврат,
	|	ТаблицаТоваров.ПроцентАвтоматическихСкидок КАК ПроцентСкидки,
	|	ТаблицаТоваров.СуммаСкидки
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|				&ДатаСреза,
	|				Подразделение = &Подразделение
	|					И ТипЦен = &ТипЦеныЗакупки) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО ТаблицаТоваров.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаТоваров.Подразделение,
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.ОтделВМагазине КАК ОтделМагазина,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)
	|			ТОГДА 1
	|		ИНАЧЕ -1
	|	КОНЕЦ * ТаблицаТоваров.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)
	|			ТОГДА 1
	|		ИНАЧЕ -1
	|	КОНЕЦ * ТаблицаТоваров.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)
	|			ТОГДА 1
	|		ИНАЧЕ -1
	|	КОНЕЦ * (ТаблицаТоваров.Сумма - ТаблицаТоваров.Себестоимость) КАК Наценка
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЧекККМОплата.Ссылка.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ЧекККМОплата.Ссылка.Подразделение,
	|	ЧекККМОплата.Ссылка.Р_ПОС.ОтделВМагазине КАК ЦФО,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.НациональнаяВалюта) КАК Валюта,
	|	ВЫБОР
	|		КОГДА ЧекККМОплата.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)
	|			ТОГДА 1
	|		ИНАЧЕ -1
	|	КОНЕЦ * ЧекККМОплата.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ЧекККМОплата.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)
	|			ТОГДА 1
	|		ИНАЧЕ -1
	|	КОНЕЦ * ЧекККМОплата.Сумма КАК СуммаВал
	|ИЗ
	|	Документ.ЧекККМ.Оплата КАК ЧекККМОплата
	|ГДЕ
	|	ЧекККМОплата.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Дата КАК Период,
	|	ТаблицаТоваров.Подразделение,
	|	ТаблицаТоваров.ОтделВМагазине КАК Объект,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиДоходов.МаржинальныйДоход) КАК СтатьяДоходов,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)
	|				ТОГДА 1
	|			ИНАЧЕ -1
	|		КОНЕЦ * (ТаблицаТоваров.Сумма - ТаблицаТоваров.Себестоимость)) КАК СуммаДоходов,
	|	ТаблицаТоваров.РазделительУчёта
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.ОтделВМагазине,
	|	ТаблицаТоваров.Дата,
	|	ТаблицаТоваров.Подразделение,
	|	ТаблицаТоваров.РазделительУчёта";
	
	#КонецОбласти
	
	запрос.УстановитьПараметр("Ссылка", 		Ссылка);
	запрос.УстановитьПараметр("ДатаСреза", 		Дата);
	запрос.УстановитьПараметр("Подразделение", 	Подразделение);
	запрос.УстановитьПараметр("ТипЦеныЗакупки", Подразделение.ЦенаСклада);
	
	результатЗапроса = запрос.ВыполнитьПакет();
	
	Движения.Продажи.Загрузить(результатЗапроса[1].Выгрузить());
	Движения.ТоварыВРознице.Загрузить(результатЗапроса[2].Выгрузить());
	Движения.ДенежныеСредстваККМ.Загрузить(результатЗапроса[3].Выгрузить());
	
	// ПЕРЕДЕЛАТЬ!!! На соединение с регистром сведений "ПараметрыУчетаПоПодразделению" в запросе
	параметрыУчетаПоПодразделению = РегистрыСведений.ПараметрыУчетаПоПодразделениям.ПолучитьПараметрыУчетаПоПодразделению(Дата, Подразделение, Новый Структура("СписаниеНаценкиПоИнкассации"));
	Если Не параметрыУчетаПоПодразделению.СписаниеНаценкиПоИнкассации Тогда
		Движения.ФинансовыйРезультат.Загрузить(результатЗапроса[4].Выгрузить());
	Иначе
		Движения.ФинансовыйРезультат.Очистить();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(объектКопирования)
	
	ЗаполнениеОбъектовСервер.ЗаполнитьДанныеСкопированногоДокумента(ЭтотОбъект, объектКопирования);
	
КонецПроцедуры

#КонецОбласти 